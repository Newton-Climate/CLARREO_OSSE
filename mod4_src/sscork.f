      SUBROUTINE SSCORK(IPH,IK,MSOFF,IPATH,KNTRVL,                      &
     &  V,S0,PFMOL0,PFMOL2,TSNOBS,SUMSSS)

!     SSCORK PERFORMS THE SINGLE SCATTERING SOLAR/LUNAR RADIANCE LAYER
!     SUM WHEN THE CORRELATED-K APPROACH IS USED AND STORES TRANSMITTED
!     SOLAR/LUNAR IRRADIANCES FOR MULTIPLE SCATTERING CALCULATIONS.

!     INPUT ARGUMENTS:
!       IPH     SCATTERING PHASE FUNCTION SWITCH.
!               =2 FOR LOWTRAN MIE FUNCTIONS
!       IK      LAYER INDEX.
!       MSOFF   LAYER INDEX OFFSET (>0 FOR MULTIPLE SCATTERING PATH).
!       IPATH   PATH TYPE SWITCH.
!               =1 FOR DIRECT SUN TO OBSERVER PATH ONLY
!               =2 FOR SUN TO SCATTERING POINT PATH
!               =3 FOR OBSERVER TO SCATTERING POINT LINE-OF-SIGHT PATH
!       KNTRVL  NUMBER OF CORRELATED K'S PER SPECTRAL INTERVAL.
!       V       SPECTRAL FREQUENCY [CM-1].
!       S0      EXTRA-TERRESTRIAL SOURCE FUNCTION [W/(CM2-MICRON)].
!       PFMOL0  MOLECULAR PHASE FUNCTION ISOTROPIC SCATTERING TERM.
!       PFMOL2  MOLECULAR PHASE FUNCTION ANISOTROPIC SCATTERING TERM.
      INTEGER IPH,IK,MSOFF,IPATH,KNTRVL
      REAL V,S0,PFMOL0,PFMOL2

!     OUTPUT ARGUMENTS:
!       TSNOBS   SOLAR/LUNAR IRRADIANCE AT THE OBSERVER.
!       SUMSSS   SOLAR/LUNAR SINGLE SCATTERING RADIANCE SUM.
      REAL TSNOBS,SUMSSS
      INCLUDE 'PARAMS.h'

!     COMMONS:
      INCLUDE 'BASE.h'
      INCLUDE 'SOLS.h'
      INTEGER NCRALT,NCRSPC
      REAL CTHIK,CALT,CEXT,CWAVLN,CCOLWD,CCOLIP,CHUMID,ASYMWD,ASYMIP
      COMMON/CARD2A/CTHIK,CALT,CEXT,NCRALT,NCRSPC,                      &
     &  CWAVLN,CCOLWD,CCOLIP,CHUMID,ASYMWD,ASYMIP

!     /AER/
!     THERE ARE "MAER=17" COMPONENTS:
!      1     AEROSOL 1 (NOMINALLY, BOUNDARY LAYER AEROSOL).
!      2     AEROSOL 2 (NOMINALLY, TROPOSPHERIC AEROSOL).
!      3     AEROSOL 3 (NOMINALLY, STRATOSPHERIC AEROSOL).
!      4     AEROSOL 4 (NOMINALLY, VOLCANIC AEROSOL).
!      5     CIRRUS CLOUD.
!      6     CLOUD 1 (NOMINALLY, WATER CLOUD).
!      7     CLOUD 2 (NOMINALLY, ICE CLOUD).
!     8-17   NOVAM (NAVY OCEANIC VERTICAL AEROSOL MODEL) AEROSOL LAYERS.

!       NAER     NUMBER OF ACTIVE AEROSOLS.
!       EXTV     SPECTRAL EXTINCTION (NORMALIZED TO 1 AT 550 NM).
!       ABSV     SPECTRAL ABSORPTION (1-ABSV/EXTV=SCATTERING ALBEDO).
!       ASYV     SPECTRAL LEGENDRE MOMENT (DIVIDED BY 2N+1).
      INTEGER NAER
      REAL EXTV,ABSV,ASYV
      COMMON/AER/NAER,EXTV(MAER),ABSV(MAER),ASYV(MXCMU,MAER)

!     /MSRD/
!       CSSCAT   COSINE OF THE SCATTERING ANGLE.
!                (AT H1 IF IMULT=1; AT OR "NEAR" H2 IF IMULT=-1)
!       SLEGEN   Nth LEGENDRE POLYNOMIAL EVALUATED AT THE COSINE OF THE
!                SCATTERING ANGLE TIMES (2N+1)/4pi (N=0 TO NSTR-1).
!       CSZEN0   LAYER BOUNDARY COSINE OF SOLAR/LUNAR ZENITH.
!       CSZEN    LAYER AVERAGE COSINE OF SOLAR/LUNAR ZENITH.
!       CSZENX   AVERAGE SOLAR/LUNAR COSINE ZENITH EXITING
!                (AWAY FROM EARTH) THE CURRENT LAYER.
!       BBGRND   THERMAL EMISSION (FLUX) AT THE GROUND [W CM-2 / CM-1].
!       BBNDRY   LAYER BOUNDARY THERMAL EMISSION (FLUX) [W CM-2 / CM-1].
!       TCONT    LAYER CONTINUUM OPTICAL DEPTH.
!       TAUT     LAYER TOTAL OPTICAL DEPTH.
!       GTSCAT   SUM OVER SCATTERING SOURCES OF SCATTERING OPTICAL DEPTH
!                AND PHASE FUNCTION LEGENDRE COEFFICIENT PRODUCTS.
!       COSBAR   LAYER EFFECTIVE SCATTERING ASYMMETRY FACTOR.
!       DEPRAT   FRACTIONAL DECREASE IN WEAK-LINE OPTICAL DEPTH TO SUN.
!       S0DEP    OPTICAL DEPTH FROM LAYER BOUNDARY TO SUN.
!       S0TRN    TRANSMITTED SOLAR IRRADIANCES [W CM-2 / CM-1]
!       UPF      LAYER BOUNDARY UPWARD THERMAL FLUX [W CM-2 / CM-1].
!       DNF      LAYER BOUNDARY DOWNWARD THERMAL FLUX [W CM-2 / CM-1].
!       UPFS     LAYER BOUNDARY UPWARD SOLAR FLUX [W CM-2 / CM-1].
!       DNFS     LAYER BOUNDARY DOWNWARD SOLAR FLUX [W CM-2 / CM-1].
      REAL CSSCAT,SLEGEN,CSZEN0,CSZEN,CSZENX,TCONT,TAUT,GTSCAT,COSBAR,  &
     &  BBGRND,BBNDRY,S0DEP,S0TRN,DEPRAT,UPF,DNF,UPFS,DNFS
      COMMON/MSRD/CSSCAT,SLEGEN(0:MAZ),                                 &
     &  CSZEN0(LAYDIM),CSZEN(LAYDIM),CSZENX(LAYDIM),TCONT(LAYDIM),      &
     &  TAUT(MXKSUB,LAYDIM),GTSCAT(0:MXCMU,1:LAYDIM),COSBAR(LAYDIM),    &
     &  BBGRND,BBNDRY(LAYDIM),S0DEP(MXKSUB,LAYTWO),S0TRN(MXKSUB,LAYTWO),&
     &  DEPRAT(MXKSUB,LAYDIM),UPF(MXKSUB,LAYDIM),DNF(MXKSUB,LAYDIM),    &
     &  UPFS(MXKSUB,LAYDIM),DNFS(MXKSUB,LAYDIM)

!     COMMON /CORKDT/
!       WTKSUB   SPECTRAL BIN SUB-INTERVAL FRACTIONAL WIDTHS.
!       DEPLAY   INCREMENTAL EXTINCTION OPTICAL DEPTHS
!       TRNLAY   INCREMENTAL TRANSMITTANCES
!       TRNCUM   CUMULATIVE TRANSMITTANCES
!       K2TAIL   POINTER FROM K BIN TO LINE TAIL SUB-BIN
!                (=0 IF MULTIPLE LINE TAIL SUB-BINS CONTRIBUTE).
!       CONTWT   WEIGHTS FOR PARTITIONING LINE TAILS INTO K'S
!                (ONLY USED IF K2TAIL IS 0).
      REAL WTKSUB,DEPLAY,TRNLAY,TRNCUM,CONTWT
      INTEGER K2TAIL
      COMMON/CORKDT/WTKSUB(MXKSUB),DEPLAY(MXKSUB),TRNLAY(MXKSUB),       &
     &  TRNCUM(MXKSUB),K2TAIL(MXKSUB),CONTWT(MTLSUB,MXKSUB)

!     /USRDTA/
      INTEGER NAERF,NWLF
      REAL WLF,F
      COMMON/USRDTA/NAERF,NWLF,WLF(MWLF),F(MAERF,MANGLS,MWLF)

!     FUNCTIONS:
      REAL PHASEF,HENGNS

!     LOCAL VARIABLES:
      INTEGER INTRVL,IKP1,LLM1,LL,IVM1,IV
      REAL PHM1,PH,FAC,ALAM,STRANL,DELDEP,PNOV,STRNK

!     SAVE CORRELATED-K COMMON:
      SAVE /CORKDT/

!     SAVED PHASE FUNCTION VARIABLES:
      REAL PMOLB,PMOLF,PAERB(4),PAERF(4),                               &
     &  PCIRB,PCIRF,PCLDB,PCLDF,PICEB,PICEF,PRAINB,PRAINF
      SAVE PMOLB,PMOLF,PAERB,PAERF,                                     &
     &  PCIRB,PCIRF,PCLDB,PCLDF,PICEB,PICEF,PRAINB,PRAINF

!     BRANCH TO PATH TYPE
      GOTO(10,30,50),IPATH

!     IPATH=1 (DIRECT SUN TO OBSERVER PATH).
   10 CONTINUE

!     STORE SUN/MOON TO OBSERVER OPTICAL
!     DEPTHS AND TRANSMITTED IRRADIANCES.
      TSNOBS=0.
      DO 20 INTRVL=1,KNTRVL
          S0DEP(INTRVL,1)=DEPLAY(INTRVL)
          S0TRN(INTRVL,1)=S0*TRNLAY(INTRVL)
          TSNOBS=TSNOBS+WTKSUB(INTRVL)*S0TRN(INTRVL,1)
   20 CONTINUE

!     FOR MULTIPLE SCATTERING PATH, RETURN AFTER
!     DEFINING TRANSMITTED SOLAR/LUNAR IRRADIANCES.
      IF(MSOFF.GT.0)RETURN

!     INITIALIZE THE SOLAR/LUNAR SINGLE SCATTERING RADIANCE LAYER SUM.
      SUMSSS=0.

!     STORE RAYLEIGH (PMOLB), FOUR AEROSOL (PAERB), CIRRUS (PCIRB),
!     CLOUD WATER DROPLET (PCLDB) AND CLOUD ICE PARTICLE (PICEB)
!     PHASE FUNCTIONS AT THE OBSERVER.
      PMOLB=PFMOL0+PFMOL2*PHSCOS(1)**2
      IF(IPH.EQ.0)THEN

!        HENYEY-GREENSTEIN
         PAERB(1)=PHASFN(1,2)
         PAERB(2)=PAERB(1)
         PAERB(3)=PAERB(1)
         PAERB(4)=PAERB(1)
         PCIRB=PAERB(1)
      ELSEIF(IPH.EQ.1)THEN

!        USER-SUPPLIED F-FUNCTION (SEE COMMON BLOCK USRDTA)
!        DETERMINE ANGLE INDICES
         LLM1=1
         DO 100 LL=2,NANGLS-1
            IF(ANGF(LL).GT.PHSANG(1))GOTO110
            LLM1=LL
 100     CONTINUE
         LL=NANGLS
 110     CONTINUE

!        DETERMINE WAVELENGTH INDICES
         IVM1=1
         ALAM=1.E4/(V+1.E-5)
         DO 120 IV=2, NWLF
            IF(WLF(IV).GT.ALAM) GO TO 130
            IVM1=IV
 120     CONTINUE
         IV=NWLF
 130     CONTINUE
         IF(IVM1.EQ.IV)THEN
            FAC=0
         ELSE
            FAC=(ALAM-WLF(IV))/(WLF(IVM1)-WLF(IV))
         ENDIF
         CALL INTERP(2,PHSANG(1),ANGF(LLM1),ANGF(LL),                   &
     &        PHM1,F(1,LLM1,IVM1),F(1,LL,IVM1))
         CALL INTERP(2,PHSANG(1),ANGF(LLM1),ANGF(LL),                   &
     &        PH,F(1,LLM1,IV),F(1,LL,IV))
         PAERB(1)=PH+(PHM1-PH)*FAC
         CALL INTERP(2,PHSANG(1),ANGF(LLM1),ANGF(LL),                   &
     &        PHM1,F(2,LLM1,IVM1),F(2,LL,IVM1))
         CALL INTERP(2,PHSANG(1),ANGF(LLM1),ANGF(LL),                   &
     &        PH,F(2,LLM1,IV),F(2,LL,IV))
         PAERB(2)=PH+(PHM1-PH)*FAC
         CALL INTERP(2,PHSANG(1),ANGF(LLM1),ANGF(LL),                   &
     &        PHM1,F(3,LLM1,IVM1),F(3,LL,IVM1))
         CALL INTERP(2,PHSANG(1),ANGF(LLM1),ANGF(LL),                   &
     &        PH,F(3,LLM1,IV),F(3,LL,IV))
         PAERB(3)=PH+(PHM1-PH)*FAC
         CALL INTERP(2,PHSANG(1),ANGF(LLM1),ANGF(LL),                   &
     &        PHM1,F(4,LLM1,IVM1),F(4,LL,IVM1))
         CALL INTERP(2,PHSANG(1),ANGF(LLM1),ANGF(LL),                   &
     &        PH,F(4,LLM1,IV),F(4,LL,IV))
         PAERB(4)=PH+(PHM1-PH)*FAC
         PCIRB=HENGNS(ASYV(1,5),PHSCOS(1))
      ELSEIF(IPH.EQ.2)THEN

!        MIE DATA-BASE
         PAERB(1)=PHASEF(1,V,AH1(1),PHSANG(1),ARH(1))
         PAERB(2)=PHASEF(2,V,AH1(1),PHSANG(1),ARH(1))
         PAERB(3)=PHASEF(3,V,AH1(1),PHSANG(1),ARH(1))
         PAERB(4)=PHASEF(4,V,AH1(1),PHSANG(1),ARH(1))
         PCIRB=HENGNS(ASYV(1,5),PHSCOS(1))
      ENDIF
      IF(NCRSPC.EQ.1)THEN

!         ANGULAR INTERPOLATE TABULATED SCATTERING PHASE FUNCTIONS:
          CALL GETPF(PHSCOS(1),PCLDB,PICEB)
          IF(ABS(ASYMWD).GE.1.)THEN

!             SPECTRALLY VARYING WATER DROPLET SCATTERING PHASE FUNCTION
              PCLDB=HENGNS(ASYV(1,6),PHSCOS(1))
          ELSE

!             SPECTRALLY INDEPENDENT WATER DROPLET PHASE FUNCTION
              PCLDB=PHASFN(1,3)
          ENDIF
          IF(ABS(ASYMIP).GE.1.)THEN

!             SPECTRALLY VARYING ICE PARTICLE SCATTERING PHASE FUNCTION
              PICEB=HENGNS(ASYV(1,7),PHSCOS(1))
          ELSE

!             SPECTRALLY INDEPENDENT ICE PARTICLE PHASE FUNCTION
              PICEB=PHASFN(1,4)
          ENDIF
      ENDIF
      PRAINB=HENGNS(TX(79),PHSCOS(1))
      RETURN

!     IPATH=2 (SUN TO SCATTERING POINT PATH).
!     TRANSLATE PREVIOUS SOLAR/LUNAR OPTICAL DEPTHS TO
!     FRONT AND STORE CURRENT VALUES IN BACK.  CALCULATE
!     SOLAR/LUNAR IRRADIANCE TRANSMITTED TO SCATTERING POINT.
   30 CONTINUE
      IKP1=IK+1
      DO 40 INTRVL=1,KNTRVL
          S0DEP(INTRVL,IKP1)=DEPLAY(INTRVL)
          S0TRN(INTRVL,IKP1)=S0*TRNLAY(INTRVL)
   40 CONTINUE

!     FOR MULTIPLE SCATTERING PATH, RETURN AFTER
!     DEFINING TRANSMITTED SOLAR/LUNAR IRRADIANCES.
      IF(MSOFF.GT.0)RETURN

!     STORE RAYLEIGH (PMOLB), FOUR AEROSOL (PAERB), CIRRUS (PCIRB),
!     CLOUD WATER DROPLET (PCLDB) AND CLOUD ICE PARTICLE (PICEB)
!     PHASE FUNCTIONS AT THE CURRENT (FRONT-SIDE) AND PREVIOUS
!     (BACK-SIDE) SCATTERING POINT.
      PMOLF=PMOLB
      PAERF(1)=PAERB(1)
      PAERF(2)=PAERB(2)
      PAERF(3)=PAERB(3)
      PAERF(4)=PAERB(4)
      PCIRF=PCIRB
      PCLDF=PCLDB
      PICEF=PICEB
      PRAINF=PRAINB
      PMOLB=PFMOL0+PFMOL2*PHSCOS(IKP1)**2
      IF(IPH.EQ.0)THEN
         PAERB(1)=PHASFN(IKP1,2)
         PAERB(2)=PAERB(1)
         PAERB(3)=PAERB(1)
         PAERB(4)=PAERB(1)
         PCIRB=PAERB(1)
      ELSEIF(IPH.EQ.1)THEN

!        USER-SUPPLIED F-FUNCTION (SEE COMMON BLOCK USRDTA)
!        DETERMINE ANGLE INDICES
         LLM1=1
         DO 140 LL=2,NANGLS-1
            IF(ANGF(LL).GT.PHSANG(IKP1))GOTO150
            LLM1=LL
 140     CONTINUE
         LL=NANGLS
 150     CONTINUE

!        WAVELENGTH INDICES HAVE ALREADY BEEN DETERMINED ABOVE
!        DETERMINE WAVELENGTH INDICES
         IVM1=1
         ALAM=1.E4/(V+1.E-5)
         DO 125 IV=2, NWLF
            IF(WLF(IV).GT.ALAM) GO TO 135
            IVM1=IV
 125     CONTINUE
         IV=NWLF
 135     CONTINUE
         IF(IVM1.EQ.IV)THEN
            FAC=0
         ELSE
            FAC=(ALAM-WLF(IV))/(WLF(IVM1)-WLF(IV))
         ENDIF
         CALL INTERP(2,PHSANG(IKP1),ANGF(LLM1),ANGF(LL),                &
     &        PHM1,F(1,LLM1,IVM1),F(1,LL,IVM1))
         CALL INTERP(2,PHSANG(IKP1),ANGF(LLM1),ANGF(LL),                &
     &        PH,F(1,LLM1,IV),F(1,LL,IV))
         PAERB(1)=PH+(PHM1-PH)*FAC
         CALL INTERP(2,PHSANG(IKP1),ANGF(LLM1),ANGF(LL),                &
     &        PHM1,F(2,LLM1,IVM1),F(2,LL,IVM1))
         CALL INTERP(2,PHSANG(IKP1),ANGF(LLM1),ANGF(LL),                &
     &        PH,F(2,LLM1,IV),F(2,LL,IV))
         PAERB(2)=PH+(PHM1-PH)*FAC
         CALL INTERP(2,PHSANG(IKP1),ANGF(LLM1),ANGF(LL),                &
     &        PHM1,F(3,LLM1,IVM1),F(3,LL,IVM1))
         CALL INTERP(2,PHSANG(IKP1),ANGF(LLM1),ANGF(LL),                &
     &        PH,F(3,LLM1,IV),F(3,LL,IV))
         PAERB(3)=PH+(PHM1-PH)*FAC
         CALL INTERP(2,PHSANG(IKP1),ANGF(LLM1),ANGF(LL),                &
     &        PHM1,F(4,LLM1,IVM1),F(4,LL,IVM1))
         CALL INTERP(2,PHSANG(IKP1),ANGF(LLM1),ANGF(LL),                &
     &        PH,F(4,LLM1,IV),F(4,LL,IV))
         PAERB(4)=PH+(PHM1-PH)*FAC
         PCIRB=HENGNS(ASYV(1,5),PHSCOS(IKP1))
      ELSEIF(IPH.EQ.2)THEN
         PAERB(1)=PHASEF(1,V,AH1(IKP1),PHSANG(IKP1),ARH(IKP1))
         PAERB(2)=PHASEF(2,V,AH1(IKP1),PHSANG(IKP1),ARH(IKP1))
         PAERB(3)=PHASEF(3,V,AH1(IKP1),PHSANG(IKP1),ARH(IKP1))
         PAERB(4)=PHASEF(4,V,AH1(IKP1),PHSANG(IKP1),ARH(IKP1))
         PCIRB=HENGNS(ASYV(1,5),PHSCOS(IKP1))
      ENDIF
      IF(NCRSPC.EQ.1)THEN

!         ANGULAR INTERPOLATE TABULATED SCATTERING PHASE FUNCTIONS:
          CALL GETPF(PHSCOS(IKP1),PCLDB,PICEB)
          IF(ABS(ASYMWD).GE.1.)THEN

!             SPECTRALLY VARYING WATER DROPLET SCATTERING PHASE FUNCTION
              PCLDB=HENGNS(ASYV(1,6),PHSCOS(IKP1))
          ELSE

!             SPECTRALLY INDEPENDENT WATER DROPLET PHASE FUNCTION
              PCLDB=PHASFN(IKP1,3)
          ENDIF
          IF(ABS(ASYMIP).GE.1.)THEN

!             SPECTRALLY VARYING ICE PARTICLE SCATTERING PHASE FUNCTION
              PICEB=HENGNS(ASYV(1,7),PHSCOS(IKP1))
          ELSE

!             SPECTRALLY INDEPENDENT ICE PARTICLE PHASE FUNCTION
              PICEB=PHASFN(IKP1,4)
          ENDIF
      ENDIF
      PRAINB=HENGNS(TX(79),PHSCOS(IKP1))
      RETURN

!     IPATH=3 (OBSERVER TO SCATTERING POINT).
   50 CONTINUE

!     NO PREPARATION REQUIRED FOR MULTIPLE SCATTERING PATH.
      IF(MSOFF.GT.0)RETURN

!     FOR NOVAM AEROSOLS, THE ASYMMETRY FACTOR IS A FUNCTION OF
!     BOTH WAVELENGTH AND ALTITUDE.  CALCULATE THE SCATTERING
!     PHASE FUNCTION FOR THE LAYER AVERAGE SCATTERING ANGLE COSINE.
      IKP1=IK+1
      IF(TX(72).LE.0.)THEN
          PNOV=0.
      ELSE
          PNOV=HENGNS(TX(73),.5*(PHSCOS(IK)+PHSCOS(IKP1)))
      ENDIF

!     LOOP OVER K-DISTRIBUTION SUB-INTERVALS CALCULATING THE AVERAGE
!     L-PATH TRANSMITTED SOLAR/LUNAR IRRADIANCE THROUGH THE LAYER.
      STRANL=0.
      DO 60 INTRVL=1,KNTRVL

!         STRNK IS THE AVERAGE K SUB-INTERVAL L-PATH TRANSMITTANCE
!         THROUGH THE LAYER STARTING FROM THE FRONT OF THE LAYER.

!                              1                                 Z
!                              /  /TRNLAY(INTRVL)  S0TRN(.,IKP1)\
!         STRNK = S0TRN(.,IK)  |  |----------------------------- |    dZ
!                              /  \         S0TRN(.,IK)         /
!                              0
          DELDEP=DEPLAY(INTRVL)+S0DEP(INTRVL,IKP1)-S0DEP(INTRVL,IK)
          STRNK=TRNLAY(INTRVL)*S0TRN(INTRVL,IKP1)
          IF(ABS(DELDEP).LT..001)THEN
              STRNK=.5*(S0TRN(INTRVL,IK)+STRNK)
          ELSE
              STRNK=(S0TRN(INTRVL,IK)-STRNK)/DELDEP
          ENDIF
          STRANL=STRANL+WTKSUB(INTRVL)*TRNCUM(INTRVL)*STRNK
   60 CONTINUE

!     SUM THE SOLAR/LUNAR SINGLE SCATTER RADIANCE CONTRIBUTIONS.
      SUMSSS=SUMSSS+STRANL*(TX(72)*PNOV+                                &
     &  .5*(TX(68)*(PAERB(1)+PAERF(1))+TX(69)*(PAERB(2)+PAERF(2))       &
     &     +TX(70)*(PAERB(3)+PAERF(3))+TX(71)*(PAERB(4)+PAERF(4))       &
     &     +TX(15)*(PMOLB   +PMOLF   )+TX(74)*(PCIRB   +PCIRF   )       &
     &     +TX(66)*(PCLDB   +PCLDF   )+TX(67)*(PICEB   +PICEF   )       &
     &     +TX(77)*(PRAINB  +PRAINF  )))
      RETURN
      END
