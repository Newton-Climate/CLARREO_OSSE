      SUBROUTINE NEWH2(H1,H2,ANGLE,RANGE,BETA,LENN,HTAN,PHI)

!     THIS ROUTINE DETERMINES
!       HTAN     TANGENT HEIGHT [KM],
!       H2       FINAL ALTITUDE [KM],
!       BETA     EARTH CENTER ANGLE [DEG], AND
!       LENN     PATH SWITCH (=1 IF PATH PASSES THROUGH HTAN)
!     GIVEN INPUTS
!       H1       INITIAL (OBSERVER) ALTITUDE [KM],
!       ANGLE    PATH ZENITH ANGLE AT H1 [DEG], AND
!       RANGE    PATH SLANT RANGE.

!     DECLARE ARGUMENTS:
      DOUBLE PRECISION H1,H2,ANGLE,RANGE,BETA,HTAN,PHI
      INTEGER LENN

!     LIST COMMONS:
      REAL RE,ZMAX
      INTEGER IPATH
      COMMON/PARMTR/RE,ZMAX,IPATH

!     DECLARE LOCAL VARIABLES:
      DOUBLE PRECISION RX,CPATH,RATIO

!     DECLARE FUNCTIONS:
      DOUBLE PRECISION TANHT

!     LIST DATA:
      DOUBLE PRECISION DEG
      DATA DEG/57.2957795131D0/

!     COMPUTE CPATH, THE PATH CONSTANT,
      CALL IRFXN(H1,RX,RATIO)
      CPATH=RX*SIN(ANGLE/DEG)*(H1+DBLE(RE))

!     FIND HTAN, H2, BETA AND LENN.
      HTAN=TANHT(CPATH)
      CALL FNDPTH(CPATH,H1,HTAN,H2,RANGE,BETA,LENN,ANGLE,PHI)
      IF(ANGLE.LE.90.)HTAN=MIN(H1,H2)

!     ROUTINE OPERATIONS COMPLETED.
      RETURN
      END
