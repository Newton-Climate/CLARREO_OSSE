      SUBROUTINE MAPMS(ML,IKMAX)

!     ROUTINE MAPMS DETERMINES WHICH ATMOSPHERIC LAYER
!     CONTAINS EACH LINE-OF-SIGHT PATH SEGMENT.

!     LIST PARAMETERS
      INCLUDE 'PARAMS.h'
      INCLUDE 'ERROR.h'

!     DECLARE INPUTS
!       ML      NUMBER OF LAYER BOUNDARIES
!       IKMAX   NUMBER OF LINE-OF-SIGHT PATHS SEGMENTS
      INTEGER ML,IKMAX

!     /MODEL/
!       ZM       PROFILE BOUNDARY ALTITUDES [KM].
!       PM       PROFILE BOUNDARY PRESSURES [MBAR].
!       TM       PROFILE BOUNDARY TEMPERATURES [K].
!       RFNDX    PROFILE BOUNDARY REFRACTIVITIES.
!       DENSTY   PROFILE BOUNDARY DENSITIES [UNITS DEPEND ON SPECIES].
!       LRHSET   FLAG, TRUE IF RELATIVE HUMIDITY CANNOT BE SCALED.
      REAL ZM,PM,TM,RFNDX,DENSTY
      LOGICAL LRHSET
      COMMON/MODEL/ZM(LAYDIM),PM(LAYDIM),TM(LAYDIM),                    &
     &  RFNDX(LAYDIM),DENSTY(MEXT,LAYDIM),LRHSET(LAYDIM)

!     /PATH/
!       QTHETA  COSINE OF PATH ZENITH AT PATH BOUNDARIES.
!       AHT     ALTITUDES AT PATH BOUNDARIES [KM].
!       IHT     ALTITUDES AT PATH BOUNDARIES [M].
!       TPH     TEMPERATURE AT PATH BOUNDARIES [K].
!       IMAP    MAPPING FROM PATH SEGMENT MIDPOINT TO VERTICAL LAYER.
!       LOWAHT  INDEX OF VERTICAL LAYER BOUNDARY AT OR JUST BELOW AHT.
!       FACAHT  ALTITUDE INTERPOLATION FRACTION FOR AHT.
      INTEGER IHT,IMAP,LOWAHT
      REAL QTHETA,AHT,TPH,FACAHT
      COMMON/PATH/QTHETA(LAYTWO),AHT(LAYTWO),IHT(0:LAYTWO),             &
     &  TPH(LAYTWO),IMAP(LAYTWO),LOWAHT(LAYTWO),FACAHT(LAYTWO)

!     DECLARE LOCAL VARIABLES
      INTEGER I,J,JP1,ILO,IHI
      REAL AVGALT
      ILO=1
      DO IHI=2,ML-1
          IF(ZM(IHI).GT.AHT(1))GOTO 10
          ILO=IHI
      ENDDO
      IHI=ML
   10 CONTINUE
      LOWAHT(1)=ILO
      FACAHT(1)=(AHT(1)-ZM(ILO))/(ZM(IHI)-ZM(ILO))
      IF(ABS(FACAHT(1)).LT..00001)FACAHT(1)=0.

!     LOOP OVER PATH SEGMENTS
      J=1
      DO JP1=2,IKMAX+1
          ILO=1
          DO IHI=2,ML-1
              IF(ZM(IHI).GT.AHT(JP1))GOTO 20
              ILO=IHI
          ENDDO
          IHI=ML
   20     CONTINUE
          LOWAHT(JP1)=ILO
          FACAHT(JP1)=(AHT(JP1)-ZM(ILO))/(ZM(IHI)-ZM(ILO))
          IF(ABS(FACAHT(JP1)).LT..00001)FACAHT(JP1)=0.

!         DEFINE LINE SEGMENT MIDDLE ALTITUDE
          AVGALT=.5*(AHT(J)+AHT(JP1))
          IMAP(J)=1
          DO I=2,ML
              IF(ZM(I).GE.AVGALT)GOTO 30
              IMAP(J)=I
          ENDDO
          IF(LJMASS)CALL WRTBUF(FATAL)
          STOP 'ERROR in MAPMS:  Path segment above top of atmosphere.'
   30     CONTINUE
          J=JP1
      ENDDO
      RETURN
      END
