      SUBROUTINE M3D(IVX,KNTRVL,IK,IKMAX,SUBINT,LNFLRT,FLRT)

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     ARGUMENTS
!       IVX      SPECTRAL FREQUENCY [CM-1].
!       KNTRVL   NUMBER OF ABSORPTION COEFFICIENT (K) SUB-INTERVALS.
!       IK       PATH SEGMENT INDEX.
!       IKMAX    NUMBER OF PATH SEGMENTS.
!       SUBINT   SPECTRAL BIN "K" SUB-INTERVAL FRACTIONAL WIDTHS.
      INTEGER IVX,KNTRVL,IK,IKMAX
      REAL SUBINT(*)
      INTEGER LNFLRT
      CHARACTER FLRT*(*)

!     COMMONS:
      INCLUDE 'IFIL.h'

!     /BASE/
!         TX ARRAY
!           1  AEROSOL SCATTERING DEPTH WEIGHTED ASYMMETRY PARAMETER.
!           2  INCREMENTAL AEROSOL SCATTERING OPTICAL DEPTH.
!           3  TOTAL O2 CONTINUUM TRANSMITTANCE.
!           4  N2 CONTINUUM TRANSMITTANCE.
!           5  TOTAL H2O CONTINUUM TRANSMITTANCE.
!           6  RAYLEIGH MOLECULAR SCATTERED TRANSMITTANCE.
!           7  AEROSOL EXTINCTION.
!           8  TOTAL OZONE CONTINUUM TRANSMITTANCE.
!           9  PRODUCT OF ALL CONTINUUM TRANSMITTANCES EXCEPT O2 & HNO3.
!          10  AEROSOL ABSORPTION.
!          11  HNO3 TRANSMITTANCE.
!          12  MOLECULAR CONTINUUM OPTICAL DEPTH.
!          13  INCREMENTAL AEROSOL EXTINCTION OPTICAL DEPTH.
!          14  TOTAL CONTINUUM OPTICAL DEPTH.
!          15  LAYER RAYLEIGH MOLECULAR SCATTERING OPTICAL DEPTH.
!          16  CIRRUS CLOUD TRANSMITTANCE (ICLD=20 ONLY).
!          64  UV/VIS NO2 TRANSMITTANCE.
!          65  UV/VIS SO2 TRANSMITTANCE.
!          66  INCREMENTAL WATER DROPLET SCATTERING OPTICAL DEPTH.
!          67  INCREMENTAL ICE PARTICLE SCATTERING OPTICAL DEPTH.
!          74  INCREMENTAL STD/SUB-VIS CIRRUS SCATTERING OPTICAL DEPTH.
!          75  INCREMENTAL CLOUD (WATER+ICE) EXTINCTION OPTICAL DEPTH.
!          76  CLOUD ASYMMETRY PARAMETER WEIGHTED BY SCATTERING DEPTH.
!          77  INCREMENTAL RAIN EXTINCTION OPTICAL DEPTH
!          78  INCREMENTAL RAIN SCATTERING OPTICAL DEPTH
!          79  INCREMENTAL RAIN ASYMMETRY PARAMETER.
!          --  MOLECULAR LINE CENTER TRANSMITTANCE  --
!          17=H2O  36=CO2  31=O3   47=N2O  44=CO   46=CH4
!          50=O2   54=NO   56=SO2  55=NO2  52=NH3  11=HNO3
      INCLUDE 'BASE.h'

!     COMMON /CORKDT/
!       WTKSUB   SPECTRAL BIN SUB-INTERVAL FRACTIONAL WIDTHS.
!       DEPLAY   INCREMENTAL EXTINCTION OPTICAL DEPTHS
!       TRNLAY   INCREMENTAL TRANSMITTANCES
!       TRNCUM   CUMULATIVE TRANSMITTANCES
!       K2TAIL   POINTER FROM K BIN TO LINE TAIL SUB-BIN
!                (=0 IF MULTIPLE LINE TAIL SUB-BINS CONTRIBUTE).
!       CONTWT   WEIGHTS FOR PARTITIONING LINE TAILS INTO K'S
!                (ONLY USED IF K2TAIL IS 0).
      REAL WTKSUB,DEPLAY,TRNLAY,TRNCUM,CONTWT
      INTEGER K2TAIL
      COMMON/CORKDT/WTKSUB(MXKSUB),DEPLAY(MXKSUB),TRNLAY(MXKSUB),       &
     &  TRNCUM(MXKSUB),K2TAIL(MXKSUB),CONTWT(MTLSUB,MXKSUB)
      SAVE /CORKDT/

!     /CARD2/
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT

!     /M3DNAM/
!       M3DGEN   NAME OF GENERAL INFORMATION FILE FOR MOD3D.
!       M3DCNT   NAME OF CONTINUA DATA FILE FOR MOD3D.
!       M3DDAT   NAME OF MOLECULAR EXTINCTION FILE SORTED BY ATMOSPHERE.
!       M3DMOL   NAME OF MOLECULAR EXTINCTION DATA FILE FOR MOD3D.
      CHARACTER*(NAMLEN) M3DGEN,M3DCNT,M3DDAT,M3DMOL
      COMMON/M3DNAM/M3DGEN,M3DCNT,M3DDAT,M3DMOL
      INCLUDE 'BMHEAD.h'

!     /M3DSPC/
!       LASCII   ASCII FILE OUTPUT FLAG FOR MOD3D FILES.
!       NATM     COUNTER FOR NUMBER OF DIFFERENT MOLECULAR ATMOSPHERES.
!       IVXMIN   MINIMUM COMPUTATION SPECTRAL FREQUENCY [CM-1].
!       IVXMAX   MAXIMUM COMPUTATION SPECTRAL FREQUENCY [CM-1].
      LOGICAL LASCII
      INTEGER NATM,IVXMIN,IVXMAX
      COMMON/M3DSPC/LASCII,NATM,IVXMIN,IVXMAX

!     /CARD1/
      INTEGER MODEL,ITYPE,IEMSCT,M1,M2,M3,IM,NOPRNT
      LOGICAL MODTRN
      COMMON/CARD1/MODEL,ITYPE,IEMSCT,M1,M2,M3,IM,NOPRNT,MODTRN

!     /M3DDEN/
!       PRES     LAYER PRESSURE [ATM].
!       AER550   AEROSOL EXTINCTION OPTICAL DEPTH AT 550 NM.
!       DENH2O   LAYER H2O DENSITY [GM/M3].
!       DENCO2   LAYER CO2 DENSITY [GM/M3].
!       DENO3    LAYER O3 DENSITY [GM/M3].
      REAL PRES,AER550,DENH2O,DENCO2,DENO3
      COMMON/M3DDEN/PRES(LAYTWO),AER550(LAYTWO),                        &
     &  DENH2O(LAYTWO),DENCO2(LAYTWO),DENO3(LAYTWO)

!     COMMON/CONSTN/
!       PZERO    STANDARD PRESSURE [MB].
!       TZERO    STANDARD TEMPERATURE [K].
!       AVOGAD   AVOGADRO CONSTANT [MOLECULES/MOLE].
!       STDVOL   STANDARD VOLUME OF IDEAL GAS [CM3 ATM / MOLE].
!       AIRMWT   EFFECTIVE WEIGHT OF AIR [GM/MOLE].
!       AMWT     MOLECULAR WEIGHTS [GM/MOLE].
!                  1  H2O   2  CO2   3  O3    4  N2O   5  CO    6  CH4
!                  7  O2    8  NO    9  SO2   10 NO2   11 NH3   12 HNO3
      REAL PZERO,TZERO,AVOGAD,STDVOL,AIRMWT,AMWT
      COMMON/CONSTN/PZERO,TZERO,AVOGAD,STDVOL,AIRMWT,AMWT(12)
      SAVE /M3DDEN/,/CONSTN/
      EXTERNAL DEVCBD,ATMCON

!     LOCAL VARIABLES:
!       INTRVL   ABSORPTION COEFFICIENT (K) SUB-INTERVAL INDEX.
!       GAER     AEROSOL ASYMMETRY FACTOR.
!       CLDSCT   CLOUD SCATTERING OPTICAL DEPTH.
!       GCLD     CLOUD ASYMMETRY FACTOR.
!       COLDEN   EFFECTIVE COLUMN DENSITY [ATM KM / K].
!       LFIRST   LOGICAL INDICATING FIRST ENTRY WITH NEW ATMOSPHERE.
      INTEGER INTRVL
      REAL CLDSCT,COLDEN,XMOL(MXKSUB),SAER(LAYTWO),XAER(LAYTWO),        &
     &  GAER(LAYTWO),SCLD(LAYTWO),XCLD(LAYTWO),GCLD(LAYTWO),            &
     &  SRAIN(LAYTWO),XRAIN(LAYTWO),GRAIN(LAYTWO),SMOL(LAYTWO)
      SAVE SAER,XAER,GAER,SCLD,XCLD,GCLD,SRAIN,XRAIN,GRAIN,SMOL
      LOGICAL LFIRST
      LFIRST=IK.EQ.1 .AND. IVX.EQ.IVXMIN
      IF(TX(2).GT.0.)THEN
          GAER(IK)=TX(1)/TX(2)
      ELSE
          GAER(IK)=0.
      ENDIF
      CLDSCT=TX(66)+TX(67)+TX(74)
      IF(CLDSCT.GT.0.)THEN
          GCLD(IK)=TX(76)/CLDSCT
      ELSE
          GCLD(IK)=0.
      ENDIF
      COLDEN=W(6)/TZERO
!old  IF(IK.EQ.1)THEN
!old      WRITE(IDBOUT,'(6X,A,I6,A,/F10.7,3(2(1X,1PE13.7),1X,0PF7.5),
!old 1      35(1X,1PE13.7))')' (FREQ =',IVX,' CM-1)',
!old 2      PRES(IK),TX(2),TX(13),GAER(IK),CLDSCT,TX(75),GCLD(IK),
!old 3      TX(78),TX(77),TX(79),TX(15),(DEPLAY(INTRVL),INTRVL=1,KNTRVL)
!old  ELSE
!old      WRITE(IDBOUT,
!old 1      '(F10.7,3(2(1X,1PE13.7),1X,0PF7.5),35(1X,1PE13.7))')
!old 2      PRES(IK),TX(2),TX(13),GAER(IK),CLDSCT,TX(75),GCLD(IK),
!old 3      TX(78),TX(77),TX(79),TX(15),(DEPLAY(INTRVL),INTRVL=1,KNTRVL)
!old  ENDIF
      IF(TX(13).GT.0.)THEN
          SAER(IK)=TX(2)/AER550(IK)
          XAER(IK)=TX(13)/AER550(IK)
      ELSE
          SAER(IK)=0.
          XAER(IK)=0.
      ENDIF
      IF(TX(75).LE.0.)THEN
          SCLD(IK)=0.
          XCLD(IK)=0.
      ELSEIF(ICLD.EQ.18)THEN
          SCLD(IK)=17.21*CLDSCT/W(16)
          XCLD(IK)=17.21*TX(75)/W(16)
      ELSEIF(ICLD.EQ.19)THEN
          SCLD(IK)=290.19*CLDSCT/W(16)
          XCLD(IK)=290.19*TX(75)/W(16)
      ELSE
          SCLD(IK)=CLDSCT/(W(66)+W(67))
          XCLD(IK)=TX(75)/(W(66)+W(67))
      ENDIF
      IF(W(3).GT.0.)THEN
          SRAIN(IK)=TX(78)/W(3)
          XRAIN(IK)=TX(77)/W(3)
      ELSE
          SRAIN(IK)=0.
          XRAIN(IK)=0.
      ENDIF
      GRAIN(IK)=TX(79)
      SMOL(IK)=TX(15)/COLDEN
      DO INTRVL=1,KNTRVL
          XMOL(INTRVL)=DEPLAY(INTRVL)/COLDEN
      ENDDO
      CALL M3DWRT(SMOL,I3DGEN,I3DCNT,I3DDAT,IHAZE,ICLD,M1,IVXMIN,IVXMAX,&
     &  IBNDWD,KNTRVL,SUBINT,IK,IKMAX,NATM,PRES,DENH2O,DENCO2,DENO3,    &
     &  SAER,XAER,GAER,SCLD,XCLD,GCLD,SRAIN,XRAIN,GRAIN,                &
     &  XMOL,LASCII,LFIRST,M3DGEN,M3DCNT,M3DDAT,M3DMOL,LNFLRT,FLRT)
      RETURN
      END
