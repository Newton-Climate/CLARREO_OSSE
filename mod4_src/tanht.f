      DOUBLE PRECISION FUNCTION TANHT(CPATH)

!     THIS ROUTINE FINDS THE TANGENT HEIGHT GIVEN
!     CPATH (THE PATH CONSTANT). SEE ATMOSPHERIC
!     TRANSMITTANCE/RADIANCE:  LOWTRAN 6, AFGL-TR-83-018

!     INCLUDE PARAMETERS
      INCLUDE 'PARAMS.h'

!     LIST COMMONS
      REAL RE,ZMAX
      INTEGER IPATH
      COMMON/PARMTR/RE,ZMAX,IPATH

!     /MODEL/
!       ZM       PROFILE BOUNDARY ALTITUDES [KM].
!       PM       PROFILE BOUNDARY PRESSURES [MBAR].
!       TM       PROFILE BOUNDARY TEMPERATURES [K].
!       RFNDX    PROFILE BOUNDARY REFRACTIVITIES.
!       DENSTY   PROFILE BOUNDARY DENSITIES [UNITS DEPEND ON SPECIES].
!       LRHSET   FLAG, TRUE IF RELATIVE HUMIDITY CANNOT BE SCALED.
      REAL ZM,PM,TM,RFNDX,DENSTY
      LOGICAL LRHSET
      COMMON/MODEL/ZM(LAYDIM),PM(LAYDIM),TM(LAYDIM),                    &
     &  RFNDX(LAYDIM),DENSTY(MEXT,LAYDIM),LRHSET(LAYDIM)

!     /CNTRL/
!       IKMAX    NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       ISSGEO   LINE-OF-SIGHT FLAG (0 = SENSOR PATH, 1 = SOLAR PATHS).
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
      INTEGER IKMAX,ML,MLFLX,ISSGEO,IMULT
      COMMON/CNTRL/IKMAX,ML,MLFLX,ISSGEO,IMULT
      REAL GNDALT
      COMMON/GRAUND/GNDALT

!     DECLARE LOCAL VARIABLES
      INTEGER J
      DOUBLE PRECISION CPATH,RX,RATIO,DZMBOT,DZMTOP,DGRND,DRE,CP

!     DECLARE FUNCTIONS
      DOUBLE PRECISION RTBIS

!     CALCULATE TOP OF ATMOSPHERE PATH CONSTANT
      DRE=DBLE(RE)
      DZMTOP=DBLE(ZM(ML))
      DGRND=DBLE(GNDALT)
      CALL IRFXN(DZMTOP,RX,RATIO)
      CP=RX*(DRE+DZMTOP)
      IF(CPATH.GT.CP)THEN

!         PATH ABOVE TOP OF ATMOSPHERE.
          TANHT=CPATH/RX-DRE
      ELSE

!         PATH BELOW TOP OF ATMOSPHERE.
          DO 10 J=ML-1,1,-1
              DZMBOT=DBLE(ZM(J))
              IF(DZMBOT.LT.DGRND)THEN

!                 LAST LAYER
                  CALL IRFXN(DGRND,RX,RATIO)
                  CP=RX*(DRE+DGRND)
                  IF(CPATH.GE.CP)THEN
                      TANHT=RTBIS(DGRND,DZMTOP,CPATH)
                  ELSE

!                     TANGENT HEIGHT BELOW GROUND ALTITUDE.
                      TANHT=DGRND
                  ENDIF
                  RETURN
              ENDIF
              CALL IRFXN(DZMBOT,RX,RATIO)
              CP=RX*(DRE+DZMBOT)
              IF(CPATH.GT.CP)THEN
                 TANHT=RTBIS(DZMBOT,DZMTOP,CPATH)
                 RETURN
              ELSE
                 TANHT=DZMBOT
              ENDIF
              DZMTOP=DZMBOT
   10     CONTINUE

!         TANGENT HEIGHT BELOW GROUND ALTITUDE.
          TANHT=DGRND
      ENDIF
      RETURN
      END
