      SUBROUTINE VSA(IHAZE,VIS,CEILHT,DEPTH,ZINVHT,Z,RH,AHAZE,IH)

!     VERTICAL STRUCTURE ALGORITHM

!     FROM U.S. ARMY ATMOSPHERIC SCIENCES LAB
!     WHITE SANDS MISSILE RANGE, NM

!     CREATES A PROFILE OF AEROSOL DENSITY NEAR THE GROUND,INCLUDING
!     CLOUDS AND FOG

!     THESE PROFILES ARE AT 9 HEIGHTS BETWEEN 0 KM AND 2 KM

!  ***VISIBILITY IS ASSUMED TO BE THE SURFACE VISIBILITY***

!     IHAZE  = THE TYPE OF AEROSOL
!     VIS    = VISIBILITY IN KM AT THE SURFACE
!     CEILHT = THE CLOUD CEILING HEIGHT IN KM
!     DEPTH  = THE CLOUD/FOG DEPTH IN KM
!     ZINVHT = THE HEIGHT OF INVERSION OR BOUNDARY LAYER IN KM

!     VARIABLES USED IN VSA

!     ZC     = CLOUD CEILING HEIGHT IN M
!     ZT     = CLOUD DEPTH IN M
!     ZINV   = INVERSION HEIGHT IN M
!           SEE BELOW FOR MORE INFORMATION ABOUT ZC, ZT, AND ZINV
!     D      = INITIAL EXTINCTION AT THE SURFACE (D=3.912/VIS-0.01159)
!     ZALGO  = THE DEPTH OF THE LAYER FOR THE ALGORITHM

!     OUTPUT FROM VSA:

!     Z      = HEIGHT IN KM
!     RH     = RELATIVE HUMIDITY AT HEIGHT Z IN PERCENT
!     AHAZE  = EXTINCTION AT HEIGHT Z IN KM**-1
!     IH     = AEROSAL TYPE FOR HEIGHT Z
!     HMAX   = MAXIMUM HEIGHT IN KM USED IN VSA, NOT NECESSARILY 2.0 KM

!     THE SLANT PATH CALCULATION USES THE FOLLOWING FUNCTION:

!                 EXT55=A*EXP(B*EXP(C*Z))

!     WHERE 'Z' IS THE HEIGHT IN KILOMETERS,
!           'A' IS A FUNCTION OF EXT55 AT Z=0.0 AND IS ALWAYS POSITIVE,
!           'B' AND 'C' ARE FUNCTIONS OF CLOUD CONDITIONS AND SURFACE
!               VISIBILITY (EITHER A OR B CAN BE POSITIVE OR NEGATIVE),
!           'EXT55' IS THE VISIBILE EXTINCTION COEFFICIENT IN KM**-1.

!     THEREFORE, THERE ARE 4 CASES DEPENDING ON THE SIGNS OF 'B' AND 'C'
!     CEILHT AND ZINVHT ARE USED AS SWITCHES TO DETERMINE WHICH CASE
!     TO USE.  THE SURFACE EXTINCTION 'D' IS CALCULATED FROM THE
!     VISIBILITY USING  D=3.912/VIS-0.01159 AS FOLLOWS-

!         CASE=1  FOG/CLOUD CONDITIONS
!                 'B' LT 0.0, 'C' LT 0.0
!                 'D' GE 7.0   KM**-1
!                 FOR A CLOUD 7.    KM**-1 IS THE BOUNDARY VALUE AT
!                 THE CLOUD BASE AND 'Z' IS THE VERTICAL DISTANCE
!                 INTO THE CLOUD.
!                 VARIABLE USED:   DEPTH
!                 ** DEFAULT:  DEPTH OF FOG/CLOUD IS 0.2 KM WHEN
!                              'DEPTH' IS 0.0

!             =2  CLOUD CEILING PRESENT
!                 'B' GT 0.0, 'C' GT 0.0
!                 VARIABLE USED:   CEILHT (MUST BE GE 0.0)
!                 ** DEFAULTS:  CASE 2 - CEILHT IS CALCULATED FROM
!                               SURFACE EXTINCTION

!             =3  RADIATION FOG OR INVERSION OR BOUNDARY LAYER PRESENT
!                 'B' LT 0.0, 'C' GT 0.0
!                 VIS LE 2.0 KM DEFAULTS TO A RADIATION FOG AT THE
!                     GROUND AND OVERRIDES INPUT BOUNDARY AEROSOL TYPE
!                 VIS GT 2.0 KM FOR AN INVERSION OR BOUNDARY LAYER
!                     WITH INPUT BOUNDARY AEROSOL TYPE
!                 ** IHAZE=9 (RADIATION FOG) ALWAYS DEFAULTS TO A
!                    RADIATION FOG NO MATTER WHAT THE VISIBILITY IS.
!                 SWITCH VARIABLE: CEILHT (MUST BE LT 0.0)
!                 VARIABLE USED:   ZINVHT (MUST BE GE 0.0)
!                 ** CEILHT MUST BE LT 0.0 FOR ZINVHT TO BE USED **
!                    HOWEVER, IF DEPTH IS GT 0.0 AND ZINVHT IS EQ 0.0,
!                    THE PROGRAM WILL SUBSTITUTE DEPTH FOR ZINVHT.
!                 ** DEFAULT:  FOR A RADIATION FOG ZINVHT IS 0.05 K
!                              FOR AN INVERSION LAYER ZINVHT IS 2.0 KM

!           NOTE: IF IHAZE = 9, BUT VIS GT 2.0 KM RECOMEND
!           THAT IHAZE DEFAULT TO RURAL AEROSOL

!             =4  NO CLOUD CEILING, INVERSION LAYER, OR BOUNDARY
!                 LAYER PRESENT, I.E. CLEAR SKIES
!                 EXTINCTION PROFILE CONSTANT WITH HEIGHT A SHORT
!                 DISTANCE ABOVE THE SURFACE

!     PARAMETERS:
      INCLUDE 'ERROR.h'
      INCLUDE 'IFIL.h'

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      EXTERNAL DEVCBD
      DIMENSION Z(10),RH(10),AHAZE(10),IH(10)
      DIMENSION AA(2),CC(3),EE(4),A(2),B(2),C(2),FAC1(9),FAC2(9)
      REAL KMTOM
      DATA AA/92.1,0.3981/,CC/-0.014,0.0125,-0.03 /,KMTOM/1000.0/
!     THE LAST 3 VALUES OF EE BELOW ARE EXTINCTIONS FOR VISIBILITIES
!     EQUAL TO 5.0, 23.0, AND 50.0 KM, RESPECTIVELY.
      DATA EE/7.0,0.7824,0.17009,0.01159/
      DATA FAC1/0.0,0.03,0.05,0.075,0.1,0.18,0.3,0.45,1.0/
      DATA FAC2/0.0,0.03,0.1,0.18,0.3,0.45,0.6,0.78,1.0/
      IF(.NOT.LJMASS) WRITE(IPR,599)

!     UPPER LIMIT ON VERTICAL DISTANCE - 2 KM
      ZHIGH=2000.
      HMAX=ZHIGH
      IF(VIS.GT.0.0)GO TO 5
!     DEFAULT FOR VISIBILITY DEPENDS ON THE VALUE OF IHAZE.
      IF(IHAZE.EQ.8)VIS=0.2
      IF(IHAZE.EQ.9)VIS=0.5
      IF(IHAZE.EQ.2.OR.IHAZE.EQ.5)VIS=5.0
      IF(IHAZE.EQ.1.OR.IHAZE.EQ.4.OR.IHAZE.EQ.7)VIS=23.0
      IF(IHAZE.EQ.6)VIS=50.0
!     IF(IHAZE.EQ.3)VIS= OR IHAZE = 10 VIS IS DETERMINED ELSEWHERE
   5  D=3.912/VIS-0.01159

      ZC=CEILHT*KMTOM
      ZT=DEPTH*KMTOM
      ZINV=ZINVHT*KMTOM
!     IHAZE=9 (RADIATION FOG) IS ALWAYS CALCULATED AS A RADIATION FOG.
      IF(IHAZE.EQ.9)ZC=-1.0
!     ALSO, CHECK TO SEE IF THE FOG DEPTH FOR A RADIATION FOG
!     WAS INPUT TO DEPTH INSTEAD OF THE CORRECT VARIABLE ZINVHT.
      IF(IHAZE.EQ.9.AND.ZT.GT.0.0.AND.ZINV.EQ.0.0)ZINV=ZT

!     'IC' DEFINES WHICH CASE TO USE.
      IC=2
      IF(D.GE.EE(1).AND.ZC.GE.0.0)IC=1

      IF(ZC.LT.0.0.AND.IC.EQ.2)IC=3
      IF(ZINV.LT.0.0.AND.IC.EQ.3)IC=4
      K=1
      GO TO (10,20,40,50),IC

!     CASE 1:  DEPTH FOG/CLOUD; INCREASING EXTINCTION WITH HEIGHT FROM
!              CLOUD/FOG BASE TO CLOUD/FOG TOP.
 10   CONTINUE
      IF(ZC.LT.HMAX.AND.IC.EQ.2)K=2
!     IC=-1 WHEN A CLOUD IS PRESENT AND THE PATH GOES INTO IT.
!     USE CASE 2 OR 2' BELOW CLOUD AND CASE 1 INSIDE IT.
      IF(K.EQ.2)IC=(-1)
!     THE BASE OF THE CLOUD HAS AN EXTINCTION COEFFICIENT OF 7.0   KM-1.
      IF(K.EQ.2)D=EE(1)
      A(K)=AA(1)
!     IF THE SURFACE EXTINCTION IS GREATER THAN THE UPPER LIMIT OF 92.1
!     KM**-1, RUN THE ALGORITHM WITH AN UPPER LIMIT OF 'D+10'.
      IF(D.GE.AA(1))A(K)=D+10.0
      C(K)=CC(1)
      IF(.NOT.LJMASS .AND. ZT.LE.0.0)WRITE(IPR  ,603)
      IF(.NOT.LJMASS .AND. ZT.LE.0.0)WRITE(IPR  ,604)
      IF(.NOT.LJMASS .AND. ZT.GT.0.0)WRITE(IPR  ,611)ZT
!     IF THE DISTANCE FROM THE GROUND TO THE CLOUD/FOG TOP IS LESS
!     THAN 2.0 KM, VSA WILL ONLY CALCULATE UP TO THE CLOUD TOP.
      IF(ZT.LE.0.0)ZT=200.
      HMAX=AMIN1(ZT+ZC,HMAX)
      GO TO 60

!     CASE 2:  CLEAR/HAZY/LIGHTLY FOGGY; INCREASING EXTINCTION WITH HEIG
!              UP TO THE CLOUD BASE.
 20   A(K)=AA(2)
      E=EE(1)
      IF(.NOT.LJMASS .AND. ZC.EQ.0.0)WRITE(IPR  ,600)
      IF(ZC.EQ.0.0)THEN
        EAK =  ALOG(E/A(K) )
        DAK =  ALOG(D/A(K) )
        ANUM = EAK / DAK
        IF(ANUM . GT. 0) THEN
              CEIL = ALOG(ANUM)/CC(2)
         ELSE
               CEIL = 2000.
          ENDIF
      ENDIF
!C    IF(ZC.EQ.0.0)CEIL=ALOG(ALOG(E/A(K))/(ALOG(D/A(K))))/CC(2)
      IF(.NOT.LJMASS .AND. ZC.EQ.0.0)WRITE(IPR  ,602)CEIL
      IF(.NOT.LJMASS .AND. ZC.GT.0.0)WRITE(IPR  ,610)ZC
      IF(ZC.EQ.0.0)ZC=CEIL
      F = (VIS * ZC/350.0)**2

!     F IS A SCALING FACTOR USED IN CASE 2

      DF = D/F
      IF(DF .LT. 1.0E-5) THEN
           A(K)=D - (D*D/(2.*F))
      ELSE
           A(K) = F*(1.0 - EXP(-D/F))
      ENDIF

!     THE COEFFICIENT A IS RECALCULATED BASED UPON THE SCALING FACTOR

      GO TO 60

!     CASE 3:  NO CLOUD CEILING BUT A RADIATION FOG OR AN INVERSION
!              OR BOUNDARY LAYER PRESENT; DECREASING EXTINCTION WITH
!              HEIGHT UP TO THE HEIGHT OF THE FOG OR LAYER.
 40   A(K)=D*1.1
      E=EE(3)
      IF(IHAZE.EQ.2.OR.IHAZE.EQ.5)E=EE(2)
      IF(IHAZE.EQ.6.OR.(VIS.GT.2.0.AND.IHAZE.NE.9))E=EE(4)
      IF(E.GT.D)E=D*0.99999
      IF(ZT.GT.0.0.AND.ZINV.EQ.0.0.AND.VIS.LE.2.0)ZINV=ZT
      IF(.NOT.LJMASS) THEN
         IF(ZINV.EQ.0.0.AND.VIS.GT.2.0.AND.IHAZE.NE.9)WRITE(IPR  ,601)
         IF(ZINV.EQ.0.0.AND.(VIS.LE.2.0.OR.IHAZE.EQ.9))WRITE(IPR  ,605)
         IF(ZINV.EQ.0.0.AND.(VIS.LE.2.0.OR.IHAZE.EQ.9))WRITE(IPR  ,604)
         IF(ZINV.GT.0.0.AND.VIS.GT.2.0.AND.IHAZE.NE.9)                  &
     &      WRITE(IPR  ,612)ZINV
         IF(ZINV.GT.0.0.AND.(VIS.LE.2.0.OR.IHAZE.EQ.9))                 &
     &      WRITE(IPR,614)ZINV
      ENDIF
      IF(ZINV.EQ.0.0.AND.VIS.GT.2.0.AND.IHAZE.NE.9)ZINV=2000
      IF(ZINV.EQ.0.0.AND.(VIS.LE.2.0.OR.IHAZE.EQ.9))ZINV= 50
      HMAX=AMIN1(ZINV,HMAX)
      ZC=0.0
      GO TO 60

!     CASE 4:  NO CLOUD CEILING OR INVERSION LAYER;
!              CONSTANT EXTINCTION WITH HEIGHT.

50     A(K) = EE(4)
       C(K) = CC(3)

60               B(K)=ALOG(D/A(K))
      IF(IC.EQ.2)C(K)=ALOG(ALOG(E/A(K))/B(K))/ZC
      IF(IC.EQ.3)C(K)=ALOG(ALOG(E/A(K))/B(K))/ZINV
      IF(ZC.LT.HMAX.AND.K.EQ.1.AND.IC.EQ.2)GO TO 10
      IF(IC.EQ.2)HMAX=AMIN1(ZC,HMAX)
      ZALGO=HMAX
      IF(IC.LT.0)ZALGO=ZC
      IF(.NOT.LJMASS) WRITE(IPR  ,619)
      IF(IC.LT.0)K=1

      DO 70 I=1,9
      IF(IC.LT.0.AND.I.EQ.5)K=2
      IF(IC.LT.0.AND.I.EQ.5)ZALGO=HMAX-ZC
      Z(I)=ZALGO*(1.0-FAC2(10-I))
      IF(IC.EQ.1)Z(I)=ZALGO*FAC1(I)
      IF(IC.EQ.4)Z(I)=ZALGO*FLOAT(I-1)/8.0
      IF(IC.LT.0.AND.I.LT.5)Z(I)=ZALGO*(1.0-FAC2(11-2*I))
      IF(IC.LT.0.AND.I.GE.5)Z(I)=ZALGO*FAC1(2*I-9)
!     IF(IC.LT.0.AND.(I.EQ.7.OR.I.EQ.8))Z(I)=ZALGO*FAC1(2*I-10)
                 AHAZE(I)=A(K)*EXP(B(K)*EXP(C(K)*Z(I)))
      IF(IC.LE.0.AND.I.GE.5)Z(I)=Z(I)+ZC
      Z(I)=Z(I)/KMTOM
      RH(I)=6.953*ALOG(AHAZE(I))+86.407
      IF(AHAZE(I).GE.EE(1))RH(I)=100.0
      VISIB=3.912/(AHAZE(I)+0.01159)
      IH(I)=IHAZE
!     IF A RADIATION FOG IS PRESENT (I.E. VIS<=2.0 KM AND IC=3),
!     IH IS SET TO 9 FOR ALL LEVELS.
      IF(VISIB.LE.2.0.AND.IC.EQ.3)IH(I)=9
!     FOR A DEPTH FOG/CLOUD CASE, IH=8 DENOTING AN ADVECTION FOG.
      IF(IC.EQ.1.OR.(IC.LT.0.AND.I.GE.5))IH(I)=8
      IF(.NOT.LJMASS) WRITE(IPR  ,620)Z(I),RH(I),AHAZE(I),VISIB,IH(I)
   70 CONTINUE
      HMAX=HMAX/KMTOM
      RETURN

599   FORMAT('0 VERTICAL STRUCTURE ALGORITHM (VSA) USED')
600   FORMAT(1H ,50X,28HCLOUD CEILING HEIGHT UNKNOWN)
601   FORMAT(1H ,50X,42HINVERSION OR BOUNDARY LAYER HEIGHT UNKNOWN,/,   &
     &  1H ,50X,39HVSA WILL USE A DEFAULT OF 2000.0 METERS,/)
605   FORMAT(1H ,50X,27HRADIATION FOG DEPTH UNKNOWN)
619   FORMAT(5X,10HHEIGHT(KM),5X,7HR.H.(%),5X,16HEXTINCTION(KM-1),      &
     &   5X,15HVIS(3.912/EXTN),5X,5HIHAZE,/)
620   FORMAT(7X,F7.4,7X,F5.1,8X,E12.4,11X,F7.4,10X,I2)
602   FORMAT(1H ,39X,35HVSA WILL USE A CALCULATED VALUE OF ,F7.1,       &
     &       7H METERS,/)
603   FORMAT(1H ,50X,19HCLOUD DEPTH UNKNOWN)
604   FORMAT(1H ,50X,38HVSA WILL USE A DEFAULT OF 200.0 METERS,/)
610   FORMAT(1H ,50X,24HCLOUD CEILING HEIGHT IS ,F9.1,7H METERS,/)
611   FORMAT(1H ,50X,15HCLOUD DEPTH IS ,F14.1,7H METERS,/)
612   FORMAT(1H ,50X,38HINVERSION OR BOUNDARY LAYER HEIGHT IS ,F7.1,    &
     & 7H METERS,/)
614   FORMAT(1H ,50X,26HDEPTH OF RADIATION FOG IS ,F7.1,7H METERS,/)
      END
