      SUBROUTINE SMPREP(SPH1,SPH2,SPANGL,SPRANG,SPBETA,ISLCT)

!     PARAMETERS:
      INCLUDE 'ERROR.h'

!     THIS SUBROUTINE PREPS OR PREPROCESSES PATH GEOMETRY
!     PARAMETERS TO SEE IS IF THE RANGE IS SMALL.  ALL SMALL PATH
!     CASES ARE CAST INTO THE EQUIVALENT CASE 2C (H1,H2,RANGE).

!     INPUT ARGUMENTS:
      REAL SPH1,SPH2,SPANGL,SPRANG,SPBETA
      INTEGER ISLCT

!     COMMONS:
      REAL SMALL
      COMMON/SMALL3/SMALL
      REAL RE,ZMAX
      INTEGER IPATH
      COMMON/PARMTR/RE,ZMAX,IPATH
      DOUBLE PRECISION H1,H2,RANGE
      COMMON/SMALL4/H1,H2,RANGE
      INCLUDE 'IFIL.h'

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      SAVE /SMALL3/
      EXTERNAL DEVCBD,ATMCON
      REAL AORIG
      COMMON/SMALL5/AORIG

!     DECLARE LOCAL VARIABLES
      DOUBLE PRECISION ANGLE,BETA,DPDEG,R1,DRE,STORE

!     LIST DATA
      DATA DPDEG/57.2957795131D0/

!     AORIG IS THE ORIGINAL INPUT ANGLE IN SINGLE PRECISION
      AORIG=SPANGL

!     DEFINE DOUBLE PRECISION VARIABLES
      H1=DBLE(SPH1)
      H2=DBLE(SPH2)
      RANGE=DBLE(SPRANG)

!     IF ALREADY IN CASE 2C (H1,H2,RANGE) RETURN.
      IF(ISLCT.EQ.23)RETURN

!     DETERMINE RANGE WITHOUT REFRACTION
!     (REFRACTION IS NOT IMPORTANT FOR SHORT PATHS).
      DRE=DBLE(RE)
      R1=H1+DRE
      IF(ISLCT.EQ.24)THEN

!         CASE 2D (H1,H2,BETA)
          BETA=DBLE(SPBETA)/DPDEG
          RANGE=SQRT((H1-H2)**2+4*R1*(H2+DRE)*SIN(BETA/2)**2)
      ELSEIF(ISLCT.EQ.22)THEN

!         CASE 2B (H1,ANGLE,RANGE)
          ANGLE=DBLE(SPANGL)
          STORE=(RANGE-H1)*(RANGE+H1)+2*R1*(H1+RANGE*COS(ANGLE/DPDEG))
          H2=STORE/(SQRT(DRE**2+STORE)+DRE)
      ENDIF

!     CHECK FOR SMALL RANGE
! JMASS TREATS FOLLOWING MESSAGE AS WARNING
      IF(RANGE.LE.SMALL .AND. RANGE.GT.0.)THEN
          SPANGL=0.
          SPBETA=0.
          SPRANG=REAL(RANGE)
          SPH1=REAL(H1)
          SPH2=REAL(H2)
          WRITE(IPR,'(/2A,F12.7,A)')' FROM SMPREP:  RANGE IS',          &
     &      ' BELOW THE SMALL RANGE CUTOFF (',SMALL,'KM).'
          WRITE(IPR,'(/14X,A,/14X,4(A,F12.7))')                         &
     &      ' THE INPUT GEOMETRY HAS BEEN CONVERTED TO CASE 2C WITH',   &
     &      ' H1 =',SPH1,'KM, H2 =',SPH2,'KM, AND RANGE =', RANGE,'KM.'
      ELSEIF(RANGE.LT.0)THEN
          WRITE(IPR,'(/A)')' FROM SMPREP:  RANGE IS LESS THAN 0.'
          IF(LJMASS)CALL WRTBUF(FATAL)
          STOP
      ENDIF
      RETURN
      END
