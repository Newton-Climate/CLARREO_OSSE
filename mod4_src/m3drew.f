      SUBROUTINE M3DREW(NIVX,KNTRVL,IKMAX,NATM,                         &
     &  I3DGEN,I3DDAT,I3DMOL,M3DMOL,LNFLRT,FLRT)

!     PARAMETERS:
!       MDATA    MAXIMUM NUMBER OF MOD3D DATA FOR A GIVEN FREQUENCY:
      INTEGER MDATA
      PARAMETER(MDATA=170000)

!     M3DREW RE-WRITES THE MOD3D MOLECULAR DATA FILE PLACING ALL
!     SPECTRAL DATA FOR A GIVEN SPECTRAL FREQUENCY IN ONE RECORD.

!     ARGUMENTS:
!       I3DGEN   GENERAL INFORMATION FILE UNIT NUMBER FOR MOD3D.
!       I3DDAT   MOLECULAR EXTINCTION FILE UNIT NUMBER BY ATMOSPHERE.
!       I3DMOL   MOLECULAR EXTINCTION DATA FILE UNIT NUMBER FOR MOD3D.
!       NIVX     NUMBER OF SPECTRAL DATA POINTS.
!       KNTRVL   NUMBER OF ABSORPTION COEFFICIENT (K) SUB-INTERVALS.
!       IKMAX    NUMBER OF PATH SEGMENTS.
!       NATM     NUMBER OF ATMOSPHERES IN MOLECULAR GRID.
!       M3DMOL   NAME OF MOLECULAR EXTINCTION DATA FILE FOR MOD3D.
      INTEGER I3DGEN,I3DDAT,I3DMOL,NIVX,KNTRVL,IKMAX,NATM
      CHARACTER M3DMOL*(*)
      INTEGER LNFLRT
      CHARACTER FLRT*(*)

!     FUNCTIONS:
!       IRECLN   RETURNS THE RECORD LENGTH FOR A DIRECT ACCESS FILES
!                CONTAINING A KNOWN NUMBER OF VARIABLES IN EACH RECORD.
      INTEGER IRECLN

!     LOCAL VARIABLES:
!       LRECLN   RECORD LENGTH OF A DIRECT ACCESS FILE.
!       NDATA    NUMBER OF MOD3D DATA FOR A GIVEN FREQUENCY.
!       IVX      SPECTRAL FREQUENCY COUNTER.
!       INTRVL   ABSORPTION COEFFICIENT (K) SUB-INTERVAL INDEX.
!       IK       LAYER INDEX.
!       IATM     ATMOSPHERE INDEX.
!       NREC     BINARY FILE RECORD NUMBER FOR M3DDAT FILE.
!       IATM     COUNTER FOR NUMBER OF DIFFERENT MOLECULAR ATMOSPHERES.
!       IDATA    MOD3D DATA COUNTER FOR A GIVEN FREQUENCY:
!       XTOT     TEMPORARY STORAGE ARRAY FOR MOLECULAR EXTINCTION DATA.
!       XMOL     TEMPORARY STORAGE ARRAY (CONTAINS K SUB-
!                INTERVAL MOLECULAR EXTINCTION OPTICAL DEPTH
!                TIMES TEMPERATURE AND OVER THE PRODUCT OF
!                PRESSURE AND PATH LENGTH [KM-1 K/ATM].
      INTEGER LRECLN,NDATA,IVX,INTRVL,IK,NREC,IATM,IDATA
      REAL XTOT(MDATA)

!     CLOSE MOD3D GENERAL INFORMATION FILE:
      CLOSE(I3DGEN,STATUS='KEEP')

!     RETURN IF SPECTRAL DATA DOES NOT FIT IN XTOT:

!     ***  Should use MALLOC command to allocate necessary memory.

      NDATA=KNTRVL*IKMAX*NATM
      IF(NDATA.GT.MDATA)RETURN

!     OPEN MOD3D MOLECULAR ABSORPTION DATA FILE:
      OPEN(UNIT=I3DMOL,FILE=M3DMOL,STATUS='UNKNOWN')
      CLOSE(UNIT=I3DMOL,STATUS='DELETE')
      LRECLN=IRECLN(NDATA,LNFLRT,FLRT)
      OPEN(UNIT=I3DMOL,FILE=M3DMOL,STATUS='NEW',                        &
     &  FORM='UNFORMATTED',ACCESS='DIRECT',RECL=LRECLN)

!     LOOP OVER SPECTRAL FREQUENCIES:
      DO 30 IVX=0,NIVX-1

!         LOOP OVER LAYERS:
          DO 20 IK=0,IKMAX-1

!             LOOP OVER ATMOSPHERES:
              DO 10 IATM=0,NATM-1

!                 M3DDAT RECORD NUMBER:
                  NREC=1+IK+(IATM*NIVX+IVX)*IKMAX
                  READ(I3DDAT,REC=NREC)                                 &
     &              (XTOT(INTRVL+KNTRVL*(IATM+NATM*IK)),INTRVL=1,KNTRVL)

!             END ATMOSPHERE LOOP:
   10         CONTINUE

!         END LAYER LOOP:
   20     CONTINUE
          WRITE(I3DMOL,REC=1+IVX)(XTOT(IDATA),IDATA=1,NDATA)

!     END SPECTRAL FREQUENCY LOOP:
   30 CONTINUE

!     CLOSE MOD3D FILES:
      CLOSE(I3DDAT,STATUS='DELETE')
      CLOSE(I3DMOL,STATUS='KEEP')
      RETURN
      END
