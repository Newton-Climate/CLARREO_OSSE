      SUBROUTINE CRPROF(CTHICK,CWDCOL,CIPCOL,M_CLOUD_LIQ,M_CLOUD_ICE,   &
     &                  NUM_LEVS,Z_CLD_VAL,LATITUDE_INPUT,              &
     &                  LONGITUDE_INPUT)

!     THIS ROUTINE DEFINES CLOUD/RAIN MODEL PROFILES.
      INTEGER NUM_LEVS
      REAL M_CLOUD_LIQ(NUM_LEVS-1)
      REAL M_CLOUD_ICE(NUM_LEVS-1)
      REAL Z_CLD_VAL(NUM_LEVS-1)
      REAL LATITUDE_INPUT,LONGITUDE_INPUT

!     LIST PARAMETERS
      INTEGER NMDLS
      PARAMETER(NMDLS=10)
      INCLUDE 'PARAMS.h'
      INCLUDE 'ERROR.h'

!     LIST ARGUMENTS
!     CTHICK   OUTPUT CLOUD THICKNESS [KM]
!     CWDCOL   OUTPUT WATER DROPLET VERTICAL COLUMN DENSITY [KM GM/M3]
!     CIPCOL   OUTPUT ICE PARTICLE VERTICAL COLUMN DENSITY [KM GM/M3]
      REAL CTHICK,CWDCOL,CIPCOL

!     LIST COMMONS:
      INCLUDE 'IFIL.h'

!     /MODEL/
!       ZM       PROFILE BOUNDARY ALTITUDES [KM].
!       PM       PROFILE BOUNDARY PRESSURES [MBAR].
!       TM       PROFILE BOUNDARY TEMPERATURES [K].
!       RFNDX    PROFILE BOUNDARY REFRACTIVITIES.
!       DENSTY   PROFILE BOUNDARY DENSITIES [UNITS DEPEND ON SPECIES].
!       LRHSET   FLAG, TRUE IF RELATIVE HUMIDITY CANNOT BE SCALED.
      REAL ZM,PM,TM,RFNDX,DENSTY
      LOGICAL LRHSET
      COMMON/MODEL/ZM(LAYDIM),PM(LAYDIM),TM(LAYDIM),                    &
     &  RFNDX(LAYDIM),DENSTY(MEXT,LAYDIM),LRHSET(LAYDIM)

!     /CNTRL/
!       IKMAX    NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       ISSGEO   LINE-OF-SIGHT FLAG (0 = SENSOR PATH, 1 = SOLAR PATHS).
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
      INTEGER IKMAX,ML,MLFLX,ISSGEO,IMULT
      COMMON/CNTRL/IKMAX,ML,MLFLX,ISSGEO,IMULT
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT
      INTEGER NCRALT,NCRSPC
      REAL CTHIK,CALT,CEXT,CWAVLN,CCOLWD,CCOLIP,CHUMID,ASYMWD,ASYMIP
      COMMON/CARD2A/CTHIK,CALT,CEXT,NCRALT,NCRSPC,                      &
     &  CWAVLN,CCOLWD,CCOLIP,CHUMID,ASYMWD,ASYMIP

!     /CLDRR/
!       PCLD     CLOUD PROFILE PRESSURE [MB].
      REAL PCLD,ZCLD,CLD,CLDICE,RR
      COMMON/CLDRR/PCLD,ZCLD(1:NZCLD,0:1),CLD(1:NZCLD,0:5),             &
     &  CLDICE(1:NZCLD,0:1),RR(1:NZCLD,0:5)
      INTEGER NBND
      REAL ZCLDRN(NZCLD),DRPWAT(NZCLD),PRTICE(NZCLD),RNPROF(NZCLD)
      COMMON/CLDRN/NBND,ZCLDRN,DRPWAT,PRTICE,RNPROF

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      SAVE /CLDRR/
      EXTERNAL DEVCBD,MDTA

!     LIST LOCAL VARIABLES AND ARRAYS
      INTEGER ICLD0,ICE0,IRAIN0,IZ0,NUMALT,N,NBASE,NTOP,NBNDM1
      REAL CBASE,FACLEN,CMIN,FAC,RNRATE(NZCLD)

!     LIST DATA
      INTEGER MDLCLD(NMDLS)
      CHARACTER*37 CLDTIT(NMDLS)
      DATA MDLCLD/1,2,3,4,5,3,5,5,1,1/
      DATA CLDTIT/'CUMULUS CLOUD                        ',              &
     &            'ALTOSTRATUS CLOUD                    ',              &
     &            'STRATUS CLOUD                        ',              &
     &            'STRATUS/STRATO-CUMULUS CLOUD         ',              &
     &            'NIMBOSTRATUS CLOUD                   ',              &
     &            'STRATUS CLOUD WITH DRIZZLE           ',              &
     &            'NIMBOSTRATUS CLOUD WITH LIGHT RAIN   ',              &
     &            'NIMBOSTRATUS CLOUD WITH MODERATE RAIN',              &
     &            'CUMULUS CLOUD WITH HEAVY RAIN        ',              &
     &            'CUMULUS CLOUD WITH EXTREME RAIN      '/

!     RETURN IF CLOUD/RAIN MODEL 1 THROUGH NMDLS WAS NOT SELECTED
      IF(ICLD.LT.1 .OR. ICLD.GT.NMDLS)RETURN

!     ICLD0    INDEX OF CLOUD/RAIN MODEL WATER DROPLET DENSITY PROFILE
!     ICE0     INDEX OF CLOUD/RAIN MODEL ICE PARTICLE DENSITY PROFILE
!     IRAIN0   INDEX OF CLOUD/RAIN MODEL RAIN RATE PROFILE
!     IZ0      INDEX OF CLOUD/RAIN MODEL ALTITUDE PROFILE
!     NUMALT   NUMBER OF CLOUD/RAIN MODEL PROFILE BOUNDARY ALTITUDES
      IF(NCRALT.LT.2)THEN

!         USE A BUILT-IN CLOUD/RAIN PROFILE DATA
          ICLD0=MDLCLD(ICLD)
          ICE0=1
          IRAIN0=ICLD-5
          IZ0=1
          NUMALT=NZCLD
      ELSE

!         READ USER-DEFINED CLOUD/RAIN PROFILE DATA
          IZ0=0
          ICLD0=0
          ICE0=0
          IRAIN0=0
          NUMALT=NCRALT
          CALL CRUPRF(NCRALT,M_CLOUD_LIQ,M_CLOUD_ICE,NUM_LEVS,Z_CLD_VAL)

      ENDIF

!     NBASE    INDEX OF CLOUD BASE
!     NTOP     INDEX OF CLOUD TOP
      !WRITE(*,*) 'CLD = ',CLD(NUMALT,ICLD0)
      !WRITE(*,*) 'CLDICE = ',CLDICE(NUMALT,ICE0)
      IF(CLD(NUMALT,ICLD0).NE.0. .AND. CLDICE(NUMALT,ICE0).NE.0.)THEN
          WRITE(IPR,'(/2A,/14X,A)')' FATAL ERROR:  CLOUD',              &
     &      ' WATER DROPLET AND ICE PARTICLE PROFILES ARE NOT',         &
     &      ' BOTH ZERO AT THE HIGHEST CLOUD PROFILE ALTITUDE.'
          STOP 'CLOUD PROFILES ARE NOT PROPERLY DEFINED AT TOP.'
      ENDIF
      DO 10 NBASE=1,NUMALT
          IF(CLD(NBASE,ICLD0).GT.0. .OR. CLDICE(NBASE,ICE0).GT.0.)GOTO20
   10 CONTINUE
      WRITE(IPR,'(/A)')                                                 &
     &  ' FATAL ERROR:  NO WATER DROPLETS OR ICE PARTICLES IN CLOUD.'
      WRITE(*,*) 'LAT = ',LATITUDE_INPUT
      WRITE(*,*) 'LON = ',LONGITUDE_INPUT
      STOP 'FATAL ERROR:  NO WATER DROPLETS OR ICE PARTICLES IN CLOUD.'
   20 CONTINUE
      DO 30 NTOP=NUMALT-1,NBASE+1,-1
          IF(CLD(NTOP,ICLD0).GT.0. .OR. CLDICE(NTOP,ICE0).GT.0.)GOTO40
   30 CONTINUE
      WRITE(IPR,'(/A)')                                                 &
     &  ' FATAL ERROR:  CLOUD BASE AND TOP ALTITUDES ARE EQUAL.'
      WRITE(*,*) 'LAT = ',LATITUDE_INPUT
      WRITE(*,*) 'LON = ',LONGITUDE_INPUT
      STOP ' FATAL ERROR:  CLOUD BASE AND TOP ALTITUDES ARE EQUAL.'
   40 CONTINUE

!     PRIORITY FOR INPUT RAIN RATES IS AS FOLLOWS:
!       IF "RAINRT" > 0, USE CONSTANT RAIN RATE FROM GROUND
!       TO CLOUD TOP.  OTHERWISE, USE THE RAIN PROFILE.
      IF(RAINRT.GT.0. .OR. (NCRALT.LT.2 .AND. IRAIN0.LE.0))THEN
          IF(RAINRT.LT.0.)RAINRT=0.
          DO 50 N=1,NTOP
              RNRATE(N)=RAINRT
   50     CONTINUE
      ELSE
          DO 60 N=1,NTOP
              RNRATE(N)=RR(N,IRAIN0)
   60     CONTINUE
      ENDIF
      RNRATE(NTOP+1)=0.

!     CBASE    CLOUD BASE ALTITUDE (RELATIVE TO SEA LEVEL) [KM]
      CBASE=CALT
      IF(CBASE.LT.0.)CBASE=ZCLD(NBASE,IZ0)
      CBASE=CBASE+ZM(1)

!     FACLEN   CLOUD STRETCH/COMPRESSION FACTOR
!     CTHICK   CLOUD THICKNESS [KM]
      FACLEN=1.
      CTHICK=ZCLD(NTOP,IZ0)-ZCLD(NBASE,IZ0)
      IF(CTHIK.GT.0.)THEN
          FACLEN=CTHIK/CTHICK
          CTHICK=CTHIK
      ENDIF

!     NBND     NUMBER OF CLOUD/RAIN PROFILE BOUNDARY ALTITUDES
!     RNPROF   TRUE RAIN RATE PROFILE [MM/HR]
!     DRPWAT   CLOUD WATER DROPLET PROFILE [GM/M3]
!     PRTICE   CLOUD ICE PARTICLE PROFILE [GM/M3]
!     ZCLDRN   CLOUD/RAIN PROFILE ALTITUDES [KM]
!     CWDCOL   CLOUD WATER DROPLET VERTICAL COLUMN DENSITY [KM GM/M3]
!     CIPCOL   CLOUD ICE PARTICLE VERTICAL COLUMN DENSITY [KM GM/M3]
!     CMIN     ALTITUDE NON-ZERO CLOUD DENSITY BEGINS [KM]

!     RAIN/CLOUD PROFILES UP TO AND INCLUDING THE CLOUD BASE
      RNPROF(1)=RNRATE(1)
      NBND=1
      CWDCOL=0.
      CIPCOL=0.
      IF(CBASE.GT.ZM(1))THEN

!         CLOUD BASE IS ABOVE THE GROUND
          ZCLDRN(1)=ZM(1)
          DRPWAT(1)=0.
          PRTICE(1)=0.
          IF(NBASE.EQ.1)THEN

!             ASSUME CLOUD WATER DROPLET AND ICE PARTICLE
!             DENSITIES DROP TO ZERO ONE METER BELOW THE CLOUD
!             BASE SINCE THE FALL OFF WAS NOT EXPLICITLY DICTATED.
              CMIN=CBASE-.001
          ELSE
              CMIN=CBASE+FACLEN*(ZCLD(NBASE-1,IZ0)-ZCLD(NBASE,IZ0))
          ENDIF
          NBND=2
          IF(CMIN.LE.ZM(1))THEN

!             CLOUD BASE IS NEAR THE GROUND.  ASSUME RAIN RATE
!             VARIES LINEARLY FROM CLOUD BASE TO GROUND.
              CMIN=ZM(1)
          ELSEIF(RNRATE(1).LE.0.)THEN

!             CLOUD WITH NO RAIN.  BEGIN PROFILE AT CMIN.
              ZCLDRN(1)=CMIN
          ELSEIF(NBASE.GT.2)THEN

!             CLOUD WITH RAIN.  STRETCH/COMPRESS RAIN
!             PROFILE BETWEEN GROUND AND CMIN.
              FAC=(CMIN-ZM(1))/(ZCLD(NBASE-1,IZ0)-ZCLD(1,IZ0))
              DO 70 NBND=2,NBASE-1
                  ZCLDRN(NBND)=ZM(1)+FAC*(ZCLD(NBND,IZ0)-ZCLD(1,IZ0))
                  DRPWAT(NBND)=0.
                  PRTICE(NBND)=0.
                  RNPROF(NBND)=RNRATE(NBND)
   70         CONTINUE
              NBND=NBASE
          ENDIF
          RNPROF(NBND)=RNRATE(NBASE)
          FAC=.5*(CBASE-ZCLDRN(NBND-1))
          CWDCOL=FAC*CLD(NBASE,ICLD0)
          CIPCOL=FAC*CLDICE(NBASE,ICE0)
      ENDIF
      ZCLDRN(NBND)=CBASE
      DRPWAT(NBND)=CLD(NBASE,ICLD0)
      PRTICE(NBND)=CLDICE(NBASE,ICE0)

!     RAIN/CLOUD PROFILES ABOVE THE CLOUD BASE
      NBNDM1=NBND
      DO 80 N=NBASE+1,NTOP+1
          NBND=NBND+1
          ZCLDRN(NBND)=CBASE+FACLEN*(ZCLD(N,IZ0)-ZCLD(NBASE,IZ0))
          DRPWAT(NBND)=CLD(N,ICLD0)
          PRTICE(NBND)=CLDICE(N,ICE0)
          RNPROF(NBND)=RNRATE(N)
          FAC=.5*(ZCLDRN(NBND)-ZCLDRN(NBNDM1))
          CWDCOL=CWDCOL+FAC*(DRPWAT(NBND)+DRPWAT(NBNDM1))
          CIPCOL=CIPCOL+FAC*(PRTICE(NBND)+PRTICE(NBNDM1))
          NBNDM1=NBND
   80 CONTINUE

!     CHECK THAT CLOUD TOP IS NOT TOO HIGH
      IF(ZCLDRN(NBND).GT.ZM(ML))THEN
          WRITE(IPR,'(/3(A,F8.3))')                                     &
     &      ' FATAL ERROR:  TOP OF CLOUD PROFILE (',ZCLDRN(NBND),       &
     &      'KM) IS ABOVE THE TOP OF THE ATMOSPHERE (',ZM(ML),'KM).'
          STOP
      ENDIF

!     CHECK INPUT CLOUD WATER DROPLET AND ICE PARTICLE COLUMN DENSITIES
      IF(CCOLWD.EQ.0. .AND. CCOLIP.EQ.0.)THEN
          WRITE(IPR,'(/2A,/(10X,A))')' WARNING: ',                      &
     &      ' BOTH THE CLOUD WATER DROPLET AND ICE PARTICLE COLUMN',    &
     &      ' DENSITIES WERE INPUT AS ZERO.  IT IS ASSUMED THAT THE',   &
     &      ' INTENT OF THE USER WAS TO NOT SCALE THE CLOUD COLUMN',    &
     &      ' DENSITIES (I.E. TO USE DEFAULTS).  THEREFORE, THESE',     &
     &      ' INPUTS (CCOLWD AND CCOLIP) HAVE BEEN RESET TO -1.000'
          CCOLWD=-1.
          CCOLIP=-1.
      ENDIF

!     SCALE WATER DROPLET PROFILE TO INPUT VERTICAL COLUMN DENSITY
      IF(CCOLWD.GE.0. .AND. CWDCOL.GT.0.)THEN
          FAC=CCOLWD/CWDCOL
          CWDCOL=CCOLWD
          DO 90 N=1,NBND
              DRPWAT(N)=FAC*DRPWAT(N)
   90     CONTINUE
      ENDIF

!     SCALE ICE PARTICLE PROFILE TO INPUT VERTICAL COLUMN DENSITY
      IF(CCOLIP.GE.0. .AND. CIPCOL.GT.0.)THEN
          FAC=CCOLIP/CIPCOL
          CIPCOL=CCOLIP
          DO 100 N=1,NBND
              PRTICE(N)=FAC*PRTICE(N)
  100     CONTINUE
      ENDIF

!     RETURN IF CLOUD/RAIN PROFILES ARE NOT BE TO OUTPUT
      IF(LJMASS .OR. NPR.GE.1)RETURN

!     WRITE OUT CLOUD/RAIN PROFILES
      IF(NCRALT.GE.2)THEN
          WRITE(IPR,'(//A,4X,A)')'1','USER-DEFINED CLOUD/RAIN PROFILES'
      ELSEIF(RAINRT.LE.0.)THEN
          WRITE(IPR,'(//A,4X,A)')'1',CLDTIT(ICLD)
      ELSE
          N=INDEX(CLDTIT(ICLD0),'CLOUD')+4
          WRITE(IPR,'(//A,4X,2A)')                                      &
     &      '1',CLDTIT(ICLD0)(1:N),' WITH CONSTANT RAIN RATE'
      ENDIF
      WRITE(IPR,'(5X,2A,F12.5,A)')'(CLOUD WATER DROPLET',               &
     &  ' VERTICAL COLUMN DENSITY:',CWDCOL,' KM GM/M3)'
      WRITE(IPR,'(5X,2A,F12.5,A)')'(CLOUD ICE PARTICLE ',               &
     &  ' VERTICAL COLUMN DENSITY:',CIPCOL,' KM GM/M3)'
      WRITE(IPR,'(4(/A),/(I5,4F14.5))')                                 &
     &  ' BOUNDARY                 WATER         ICE',                  &
     &  ' LAYER                    DROPLET       PARTICLE        RAIN', &
     &  ' NUMBER    ALTITUDE       DENSITY       DENSITY         RATE', &
     &  '             (KM)         (GM/M3)       (GM/M3)       (MM/HR)',&
     &  (N,ZCLDRN(N),DRPWAT(N),PRTICE(N),RNPROF(N),N=1,NBND)
      WRITE(IPR,'(/A,//)')' END OF CLOUD/RAIN PROFILES'
      RETURN
      END
