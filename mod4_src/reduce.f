      SUBROUTINE REDUCE(H1,H2,ANGLE,PHI)

!     PARAMETERS:
      INCLUDE 'ERROR.h'

!     ZMAX IS THE HIGHEST LEVEL IN THE ATMOSPHERIC PROFILE STORED IN
!     COMMON /MODEL/.  IF H1 AND/OR H2 ARE GREATER THAN ZMAX, THIS
!     SUBROUTINE REDUCES THEM TO ZMAX AND RESETS ANGLE AND/OR PHI
!     AS NECESSARY.  THIS REDUCTION IS NECESSARY, FOR EXAMPLE, FOR
!     SATELLITE ALTITUDES, BECAUSE (1) THE DENSITY PROFILES ARE POORLY
!     DEFINED ABOVE ZMAX AND (2) THE CALCULATION TIME FOR PATHS ABOVE
!     ZMAX CAN BE EXCESSIVE (E.G. FOR GEOSYNCHRONOUS ALTITUDES).

!     ARGUMENTS:
      DOUBLE PRECISION H1,H2,ANGLE,PHI

!     COMMONS:
      INCLUDE 'IFIL.h'
      REAL RE,ZMAX
      INTEGER IPATH
      COMMON/PARMTR/RE,ZMAX,IPATH

!     BLOCK DATA ROUTINES EXTERNAL:
      EXTERNAL DEVCBD

!     FUNCTIONS:
      DOUBLE PRECISION DPANDX

!     LOCAL VARIABLES:
      DOUBLE PRECISION CPATH,CZMAX,ANGMAX,SH,GAMMA,DPZMAX,DPRE

!     LIST DATA:
      DOUBLE PRECISION DPDEG
      DATA DPDEG/57.2957795131D0/
      DPRE=DBLE(RE)
      DPZMAX=DBLE(ZMAX)
      IF(H1.LE.DPZMAX .AND. H2.LE.DPZMAX)RETURN
      CALL DPFISH(H1,SH,GAMMA)
      CPATH=DPANDX(DPRE+H1,H1,SH,GAMMA)*SIN(ANGLE/DPDEG)
      CALL DPFISH(DPZMAX,SH,GAMMA)
      CZMAX=DPANDX(DPRE+DPZMAX,DPZMAX,SH,GAMMA)
      ANGMAX=DBLE(180.)-ASIN(CPATH/CZMAX)*DPDEG
      IF(H1.GT.DPZMAX)THEN
          H1=DPZMAX
          ANGLE=ANGMAX
      ENDIF
      IF(H2.GT.DPZMAX)THEN
          H2=DPZMAX
          PHI=ANGMAX
      ENDIF
      IF(.NOT.LJMASS .AND. NPR.LE.1)WRITE(IPR,'(//A,/(4X,2A,F10.3,A))') &
     &  ' FROM SUBROUTINE REDUCE: ','ONE OR BOTH OF H1 AND H2 ARE',     &
     &  ' ABOVE THE TOP OF THE ATMOSPHERIC PROFILE ZMAX = ',ZMAX,       &
     &  ' KM AND HAVE BEEN RESET TO ZMAX','ANGLE AND/OR PHI HAVE ALSO', &
     &  ' BEEN RESET TO THE ZENITH ANGLE AT ZMAX = ',ANGMAX,' DEG'
      RETURN
      END
