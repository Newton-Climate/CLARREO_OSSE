      SUBROUTINE WRTBUF(ERCODE)

!     THIS ROUTINE READS WARNINGS AND ERRORS FROM MODTRAN STANDARD
!     OUTPUT FILES AND STORES THEN IN THE ERROR BUFFER.

!     INPUT ARGUMENTS:
!       ERCODE    ERROR CODE (0=SUCCESS, 1=WARNING, 2=FATAL ERROR).
      INTEGER ERCODE

!     HEADER FILES:
      INCLUDE 'PARAMS.h'
      INCLUDE 'ERROR.h'
      INCLUDE 'JMASS.h'

!     COMMONS:
      INCLUDE 'IFIL.h'

!     /SUNFLG/,/SUNNAM/
!       LRDSUN   SOLAR DATA FLAG, TRUE IF IRRADIANCES IN COMMON BLOCK
!                /SOLAR1/ HAVE BEEN MODIFIED FROM THE BLOCK DATA.
!       SUNFIL   NAME OF DEFAULT FILE CONTAINING SOLAR IRRADIANCE DATA.
      LOGICAL LRDSUN
      COMMON/SUNFLG/LRDSUN
      CHARACTER*12 SUNFIL
      COMMON/SUNNAM/SUNFIL

!     /CARD1/
      INTEGER MODEL,ITYPE,IEMSCT,M1,M2,M3,IM,NOPRNT
      LOGICAL MODTRN
      COMMON/CARD1/MODEL,ITYPE,IEMSCT,M1,M2,M3,IM,NOPRNT,MODTRN

!     /JM1A1/
!       DISSTR   CHARACTER STRING USED TO READ IN DISORT LOGICALS.
!       H2OSTR   VERTICAL WATER COLUMN CHARACTER STRING (IF THE
!                FIRST NON-BLANK CHARACTER IS A "G", THE WATER
!                COLUMN IN GM/CM2 FOLLOWS "G"; IF THE FIRST
!                NON-BLANK CHARACTER IS AN "A", THE WATER COLUMN
!                IN ATM-CM AT 273.15K FOLLOWS "A"; OTHERWISE THE
!                STRING CONTAINS A WATER COLUMN SCALING FACTOR).
!       O3STR    VERTICAL OZONE COLUMN CHARACTER STRING (IF THE
!                FIRST NON-BLANK CHARACTER IS A "G", THE OZONE
!                COLUMN IN GM/CM2 FOLLOWS "G"; IF THE FIRST
!                NON-BLANK CHARACTER IS AN "A", THE OZONE COLUMN
!                IN ATM-CM AT 273.15K FOLLOWS "A"; OTHERWISE THE
!                STRING CONTAINS AN OZONE COLUMN SCALING FACTOR).
!       SUNFL2   TOP-OF-ATMOSPHERE SPECTRAL SOLAR IRRADIANCES FILE NAME.
!       BMNAME   NAME OF MOLECULAR BAND MODEL PARAMETERS FILE.
!       FILTNM   NAME OF FILTER RESPONSE FUNCTION FILE.
!       H2OAER   FLAG, TRUE IF DEFAULT AEROSOL PROPERTIES ARE REVISED
!                BASED ON WATER COLUMN SCALING.
!       DATDIR   NAME OF THE MODTRAN DATA DIRECTORY.
      CHARACTER*3 DISSTR
      CHARACTER*10 H2OSTR,O3STR
      CHARACTER*(NAMLEN) SUNFL2,FILTNM,BMNAME
      CHARACTER*1 H2OAER
      CHARACTER*(NAMLEN-12) DATDIR
      COMMON/JM1A1/DISSTR,H2OSTR,O3STR,SUNFL2,                          &
     &  FILTNM,BMNAME,H2OAER,DATDIR

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      SAVE /SUNFLG/,/SUNNAM/
      EXTERNAL DEVCBD

!     FUNCTIONS:
!       NUNIT    RETURNS AN UNUSED FILE UNIT NUMBER.
!       LENSTR   RETURNS STRING LENGTH AFTER TRIMMING LEADING BLANKS.
      INTEGER NUNIT,LENSTR

!     LOCAL VARIABLES:
!       IOS      STATUS OF READ.
!       NBUG     UNIT NUMBER OF DEBUG OUTPUT FILE.
!       ICOUNT   SPECTRAL FREQUENCY COUNTER.
!       LENDAT   LENGTH OF DATDIR STRING.
!       LOPEN    OPEN FILE FLAG.
      INTEGER IOS,NBUG,ICOUNT,LENDAT
      LOGICAL LOPEN

!     DATA:
!       SVSTAT   FILE SAVE STATUS, SET TO 'KEEP' FOR DEBUGGING.
      CHARACTER*6 SVSTAT

!NORM
      DATA SVSTAT/'DELETE'/
!BUG
!BUG  DATA SVSTAT/'KEEP'/

!     CHECK IF SOLAR BLOCK DATA HAS BEEN MODIFIED:
      IF(LRDSUN)THEN

!         SOLAR IRRADIANCE BLOCK DATA WAS OVER-WRITTEN IN PREVIOUS
!         RUN.  THE ORIGINAL IS NOW NEEDED AND MUST BE REGENERATED.
          LENDAT=LENSTR(DATDIR)
          CALL RDSUN(5,DATDIR(1:LENDAT)//SUNFIL,'1',0.)
          LRDSUN=.FALSE.
      ENDIF

!     REWIND STANDARD OUTPUT AND READ IN EACH LINE:
      INQUIRE(IPR,OPENED=LOPEN)
      IF(.NOT.LOPEN)RETURN
      REWIND(IPR)
      READ(IPR,'((A512))',IOSTAT=IOS)(ERRORBUF(LSTERR),LSTERR=0,MAXERR)
      IF(IOS.EQ.0)THEN

!         END-OF-FILE WAS NEVER REACHED:
          LSTERR=MAXERR
          WRITE(ERRORBUF(LSTERR)(1:98),'(A37,I4,A57)')                  &
     &      ' WARNING from WRTBUF:  Only the first',LSTERR,             &
     &      ' lines of messages from modtran are included in ERRORBUF.'
          ERRORBUF(LSTERR)(99:512)=' '
          ERRORCODE=MAX(ERRORCODE,WARNING)
      ELSEIF(IOS.LT.0)THEN

!         END-OF-FILE REACHED:
          IF(LSTERR.LE.0)THEN

!             OUTPUT FILE IS EMPTY:
              LSTERR=-1
              ERRORCODE=ERCODE
          ELSE
              LSTERR=LSTERR-1
              ERRORCODE=MAX(WARNING,ERCODE)
          ENDIF
      ELSE

!         ERROR READING STANDARD OUTPUT:
          WRITE(ERRORBUF(LSTERR)(1:75),'(A38,I4,A33)')                  &
     &      ' ERROR in WRTBUF:  Unable to read line',LSTERR,            &
     &      ' of MODTRAN standard output file.'
          ERRORBUF(LSTERR)(76:512)=' '

!         RETURN WITHOUT DELETING MODTRAN FILES:
          ERRORCODE=FATAL
          RETURN
      ENDIF

!     DELETE ALL OPEN MODTRAN I/O FILES AND CLOSE DATA/SCRATCH FILES:
      INQUIRE(IRD,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IRD,STATUS=SVSTAT)
      INQUIRE(IPR,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IPR,STATUS=SVSTAT)
      INQUIRE(IPU,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IPU,STATUS=SVSTAT)
      INQUIRE(IPR1,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IPR1,STATUS=SVSTAT)
      INQUIRE(ITB,OPENED=LOPEN)
      IF(LOPEN)CLOSE(ITB,STATUS='KEEP')
      INQUIRE(ITBX,OPENED=LOPEN)
      IF(LOPEN)CLOSE(ITBX,STATUS='KEEP')
      INQUIRE(ISCRCH,OPENED=LOPEN)
      IF(LOPEN)CLOSE(ISCRCH)
!DRF      INQUIRE(IPLOT,OPENED=LOPEN)
!DRF      IF(LOPEN)CLOSE(IPLOT,STATUS=SVSTAT)
      INQUIRE(IPUSCR,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IPUSCR,STATUS=SVSTAT)
      INQUIRE(IPUSCN,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IPUSCN,STATUS=SVSTAT)
!DRF      INQUIRE(IPTSCN,OPENED=LOPEN)
!DRF      IF(LOPEN)CLOSE(IPTSCN,STATUS=SVSTAT)
      INQUIRE(IFLUX,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IFLUX,STATUS=SVSTAT)
      INQUIRE(ICR,OPENED=LOPEN)
      IF(LOPEN)CLOSE(ICR,STATUS=SVSTAT)
      INQUIRE(IDBOUT,OPENED=LOPEN)
      IF(LOPEN)CLOSE(IDBOUT,STATUS=SVSTAT)

!     WRITE OUT COMMON BLOCK DATA AND STOP IF DEBUG OPTION IS ON:
      IF(SVSTAT.EQ.'KEEP')THEN

!         DEBUG OPTION IS ON.  OPEN DEBUG DATA FILE.
          NBUG=NUNIT()
          OPEN(NBUG,FILE='DEBUG.DAT',STATUS='UNKNOWN')
          CLOSE(NBUG,STATUS='DELETE')
          OPEN(NBUG,FILE='DEBUG.DAT',STATUS='NEW')

!         WRITE TWO COLUMN PLOT DATA:
          IF(IEMSCT.EQ.0)THEN

!             FREQUENCY [CM-1] VERSUS SPECTRAL TRANSMITTANCE:
              WRITE(NBUG,'((0PF15.0,F15.8))')(FREQS(ICOUNT),            &
     &          TOTTRANS(ICOUNT),ICOUNT=1,OUTPUTCOUNT)
          ELSEIF(IEMSCT.EQ.3)THEN

!             FREQUENCY [CM-1] VERSUS TRANSMITTED SOLAR
!             SPECTRAL IRRADIANCE [W CM-2 / CM-1]:
              WRITE(NBUG,'((0PF15.0,1PE15.5))')(FREQS(ICOUNT),          &
     &          TRANSIRRADCM(ICOUNT),ICOUNT=1,OUTPUTCOUNT)
          ELSE

!             FREQUENCY [CM-1] VERSUS TOTAL SPECTRAL
!             RADIANCE [W CM-2 SR-1 / CM-1].
              WRITE(NBUG,'((0PF15.0,1PE15.5))')(FREQS(ICOUNT),          &
     &          TRADCM(ICOUNT),ICOUNT=1,OUTPUTCOUNT)
          ENDIF
          CLOSE(NBUG,STATUS='KEEP')
          STOP ' Error in WRTBUF:  Debug option is on.'
      ENDIF
      RETURN
      END
