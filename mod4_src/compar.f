      LOGICAL FUNCTION COMPAR(IPARM,IPH,IDAY,ISOURC,PARM1,              &
     &  PARM2,PARM3,PARM4,TIME,G,ANGLEM,IPRMSV,IPHSV,IDAYSV,            &
     &  ISRCSV,PRM1SV,PRM2SV,PRM3SV,PRM4SV,TIMESV,GSV,ANGMSV)

!     PARAMETERS:
      INCLUDE 'ERROR.h'

!     LOGICAL FUNCTION COMPAR IS TRUE IF SOLAR MULTIPLE
!     SCATTERING CALCULATIONS DO NOT HAVE TO BE REDONE.

!     IPARM, IPH, ETC. ARE THE CURRENT SOLARPARAMETERS.  IPRMSV, IPHSV,
!     ETC. ARE THE SOLAR PARAMETERS OF THE IMMEDIATELY PRECEDING RUN.
      INTEGER IPARM,IDAY,IPH,ISOURC,IPRMSV,IPHSV,IDAYSV,ISRCSV
      REAL PARM1,PARM2,PARM3,PARM4,TIME,G,ANGLEM,                       &
     &  PRM1SV,PRM2SV,PRM3SV,PRM4SV,TIMESV,GSV,ANGMSV

!     INITIALIZE TO FALSE
      COMPAR=.FALSE.

!     CHECK PHASE FUNCTION TYPE, EXTRA-TERRESTRIAL SOURCE,
!     DAY OF YEAR AND SOLAR/LUNAR SPECIFICATION FLAG.
      IF(IPH.NE.IPHSV .OR. ISOURC.NE.ISRCSV .OR.                        &
     &  IDAYSV.NE.IDAY .OR. IPRMSV.NE.IPARM)RETURN

!     THE ASYMMETRY FACTOR G IS ONLY USED WHEN THE HENYEY-GREENSTEIN
!     SCATTERING PHASE FUNCTION IS SELECTED, IPH=0.
      IF(IPH.EQ.0 .AND. G.NE.GSV)RETURN

!     THE LUNAR PHASE IS ONLY USED WITH THE LUNAR SOURCE.
      IF(ISOURC.EQ.1 .AND. ANGMSV.NE.ANGLEM)RETURN

!     PARM2 IS ALWAYS USED:
      IF(PARM2.NE.PRM2SV)RETURN

!     CHECK FOR EPHERMERAL SPECIFICATION:
      IF(IPARM.NE.2 .AND. IPARM.NE.12)THEN

!         CHECK PARM1:
          IF(PARM1.NE.PRM1SV)RETURN

!         BRANCH BASED ON SOLAR/LUNAR SPECIFICATION:
          IF(IPARM.EQ.0 .OR. IPARM.EQ.10)THEN

!             CHECK SOLAR/LUNAR LATITUDE AND LONGITUDE:
              IF(PARM3.NE.PRM3SV .OR. PARM4.NE.PRM4SV)RETURN
          ELSEIF(IPARM.EQ.1 .OR. IPARM.EQ.11)THEN

!             CHECK TIME:
              IF(TIME.NE.TIMESV)RETURN
          ELSE

!             INVALID IPARM
              IF(LJMASS)CALL WRTBUF(FATAL)
              STOP 'ERROR IN COMPAR:  Invalid IPARM'
          ENDIF
      ENDIF
      COMPAR=.TRUE.
      RETURN
      END
