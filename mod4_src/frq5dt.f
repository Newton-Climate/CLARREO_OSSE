      SUBROUTINE FRQ5DT(LOOP0,IV)

!     THIS ROUTINE DEFINES THE LAYER INDEPENDENT 5 CM-1 DATA
      LOGICAL LOOP0

!     COMMONS:

!     /CARD1/
      INTEGER MODEL,ITYPE,IEMSCT,M1,M2,M3,IM,NOPRNT
      LOGICAL MODTRN
      COMMON/CARD1/MODEL,ITYPE,IEMSCT,M1,M2,M3,IM,NOPRNT,MODTRN
      COMMON/AABBCC/A1(11),B1(11),C1(11),IBND(11),QA(11),CPS(11)
      COMMON/WNLOHI/IWLH2O(15),IWLO3(6),IWLCO2(11),IWLCO(4),IWLCH4(5),  &
     &  IWLN2O(12),IWLO2(7),IWLNH3(3),IWLNO(2),IWLNO2(4),IWLSO2(5),     &
     &              IWHH2O(15),IWHO3(6),IWHCO2(11),IWHCO(4),IWHCH4(5),  &
     &  IWHN2O(12),IWHO2(7),IWHNH3(3),IWHNO(2),IWHNO2(4),IWHSO2(5)
      COMMON/H2O/CPH2O(3515)
      REAL CPO3
      COMMON/O3/CPO3(447)
      COMMON/UFMIX1/CPCO2(1219)
      COMMON/UFMIX2/CPCO(173),CPCH4(493),CPN2O(704),CPO2(382)
      COMMON/TRACEG/CPNH3(431),CPNO(62),CPNO2(142),CPSO2(226)

!     /FRQ5/
!       O2C127   O2 1.27 MICRON BAND CONTINUUM ABSORPTION.
!       O2C106   O2 1.06 MICRON BAND CONTINUUM ABSORPTION.
      REAL SIGO20,SIGO2A,SIGO2B,O3T0,O3T1,O3T2,RAYSCT,N2CONT,           &
     &  SH2OT0,SH2OT,FH2O,CHNO3,O2HERZ,O2C127,O2C106,CRSNO2,CRSSO2
      COMMON/FRQ5/SIGO20,SIGO2A,SIGO2B,O3T0,O3T1,O3T2,RAYSCT,N2CONT,    &
     &  SH2OT0,SH2OT,FH2O,CHNO3,O2HERZ,O2C127,O2C106,CRSNO2,CRSSO2
      REAL SH2OT1

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      SAVE /H2O/,/O3/,/TRACEG/,/UFMIX1/,/UFMIX2/,/WNLOHI/
      EXTERNAL CPWAT,CPO3BD,CPTRCG,CPUMIX,WVBNRG

!     FUNCTIONS:
!       C4DTA   N2 CONTINUUM DATA
!       C6DTA   RAYLEIGH SPECTRAL SCATTERING COEFFICIENTS [KM-1].
!       HERTDA  O2 HERZBERG BAND.
!       O2MATE  O2 1.27 MICRON CONTINUUM ABSORPTION.
!       O2INF2  O2 1.06 MICRON CONTINUUM ABSORPTION.
!       O3HHT0  O3 HARTLEY-HUGGINS DATA
!       O3HHT1  O3 HARTLEY-HUGGINS DATA
!       O3HHT2  O3 HARTLEY-HUGGINS DATA
      REAL C4DTA,C6DTA,HERTDA,O2MATE,O2INF2,O3HHT0,O3HHT1,O3HHT2
      SAVE INDH2O,INDO3,INDCO2,INDCO,INDCH4,INDN2O,                     &
     &  INDO2,INDNH3,INDNO,INDNO2,INDSO2

!     DATA:
      DATA MNAER/1/
      IF(LOOP0)THEN

!         INITIALIZE FREQUENCY REGION INDEX FOR EACH MOLECULE
          INDH2O=1
          INDO3 =1
          INDCO2=1
          INDCO =1
          INDCH4=1
          INDN2O=1
          INDO2 =1
          INDNH3=1
          INDNO =1
          INDNO2=1
          INDSO2=1
          RETURN
      ENDIF
      V=FLOAT(IV)
      IF(.NOT.MODTRN)THEN
          CPS( 1)=CXDTA(V,IWLH2O,IWHH2O,CPH2O,INDH2O)
          CPS( 2)=CXDTA(V,IWLCO2,IWHCO2,CPCO2,INDCO2)
          CPS( 3)=CXDTA(V,IWLO3 ,IWHO3 ,CPO3 ,INDO3 )
          CPS( 4)=CXDTA(V,IWLN2O,IWHN2O,CPN2O,INDN2O)
          CPS( 5)=CXDTA(V,IWLCO, IWHCO, CPCO, INDCO )
          CPS( 6)=CXDTA(V,IWLCH4,IWHCH4,CPCH4,INDCH4)
          CPS( 7)=CXDTA(V,IWLO2, IWHO2, CPO2, INDO2 )
          CPS( 8)=CXDTA(V,IWLNO, IWHNO, CPNO, INDNO )
          CPS( 9)=CXDTA(V,IWLSO2,IWHSO2,CPSO2,INDSO2)
          CPS(10)=CXDTA(V,IWLNO2,IWHNO2,CPNO2,INDNO2)
          CPS(11)=CXDTA(V,IWLNH3,IWHNH3,CPNH3,INDNH3)
      ENDIF

!     N2 CONTINUUM
      N2CONT=C4DTA(IV)
      CALL ABCDTA(V)

!     H2O CONTINUUM:
      CALL CKD(V,SH2OT0,SH2OT1,FH2O)
      IF(SH2OT0.GT.0.)THEN
          SH2OT=SH2OT1/SH2OT0
      ELSE
          SH2OT=0.
      ENDIF

!     HNO3 ABSORPTION CALCULATION
      CHNO3=0.
      IF(.NOT.MODTRN)CALL HNO3(V,CHNO3)

!     RAYLEIGH SCATTERING COEFFICIENT [KM-1]:
      RAYSCT=C6DTA(V)

!     AEROSOL OPTICAL PROPERTIES:
      CALL AEREXT(V,MNAER)

!     O2 CONTINUUM CONTRIBUTIONS:
      O2HERZ=HERTDA(V)
      O2C127=O2MATE(V)
      O2C106=O2INF2(V)
      CALL O2CONT(V,SIGO20,SIGO2A,SIGO2B)
      IF(V.GE.49600.)CALL SCHRUN(V,CPS(7))

!     DIFFUSE OZONE:
      IF(V.GE.40800.)THEN
          CALL O3UV(V,O3T0)
          O3T1=0.
          O3T2=0.
      ELSEIF(V.GT.24565.)THEN
          O3T0=O3HHT0(V)
          O3T1=O3HHT1(V)
          O3T2=O3HHT2(V)
      ELSE
          CALL O3CHAP(V,O3T0,O3T1,O3T2)
      ENDIF

!     NO2 CROSS-SECTIONS (14000 CM-1 TO 50000 CM-1)
      CALL NO2XS(V,CRSNO2)

!     SO2 CROSS-SECTIONS (14000 CM-1 TO 50000 CM-1)
      CALL SO2XS(V,CRSSO2)
      RETURN
      END
