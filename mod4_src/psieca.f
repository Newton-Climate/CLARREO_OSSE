      SUBROUTINE PSIECA(OLAT,OLONG,SLAT,SLONG,PSI,ECA)

!     THIS ROUTINE CALCULATES THE EARTH CENTER ANGLE AND THE PATH
!     AZIMUTH FROM AN OBSERVER TO A SOURCE POINT GIVEN LATITUDE AND
!     LONGITUDE INFORMATION.

!     DECLARE INPUTS
!       OLAT   OBSERVER LATITUDE [DEG NORTH, -90 TO 90]
!       OLONG  OBSERVER LONGITUDE [DEG WEST, 0 TO 360]
!       SLAT   SOURCE POINT LATITUDE [DEG NORTH, -90 TO 90]
!       SLONG  SOURCE POINT LONGITUDE [DEG WEST, 0 TO 360]
      REAL OLAT,OLONG,SLAT,SLONG

!     DECLARE OUTPUTS
!       PSI    PATH AZIMUTH AT OBSERVER [DEG EAST OF NORTH, -180 TO 180]
!       ECA    EARTH CENTER ANGLE [DEG, 0 TO 180]
      REAL PSI,ECA

!     NOTE:  OLONG AND PSI ARE NOT WELL DEFINED AT THE NORTH (OLAT=90)
!            AND SOUTH (OLAT=-90) POLES.  THIS ROUTINE RETURNS THE
!            VALUE OF PATH AZIMUTH OBTAINED IN THE LIMIT AS |OLAT|
!            APPROACHES 90 FROM BELOW.

!     DECLARE LOCAL VARIABLES
      REAL DEG,SOLAT,COLAT,SSLAT,CSLAT,DLONG,SDLONG,CDLONG,COSECA,X,Y

!     LIST DATA
      DATA DEG/57.29578/

!     TREAT SAME LONGITUDE AS SPECIAL CASE
      IF(OLONG.EQ.SLONG)THEN
          IF(OLAT.LE.SLAT)THEN
              ECA=SLAT-OLAT
              PSI=0.
          ELSE
              ECA=OLAT-SLAT
              PSI=180.
          ENDIF
          RETURN
      ENDIF
      SOLAT=SIN(OLAT/DEG)
      COLAT=COS(OLAT/DEG)
      SSLAT=SIN(SLAT/DEG)
      CSLAT=COS(SLAT/DEG)
      DLONG=(OLONG-SLONG)/DEG
      SDLONG=SIN(DLONG)
      CDLONG=COS(DLONG)
      COSECA=COLAT*CSLAT*CDLONG+SOLAT*SSLAT
      IF(COSECA.GE.1.)THEN
          ECA=0.
      ELSEIF(COSECA.GT.-1.)THEN
          ECA=DEG*ACOS(COSECA)
      ELSE
          ECA=180.
      ENDIF
      X=COLAT*SSLAT-SOLAT*CSLAT*CDLONG
      Y=CSLAT*SDLONG
      IF(X.EQ.0.)THEN
          PSI=90.
          IF(Y.LT.0.)PSI=-90.
      ELSE
          PSI=DEG*ATAN(Y/X)
          IF(X.LT.0)PSI=PSI+180.
          IF(PSI.GT.180.)PSI=PSI-360.
      ENDIF
      RETURN
      END
