      REAL FUNCTION PHASEF(ITYPE,V, ALT, SANGLE, RH)
      REAL ALAM,ALT,ALTB,ANG,COSANG,HENGNS,PF,PF11,PF12,PF21,PF22,      &
     &  PHFA,RH,RHPTS,SANGLE,V,WAVE,YANG1,YANG2
      INTEGER I,I1,IANG1,IANG2,IREG,IREGC,IRH,IRHHI,IRHLO,              &
     &  IWAV1,IWAV2,NN,NN0
      INCLUDE 'PARAMS.h'
      INCLUDE 'ERROR.h'

!     THIS ROUTINE IS A BIT DIFFERENT FROM AND REPLACES
!     THE OLD PHASEF

!     RETURNS THE AEROSOL PHASE FUNCTION FROM THE STORED DATA BASE

!     THE TRUTH TABLE MNUM(27,26) STORED IN COMMON/MNMPHS/
!     IN SUBROUTINE PHSDTA IS QUERIED TO DETERMINE THE PROPER PHASE
!     FUNCTION NEEDED.
!     THE 27 POSITIONS REPRESENT THE 27 SPECIFIC FREQUENCIES SHOWN IN
!     DATA STATEMENT WAVE  .2-40 MICRONS.
!     THE NUMBERS STORED IN THESE 27 POSITIONS REPRESENT THE CORRECT
!     PHASE FUNCTIONS CHOSEN FROM THE DATA STATEMENT PHSFNC'S 1-70
!     POSSIBLE CHOICES.
!     THE 26 DATA STATEMENTS EACH HAVING 27 FREQUENCIES REPRESENT THE
!     FOLLOWING 26 MODELS;
!     1=RURAL     0%RH   2=RURAL    70%RH   3=RURAL    80%RH
!     4=RURAL    99%RH   5=MARITIME  0%RH   6=MARITIME 70%RH
!     7=MARITIME 80%RH   8=MARITIME 99%RH   9=URBAN     0%RH
!     10=URBAN    70%RH  11=URBAN    80%RH  12=URBAN    99%RH
!     13=OCEANIC   0%RH  14=OCEANIC  70%RH  15=OCEANIC  80%RH
!     16=OCEANIC  99%RH  17=TROPOSPH  0%RH  18=TROPOSPH 70%RH
!     19=TROPOSPH 80%RH  20=TROPOSPH 99%RH  21=STRATOSPHERIC
!     22=AGED VOLCANIC   23=FRESH VOLCANIC  24=RADIATION FOG
!     25=ADVECTIVE FOG   26=METEORIC DUST

!     IN THE PRESENT VERSION THE 4 OCEANIC MODELS 13-16
!     ARE NOT UTILIZED.

!     COMMONS:
      INCLUDE 'IFIL.h'

!     COMMON/RCNSTN/
!     PI       THE CONSTANT PI.
!     DEG      NUMBER OF DEGREES IN ONE RADIAN.
!     BIGNUM   MAXIMUM SINGLE PRECISION NUMBER.
!     BIGEXP   MAXIMUM EXPONENTIAL ARGUMENT WITHOUT OVERFLOW.
!     RRIGHT   SMALLEST SINGLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
      REAL PI,DEG,BIGNUM,BIGEXP,RRIGHT
      COMMON/RCNSTN/PI,DEG,BIGNUM,BIGEXP,RRIGHT

!     ITYPE MOD:  ITYPE = 1,2,3 OR 4; DEFINES THE REGION OF THE AEROSOL
!     ITYPE MOD:  IS IT THE BOUNDARY LAYER? TROPOSPHERIC?
!     ITYPE MOD:  STRATOSPHERIC? MID-STRATO TO MESO TO THERMOSPHERIC?
!     ITYPE MOD:  THE ANSWER DEPENDS ON ITYPE.
      INTEGER ITYPE
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT
      COMMON /CARD2D/ IREG(14), ALTB(14), IREGC(14)

!     /AER/
!     THERE ARE "MAER=17" COMPONENTS:
!      1     AEROSOL 1 (NOMINALLY, BOUNDARY LAYER AEROSOL).
!      2     AEROSOL 2 (NOMINALLY, TROPOSPHERIC AEROSOL).
!      3     AEROSOL 3 (NOMINALLY, STRATOSPHERIC AEROSOL).
!      4     AEROSOL 4 (NOMINALLY, VOLCANIC AEROSOL).
!      5     CIRRUS CLOUD.
!      6     CLOUD 1 (NOMINALLY, WATER CLOUD).
!      7     CLOUD 2 (NOMINALLY, ICE CLOUD).
!     8-17   NOVAM (NAVY OCEANIC VERTICAL AEROSOL MODEL) AEROSOL LAYERS.

!       NAER     NUMBER OF ACTIVE AEROSOLS.
!       EXTV     SPECTRAL EXTINCTION (NORMALIZED TO 1 AT 550 NM).
!       ABSV     SPECTRAL ABSORPTION (1-ABSV/EXTV=SCATTERING ALBEDO).
!       ASYV     SPECTRAL LEGENDRE MOMENT (DIVIDED BY 2N+1).
      INTEGER NAER
      REAL EXTV,ABSV,ASYV
      COMMON/AER/NAER,EXTV(MAER),ABSV(MAER),ASYV(MXCMU,MAER)

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      EXTERNAL DEVCBD
      DIMENSION RHPTS(4), WAVE(27), ANG(34)
      DATA ANG/0., 2., 4., 6., 8., 10., 12., 16., 20., 24., 28., 32.,   &
     &     36., 40., 50., 60., 70., 80., 90., 100., 110., 120., 125.,   &
     &     130., 135., 140., 145., 150., 155., 160., 165., 170., 175.,  &
     &     180./
      DATA WAVE/.2, .3, .55, .6943, 1.06, 1.536, 2.0, 2.5, 2.7, 3., 3.2,&
     &     3.39, 5., 6., 7.2, 7.9, 8.7, 9.2, 10.0, 10.59, 12.5, 15.0,   &
     &     17.2, 18.5, 21.3, 30., 40./
      DATA RHPTS/0., 70., 80., 99./
      PHASEF = 0.0
      ALAM = 1.E4/(V+1.E-5)
      IF(SANGLE.LT.0. .OR. SANGLE.GT.180.)THEN
          WRITE(IPR,'(/A,/A,F12.5,A)')                                  &
     &      ' Error in PHASEF:  Scattering angle is out of range.',     &
     &      '                   Scattering angle =',SANGLE,' degrees.'
          IF(LJMASS)CALL WRTBUF(FATAL)
          STOP ' Error in PHASEF:  Scattering angle is out of range.'
      ENDIF
      IF(ALAM.GT.WAVE(27))THEN
         COSANG = COS(SANGLE/DEG)
         PHASEF = HENGNS(ASYV(1,1), COSANG)
         RETURN
      ENDIF

!     ITYPE MOD:  THIS ROUTINE WAS MODIFIED SO THAT
!     ITYPE MOD:  PHASEF CALCULATED IS ITYPE-SPECIFIC

!     INITIALIZE NN
      NN=-99

!     DETERMINE THE AEROSOL MODEL NUMBER
      IF(ALT.LE.ALTB(1).AND.ITYPE.EQ.1)THEN
         IF(IHAZE.EQ.0)RETURN
         IF(IHAZE.EQ.10)THEN
            COSANG = COS(SANGLE/DEG)
            PHASEF = HENGNS(ASYV(1,1), COSANG)
            RETURN
         ENDIF

!        CHECK IF CLOUD,RAIN OR DESERT MODEL IS REQUESTED
         IF(IHAZE.EQ.7)THEN
            COSANG = COS(SANGLE/DEG)
            PHASEF = HENGNS(ASYV(1,1), COSANG)
            RETURN
         ENDIF
         IF(IHAZE.GE.8)THEN
!           NORMALLY 0-2KM FOG MODELS, NO RH DEPENDENCE
            IF(IHAZE.EQ.8)NN = 24
            IF(IHAZE.EQ.9)NN = 25
            GO TO 150
         ELSE

!           NORMALLY 0-2KM BOUNDARY LAYER MODELS, RH DEPENDENT
            DO 110 I1 = 1, 4
               I = I1
               IF(RHPTS(I).EQ.RH)GO TO 130
               IF(RHPTS(I).GT.RH)GO TO 120
 110        CONTINUE
 120        IRHLO = I - 1
            IRHHI = I
            GO TO 140
         ENDIF
 130     IRHLO = I
         IRHHI = I

!        RURAL MODEL
 140     IF(IHAZE.EQ.1 .OR. IHAZE.EQ.2)NN0 = 0

!        MARITIME MODEL
         IF(IHAZE.EQ.3 .OR. IHAZE.EQ.4)NN0 = 4

!        URBAN MODEL
         IF(IHAZE.EQ.5)NN0 = 8

!        TROPOSPHERIC MODEL
         IF(IHAZE.EQ.6)NN0 = 16
         NN = NN0 + IRHLO
      ELSEIF(ITYPE.EQ.2.AND.ALT.LE.ALTB(2))THEN
!        NORMALLY 2-10KM TROPOSPHERIC MODEL
         IF(IHAZE.EQ.7)THEN
            COSANG = COS(SANGLE/DEG)
            PHASEF = HENGNS(ASYV(1,2), COSANG)
            RETURN
         ENDIF
         NN = 18
      ELSEIF(ITYPE.EQ.4.AND.ALT.LE.ALTB(4))THEN
!        NORMALLY 30-100KM METEORIC MODEL
         NN = 26
      ELSEIF(ITYPE.EQ.3.AND.ALT.LE.ALTB(3))THEN
!        NORMALLY 10-30KM STRATOSPHERIC MODELS
!        BACKGROUND MODEL
         IF(IVULCN.EQ.0 .OR. IVULCN.EQ.1)NN = 21
!        AGED VOLCANIC MODEL
         IF(IVULCN.EQ.2 .OR. IVULCN.EQ.4)NN = 22
!        FRESH VOLCANIC
         IF(IVULCN.EQ.3 .OR. IVULCN.EQ.5 .OR. IVULCN.EQ.8)NN = 23
!        BACKGROUND STRATO
         IF(IVULCN.EQ.6 .OR. IVULCN.EQ.7)NN = 21
      ENDIF
 150  IRH = 0

!     DETERMINE THE BOUNDING ANGLE INDICES
      DO 160 I1 = 1, 34
         I = I1
         IF(ANG(I).EQ.SANGLE)GO TO 180
         IF(ANG(I).GT.SANGLE)GO TO 170
 160  CONTINUE
 170  IANG1 = I - 1
      IANG2 = I
      GO TO 190
 180  IANG1 = I
      IANG2 = I

!     DETERMINE THE BOUNDING WAVELENGTH INDICES
 190  DO 200 I1 = 1, 27
         I = I1
         IF(WAVE(I).EQ.ALAM)GO TO 220
         IF(WAVE(I).GT.ALAM)GO TO 210
 200  CONTINUE
 210  IWAV1 = I - 1
      IWAV2 = I
      IF(IWAV1.LT.1)IWAV1 = 1
      GO TO 230
 220  IWAV1 = I
      IWAV2 = I

!     FUNCTION PF CHOOSES DESIRED PHASE FUNCTION FROM LOOK UP TABLE
!     MNUM(IWAV,NN)  WHERE IWAV IS FREQ. AND NN IS MODEL NO.

!     WAVELENGTH INTERPOLATION ONLY USES PF11 AND PF21
!     ANGLE INTERPOLATION ONLY USES PF11 AND PF12
!     WAVELENGTH AND ANGLE INTERPOLATION USES PF11,PF21 AND PF12,PF22.

 230  CONTINUE
      IF(NN.EQ.-99 .AND. PHASEF.EQ.0.)RETURN
      PF11 = PF(NN, IWAV1, IANG1)
      PF21 = PF(NN, IWAV2, IANG1)
      PF12 = PF(NN, IWAV1, IANG2)
      PF22 = PF(NN, IWAV2, IANG2)
!     INTERPOLATE IN WAVELENGTH THEN ANGLE
      IF(IWAV1.EQ.IWAV2)THEN
         IF(IANG1.EQ.IANG2)THEN
!           NO INTERPOLATION IS NECESSARY
            PHASEF=PF(NN,IWAV1,IANG1)
         ELSE
!           ONLY ANGLE INTERPOLATION IS NECESSARY
            CALL INTERP(2,SANGLE,ANG(IANG1),ANG(IANG2),PHASEF,PF11,PF12)
         ENDIF
      ELSEIF(IANG1.EQ.IANG2)THEN
!        ONLY WAVELENGTH INTERPOLATION IS NECESSARY
         CALL INTERP(2,ALAM,WAVE(IWAV1),WAVE(IWAV2),PHASEF,PF11,PF21)
      ELSE
!        BOTH INTERPOLATIONS ARE NECESSARY
         CALL INTERP(2,ALAM,WAVE(IWAV1),WAVE(IWAV2),YANG1,PF11,PF21)
         CALL INTERP(2,ALAM,WAVE(IWAV1),WAVE(IWAV2),YANG2,PF12,PF22)
         CALL INTERP(2,SANGLE,ANG(IANG1),ANG(IANG2),PHASEF,YANG1,YANG2)
      ENDIF

!     HUMIDITY DEPENDENCE - FIRST AEROSOL ONLY (ITYPE=1)
      IF(ITYPE.EQ.1.AND.ALT.LE.ALTB(1).AND.NN.LT.17.AND.IRHLO.NE.IRHHI) &
     &     THEN
         IF(IRH.EQ.0)THEN
            NN = NN0 + IRHHI
            PHFA = PHASEF
            IRH = 1
            GO TO 230
         ENDIF
         PHASEF=PHFA                                                    &
     &     +(RH-RHPTS(IRHLO))*(PHASEF-PHFA)/(RHPTS(IRHHI)-RHPTS(IRHLO))
      ENDIF
      RETURN
      END
