      SUBROUTINE IRFXN(X,RX,RATIO)
      INCLUDE 'PARAMS.h'

!     THIS ROUTINE FINDS INDEX OF REFRACTION AND ITS DERIVATIVE AT X.

!     X = HEIGHT FROM THE SURFACE OF THE EARTH
!     RX = INDEX OF REFRACTION
!     DRX = DERIVATIVE
!     RATIO = RX/DRX

      DOUBLE PRECISION  X, RX, RATIO, XN, H
      INTEGER I, J

!     /MODEL/
!       ZM       PROFILE BOUNDARY ALTITUDES [KM].
!       PM       PROFILE BOUNDARY PRESSURES [MBAR].
!       TM       PROFILE BOUNDARY TEMPERATURES [K].
!       RFNDX    PROFILE BOUNDARY REFRACTIVITIES.
!       DENSTY   PROFILE BOUNDARY DENSITIES [UNITS DEPEND ON SPECIES].
!       LRHSET   FLAG, TRUE IF RELATIVE HUMIDITY CANNOT BE SCALED.
      REAL ZM,PM,TM,RFNDX,DENSTY
      LOGICAL LRHSET
      COMMON/MODEL/ZM(LAYDIM),PM(LAYDIM),TM(LAYDIM),                    &
     &  RFNDX(LAYDIM),DENSTY(MEXT,LAYDIM),LRHSET(LAYDIM)

!     /CNTRL/
!       IKMAX    NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       ISSGEO   LINE-OF-SIGHT FLAG (0 = SENSOR PATH, 1 = SOLAR PATHS).
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
      INTEGER IKMAX,ML,MLFLX,ISSGEO,IMULT
      COMMON/CNTRL/IKMAX,ML,MLFLX,ISSGEO,IMULT

      IF (X .GT. ZM(ML)) THEN
         J = ML
         GO TO 200
      ENDIF
      DO 300 I = 2, ML
         IF (X .LE. ZM(I)) THEN
            J = I
            GO TO 200
         ENDIF
 300  CONTINUE

 200  I = J -1

      H = -DBLE((ZM(J)-ZM(I))/LOG(RFNDX(J)/RFNDX(I)))
      XN = DBLE(RFNDX(I))*EXP(-(X-DBLE(ZM(I)))/H)
      RX = 1 + XN
      RATIO = -(RX*H)/XN
      RETURN
      END
