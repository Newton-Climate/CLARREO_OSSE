      SUBROUTINE LAYCLD(K,EQLWCZ,RRATZ,ICLD1,GNDALT)

!     ROUTINE TO DETERMINE CLOUD DENSITY AND RAIN RATE FOR LAYER K

!     ZM      COMMON /MODEL/ FINAL ALTITUDES FOR LOWTRAN
!     ZK      EFFECTIVE CLOUD ALTITUDES
!     ZCLD    CLOUD ALTITUDE ARRAY

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     COMMONS:

!     /CLDRR/
!       PCLD     CLOUD PROFILE PRESSURE [MB].
      REAL PCLD,ZCLD,CLD,CLDICE,RR
      COMMON/CLDRR/PCLD,ZCLD(1:NZCLD,0:1),CLD(1:NZCLD,0:5),             &
     &  CLDICE(1:NZCLD,0:1),RR(1:NZCLD,0:5)

!     /MODEL/
!       ZM       PROFILE BOUNDARY ALTITUDES [KM].
!       PM       PROFILE BOUNDARY PRESSURES [MBAR].
!       TM       PROFILE BOUNDARY TEMPERATURES [K].
!       RFNDX    PROFILE BOUNDARY REFRACTIVITIES.
!       DENSTY   PROFILE BOUNDARY DENSITIES [UNITS DEPEND ON SPECIES].
!       LRHSET   FLAG, TRUE IF RELATIVE HUMIDITY CANNOT BE SCALED.
      REAL ZM,PM,TM,RFNDX,DENSTY
      LOGICAL LRHSET
      COMMON/MODEL/ZM(LAYDIM),PM(LAYDIM),TM(LAYDIM),                    &
     &  RFNDX(LAYDIM),DENSTY(MEXT,LAYDIM),LRHSET(LAYDIM)

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      SAVE /CLDRR/
      EXTERNAL MDTA
      IF(ICLD1.LE.0 .OR. ICLD1.GT.11)RETURN
      ZK=ZM(K)-GNDALT
      IF(ZK.LT.0.)ZK=0.
      IF(ICLD1.LE.5)THEN

!         ICLD1 IS 1- 5 ONE OF 5 SPECIFIC CLOUD MODELS IS CHOSEN
          MC=ICLD1
          MR=6
      ELSE

!         ICLD1 IS 6-11 ONE OF 5 SPECIFIC CLOUD/RAIN MODELS CHOSEN
          MC=1
          IF(ICLD1.EQ.6)MC=3
          IF(ICLD1.EQ.7 .OR. ICLD1.EQ.8)MC=5
          MR=ICLD1-5
      ENDIF
      EQLWCZ=0.
      RRATZ=0.
      IF(ZK.GE.ZCLD(1,1))THEN
          MKM1=1
          DO 10 MK=2,16
              IF(ZK.LE.ZCLD(MK,1))THEN
                  FAC=(ZCLD(MK,1)-ZK)/(ZCLD(MK,1)-ZCLD(MKM1,1))
                  EQLWCZ=CLD(MK,MC)+FAC*(CLD(MKM1,MC)-CLD(MK,MC))
                  IF(MR.LE.5)RRATZ=RR(MK,MR)+FAC*(RR(MKM1,MR)-RR(MK,MR))
                  RETURN
              ENDIF
   10     MKM1=MK
      ENDIF
      RETURN
      END
