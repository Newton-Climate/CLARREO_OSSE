      DOUBLE PRECISION FUNCTION RTBIS(X1,X2,CPATH)

!     THIS FUNCTION FINDS THE TANGENT HEIGHT BETWEEN X1 AND X2 AS THE
!     ROOT OF FUNC(X) = X * (REFRACTIVE INDEX) - CPATH.  THIS ROUTINE
!     IS FROM "NUMERICAL RECIPES: THE ART OF SCIENTIFIC COMPUTING",
!     W.H. PRESS, B.P. FLANNERY, S.A. TEUKOLSKY AND W.T. VETTERING,
!     (CAMBRIDGE UNIVERSITY PRESS, CAMBRIDGE, 1986).

!     PARAMETERS:
      INCLUDE 'ERROR.h'
      DOUBLE PRECISION X1, X2, CPATH
      DOUBLE PRECISION F,FMID,DX,XMID,XACC,F1,F2,RATIO,DPRE
      INTEGER J, JMAX
      REAL RE,ZMAX
      INTEGER IPATH
      COMMON/PARMTR/RE,ZMAX,IPATH
      PARAMETER (JMAX=40)
      DATA XACC/1.D-5/
      DPRE=DBLE(RE)
      CALL IRFXN(X2,FMID, RATIO)
      FMID = FMID*(X2+DPRE)-CPATH
      CALL IRFXN(X1,F, RATIO)
      F = F*(X1+DPRE)-CPATH
      IF(F*FMID.GT.0.) THEN
          IF(LJMASS)CALL WRTBUF(FATAL)
          STOP 'ERROR in RTBIS:  Root must be bracketed for bisection.'
      ENDIF
      IF(F.LT.0.)THEN
         RTBIS=X1
         DX=X2-X1
      ELSE
         RTBIS=X2
         DX=X1-X2
      ENDIF
      DO 11 J=1,JMAX
         DX=DX/2
         XMID=RTBIS+DX
         CALL IRFXN(XMID,FMID, RATIO)
         FMID = FMID*(XMID+DPRE)-CPATH
         IF(FMID.LE.0.)RTBIS=XMID
         IF(ABS(DX).LT.XACC .OR. FMID.EQ.0.) RETURN
 11   CONTINUE

!     COMES HERE IF UNABLE TO SOLVE.

      CALL IRFXN(X2,F2, RATIO)
      F2 = F2*(X2+DPRE)-CPATH
      CALL IRFXN(X1,F1, RATIO)
      F1 = F1*(X1+DPRE)-CPATH
      IF (ABS(F2) .LT. ABS(F1)) THEN
         RTBIS = X2
      ELSE
         RTBIS = X1
      ENDIF
      END
