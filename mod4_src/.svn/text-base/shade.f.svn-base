      SUBROUTINE SHADE(IPH,IK,MSOFF,IPATH,KNTRVL,V,                     &
     &  PFMOL0,PFMOL2,TSNOBS,TSNREF,SUMSSS)

!     ROUTINE SHADE SETS THE SOLAR TRANSMISSION TERMS TO ZERO FOR
!     SHADED SCATTERING POINTS

!     ARGUMENTS:
!       PFMOL0  MOLECULAR PHASE FUNCTION ISOTROPIC SCATTERING TERM.
!       PFMOL2  MOLECULAR PHASE FUNCTION ANISOTROPIC SCATTERING TERM.
      INTEGER IPH,IK,MSOFF,IPATH,KNTRVL
      REAL V,PFMOL0,PFMOL2,TSNOBS,TSNREF,SUMSSS
      INCLUDE 'PARAMS.h'

!     COMMON /CORKDT/
!       WTKSUB   SPECTRAL BIN SUB-INTERVAL FRACTIONAL WIDTHS.
!       DEPLAY   INCREMENTAL EXTINCTION OPTICAL DEPTHS
!       TRNLAY   INCREMENTAL TRANSMITTANCES
!       TRNCUM   CUMULATIVE TRANSMITTANCES
!       K2TAIL   POINTER FROM K BIN TO LINE TAIL SUB-BIN
!                (=0 IF MULTIPLE LINE TAIL SUB-BINS CONTRIBUTE).
!       CONTWT   WEIGHTS FOR PARTITIONING LINE TAILS INTO K'S
!                (ONLY USED IF K2TAIL IS 0).
      REAL WTKSUB,DEPLAY,TRNLAY,TRNCUM,CONTWT
      INTEGER K2TAIL
      COMMON/CORKDT/WTKSUB(MXKSUB),DEPLAY(MXKSUB),TRNLAY(MXKSUB),       &
     &  TRNCUM(MXKSUB),K2TAIL(MXKSUB),CONTWT(MTLSUB,MXKSUB)
      SAVE /CORKDT/

!     COMMON/RCNSTN/
!       PI       THE CONSTANT PI
!       DEG      NUMBER OF DEGREES IN ONE RADIAN.
!       BIGNUM   MAXIMUM SINGLE PRECISION NUMBER.
!       BIGEXP   MAXIMUM EXPONENTIAL ARGUMENT WITHOUT OVERFLOW.
!       RRIGHT   SMALLEST SINGLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
      REAL PI,DEG,BIGNUM,BIGEXP,RRIGHT
      COMMON/RCNSTN/PI,DEG,BIGNUM,BIGEXP,RRIGHT

!     /MSRD/
!       CSSCAT   COSINE OF THE SCATTERING ANGLE.
!                (AT H1 IF IMULT=1; AT OR "NEAR" H2 IF IMULT=-1)
!       SLEGEN   Nth LEGENDRE POLYNOMIAL EVALUATED AT THE COSINE OF THE
!                SCATTERING ANGLE TIMES (2N+1)/4pi (N=0 TO NSTR-1).
!       CSZEN0   LAYER BOUNDARY COSINE OF SOLAR/LUNAR ZENITH.
!       CSZEN    LAYER AVERAGE COSINE OF SOLAR/LUNAR ZENITH.
!       CSZENX   AVERAGE SOLAR/LUNAR COSINE ZENITH EXITING
!                (AWAY FROM EARTH) THE CURRENT LAYER.
!       BBGRND   THERMAL EMISSION (FLUX) AT THE GROUND [W CM-2 / CM-1].
!       BBNDRY   LAYER BOUNDARY THERMAL EMISSION (FLUX) [W CM-2 / CM-1].
!       TCONT    LAYER CONTINUUM OPTICAL DEPTH.
!       TAUT     LAYER TOTAL OPTICAL DEPTH.
!       GTSCAT   SUM OVER SCATTERING SOURCES OF SCATTERING OPTICAL DEPTH
!                AND PHASE FUNCTION LEGENDRE COEFFICIENT PRODUCTS.
!       COSBAR   LAYER EFFECTIVE SCATTERING ASYMMETRY FACTOR.
!       DEPRAT   FRACTIONAL DECREASE IN WEAK-LINE OPTICAL DEPTH TO SUN.
!       S0DEP    OPTICAL DEPTH FROM LAYER BOUNDARY TO SUN.
!       S0TRN    TRANSMITTED SOLAR IRRADIANCES [W CM-2 / CM-1]
!       UPF      LAYER BOUNDARY UPWARD THERMAL FLUX [W CM-2 / CM-1].
!       DNF      LAYER BOUNDARY DOWNWARD THERMAL FLUX [W CM-2 / CM-1].
!       UPFS     LAYER BOUNDARY UPWARD SOLAR FLUX [W CM-2 / CM-1].
!       DNFS     LAYER BOUNDARY DOWNWARD SOLAR FLUX [W CM-2 / CM-1].
      REAL CSSCAT,SLEGEN,CSZEN0,CSZEN,CSZENX,TCONT,TAUT,GTSCAT,COSBAR,  &
     &  BBGRND,BBNDRY,S0DEP,S0TRN,DEPRAT,UPF,DNF,UPFS,DNFS
      COMMON/MSRD/CSSCAT,SLEGEN(0:MAZ),                                 &
     &  CSZEN0(LAYDIM),CSZEN(LAYDIM),CSZENX(LAYDIM),TCONT(LAYDIM),      &
     &  TAUT(MXKSUB,LAYDIM),GTSCAT(0:MXCMU,1:LAYDIM),COSBAR(LAYDIM),    &
     &  BBGRND,BBNDRY(LAYDIM),S0DEP(MXKSUB,LAYTWO),S0TRN(MXKSUB,LAYTWO),&
     &  DEPRAT(MXKSUB,LAYDIM),UPF(MXKSUB,LAYDIM),DNF(MXKSUB,LAYDIM),    &
     &  UPFS(MXKSUB,LAYDIM),DNFS(MXKSUB,LAYDIM)

!     DECLARE LOCAL VARIABLES
      INTEGER INTRVL
      REAL S0,TMOL,BIGDEP
      S0=0.
      IF(KNTRVL.EQ.1)THEN
          TMOL=0.
          CALL SSRAD(IPH,IK,MSOFF,IPATH,V,S0,                           &
     &      PFMOL0,PFMOL2,TMOL,TSNOBS,TSNREF,SUMSSS)
      ELSE

!         DIVIDE BIGNUM BY THE LAYER NUMBER TO ARTIFICIALLY REQUIRE
!         THAT OPTICAL DEPTH DECREASES WITH INCREASING LAYER NUMBER.
          BIGDEP=BIGNUM/IK
          DO 10 INTRVL=1,KNTRVL
              TRNLAY(INTRVL)=0.
              DEPLAY(INTRVL)=BIGDEP
   10     CONTINUE
          IF(MSOFF.GT.0)THEN

!             FOR MULTIPLE SCATTERING, STORE THE DECREASE IN WEAK-LINE
!             OPTICAL DEPTH TO THE SUN ACROSS THE CURRENT LAYER.
              IF(IPATH.EQ.1)THEN
                  DO 20 INTRVL=1,KNTRVL
                      DEPRAT(INTRVL,IK)=BIGDEP
   20             CONTINUE
              ELSE
                  DO 30 INTRVL=1,KNTRVL
                      DEPRAT(INTRVL,IK+1)=BIGDEP
                      DEPRAT(INTRVL,IK)=1.-BIGDEP/DEPRAT(INTRVL,IK)
   30             CONTINUE
              ENDIF
          ENDIF
          CALL SSCORK(IPH,IK,MSOFF,IPATH,KNTRVL,V,S0,                   &
     &      PFMOL0,PFMOL2,TSNOBS,SUMSSS)
      ENDIF
      RETURN
      END
