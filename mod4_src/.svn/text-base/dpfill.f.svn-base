      SUBROUTINE DPFILL(HA,HB,JNEXT)
!********************************************************************
!     THIS SUBROUTINE DEFINES THE ATMOSPHERIC BOUNDARIES OF THE PATH
!     FROM HA TO HB AND INTERPOLATES (EXTRAPOLATES) THE DENSITIES TO
!     THESE BOUNDARIES ASSUMING THE DENSITIES VARY EXPONENTIALLY
!     WITH HEIGHT
!********************************************************************
      INTEGER I,J,JNEXT,IA,IB,I2,K,I1
      LOGICAL LSMALL
      INCLUDE 'PARAMS.h'
      INCLUDE 'ERROR.h'
      DOUBLE PRECISION HA,HB,Z(LAYDIM),P(LAYDIM),T(LAYDIM),             &
     &  DRFNDX(LAYDIM),DDNSTY(MEXT,LAYDIM),A

!     PARAMETER MEXT DENOTES THE NUMBER OF MODTRAN "SPECIES".
!     THIS INCLUDES THE 12 ORIGINAL BAND MODEL PARAMETER MOLECULES
!     PLUS A HOST OF OTHER ABSORPTION AND/OR SCATTERING SOURCES.

      DOUBLE PRECISION DI1X, DI2X, DJNXTX

      COMMON /MODELX/ DNSTYX(NMOLX,LAYDIM)
      COMMON /RFRPTX/ DENPX(NMOLX,LAYDIM+1),AMTPX(NMOLX,LAYDIM+1)

!     SSI COMMENTS ON DOUBLE PRECISION VARIABLES:
!     RFRPTH IS THE OLD COMMON BLOCK IN SINGLE PRECISION.
!     DPRFRP IS THE SAME COMMON BLOCK IN DOUBLE PRECISION; IT IS NEW.
!     IN THIS ROUTINE SP IS USED AS A PREFIX TO DENOTE THE
!     SINGLE PRECISION VARIABLES OF RFRPTH.
!     THE FOLLOWING ARE THE EXCEPTIONS:
!     SPPPSM STANDS FOR THE OLD SINGLE PRECISION PPSUM
!     SPTPSM STANDS FOR THE OLD SINGLE PRECISION TPSUM
!     SPRHOP STANDS FOR THE OLD SINGLE PRECISION RHOPSM
!     THE VARIABLES OF THE DOUBLE PRECISION BLOCK DPRFRP HAVE THE
!     SAME NAMES AS THOSE OF THE ORIGINAL SINGLE PRECISION BLOCK;
!     THAT IS, WITHOUT ANY PREFIXES.

!     VARIABLES IN THE COMMON BLOCK /MODEL/ ARE TEMPORARILY STORED
!     IN DOUBLE PRECISION ARRAYS FOR DPFILL CALCULATIONS:
!       ZM => Z, PM => P, TM => T, RFNDX => DRFNDX & DENSTY => DDNSTY

!     SOME OTHER VARIABLES WERE DECLARED IN DOUBLE PRECISION.
!     THEIR SP COUNTERPARTS WERE PREFIXED WITH "SP".
!     SINCE THESE DO NOT INVOLVE COMMON BLOCKS NOTHING MORE ABOUT
!     THEM IS SAID.  SEE THE DECLARATIONS ABOVE FOR THE SPECIFIC
!     VARIABLES.
      INCLUDE 'IFIL.h'

!     /MODEL/
!       ZM       PROFILE BOUNDARY ALTITUDES [KM].
!       PM       PROFILE BOUNDARY PRESSURES [MBAR].
!       TM       PROFILE BOUNDARY TEMPERATURES [K].
!       RFNDX    PROFILE BOUNDARY REFRACTIVITIES.
!       DENSTY   PROFILE BOUNDARY DENSITIES [UNITS DEPEND ON SPECIES].
!       LRHSET   FLAG, TRUE IF RELATIVE HUMIDITY CANNOT BE SCALED.
      REAL ZM,PM,TM,RFNDX,DENSTY
      LOGICAL LRHSET
      COMMON/MODEL/ZM(LAYDIM),PM(LAYDIM),TM(LAYDIM),                    &
     &  RFNDX(LAYDIM),DENSTY(MEXT,LAYDIM),LRHSET(LAYDIM)

!     /CNTRL/
!       IKMAX    NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       ISSGEO   LINE-OF-SIGHT FLAG (0 = SENSOR PATH, 1 = SOLAR PATHS).
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
      INTEGER IKMAX,ML,MLFLX,ISSGEO,IMULT
      COMMON/CNTRL/IKMAX,ML,MLFLX,ISSGEO,IMULT

!     /RFRPTH/
      REAL SPZP,SPPP,SPTP,SPPPSM,SPTPSM,SPRHOP,SPDENP,SPAMTP,DRANGE
      COMMON/RFRPTH/SPZP(LAYDIM+1),SPPP(LAYDIM+1),SPTP(LAYDIM+1),       &
     &  SPPPSM(LAYDIM+1),SPTPSM(LAYDIM+1),SPRHOP(LAYDIM+1),             &
     &  SPDENP(MEXT,LAYDIM+1),SPAMTP(MEXT,LAYDIM+1),DRANGE(LAYDIM+1)
      DOUBLE PRECISION ZP,PP,TP,RFNDXP,PPSUM,TPSUM,RHOPSM,DENP,AMTP
      COMMON/DPRFRP/ZP(LAYDIM+1),PP(LAYDIM+1),TP(LAYDIM+1),             &
     &  RFNDXP(LAYDIM+1),PPSUM(LAYDIM+1),TPSUM(LAYDIM+1),               &
     &  RHOPSM(LAYDIM+1),DENP(MEXT,LAYDIM+1),AMTP(MEXT,LAYDIM+1)
      COMMON /SMALL2/LSMALL

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      EXTERNAL DEVCBD
      DO 9 I=1, LAYDIM
         Z(I)=DBLE(ZM(I))
         P(I)=DBLE(PM(I))
         T(I)=DBLE(TM(I))
         DRFNDX(I)=DBLE(RFNDX(I))
         DO 9999 J=1, MEXT
            DDNSTY(J,I)=DBLE(DENSTY(J,I))
 9999    CONTINUE
!        FOR CFC'S, WE ALWAYS USE SINGLE PRECISION VARIABLES, SO NO
!        NEED FOR THE ABOVE LOOP FOR CFC'S.

 9    CONTINUE

      IF(HA.GE.HB .AND.    (.NOT. LSMALL )) THEN
         WRITE(IPR,22) HA,HB,JNEXT
 22      FORMAT('0SUBROUTINE DPFILL- ERROR, HA .GE. HB',//,             &
     &        10X,'HA, HB, JNEXT = ',2E25.15,I6)
         IF(LJMASS)CALL WRTBUF(FATAL)
         STOP
      ENDIF
!***  FIND Z(IA):  THE SMALLEST Z.GT.HA
      DO 100 IA=1,ML
         IF(HA.LT.Z(IA))GOTO110
 100  CONTINUE
      IA=ML+1
      IB=IA
      GO TO 130
!***  FIND Z(IB):  THE SMALLEST Z.GE.HB
 110  CONTINUE
      DO 120 IB=IA,ML
         IF(HB-Z(IB).LE..0001)GOTO130
 120  CONTINUE
      IB=ML+1
 130  CONTINUE
!***  INTERPOLATE DENSITIES TO HA,HB
      ZP(JNEXT)=HA
      I2=IA
      IF(I2.EQ.1) I2=2
      IF(I2.GT.ML) I2=ML
      I1=I2-1
      A=(HA-Z(I1))/(Z(I2)-Z(I1))
      CALL DPEXNT(PP(JNEXT),P(I1),P(I2),A)
      TP(JNEXT)=T(I1)+(T(I2)-T(I1))*A
      CALL DPEXNT(RFNDXP(JNEXT),DRFNDX(I1),DRFNDX(I2),A)
      DO 140 K=1,MEXT
          IF(K.EQ.66 .OR. K.EQ.67 .OR. K.EQ.16)THEN

!             LINEARLY INTERPOLATE CLOUD DENSITIES
              DENP(K,JNEXT)=DDNSTY(K,I1)+A*(DDNSTY(K,I2)-DDNSTY(K,I1))
          ELSE

!             EXPONENTIALLY INTERPOLATE
              CALL DPEXNT(DENP(K,JNEXT),DDNSTY(K,I1),DDNSTY(K,I2),A)
          ENDIF
 140  CONTINUE

      DO 145 KX=1,NMOLX
!        STORE SINGLE PRECISION VARIABLES IN DOUBLE PRECISION.
         DI1X=DBLE(DNSTYX(KX,I1))
         DI2X=DBLE(DNSTYX(KX,I2))
         CALL DPEXNT(DJNXTX,DI1X,DI2X,A)
!        BACK TO SINGLE PRECISION
         DENPX(KX,JNEXT)=REAL(DJNXTX)
 145  CONTINUE

      IF(IA.NE.IB) THEN
!***     FILL IN DENSITIES BETWEEN HA AND HB
         I1=IA
         I2=IB-1
         DO 151 I=I1,I2
            JNEXT=JNEXT+1
            ZP(JNEXT)=Z(I)
            PP(JNEXT)=P(I)
            TP(JNEXT)=T(I)
            RFNDXP(JNEXT)=DRFNDX(I)
            DO 150 K=1,MEXT
               DENP(K,JNEXT)=DDNSTY(K,I)
 150        CONTINUE

            DO 155 KX=1,NMOLX
               DENPX(KX,JNEXT)=DNSTYX(KX,I)
 155        CONTINUE

 151     CONTINUE
      ENDIF
!***  INTERPOLATE THE DENSITIES TO HB
      JNEXT=JNEXT+1
      ZP(JNEXT)=HB
      I2=IB
      IF(I2.EQ.1) I2=2
      IF(I2.GT.ML) I2=ML
      I1=I2-1
      A=(HB-Z(I1))/(Z(I2)-Z(I1))
      CALL DPEXNT(PP(JNEXT),P(I1),P(I2),A)
      TP(JNEXT)=T(I1)+(T(I2)-T(I1))*A
      CALL DPEXNT(RFNDXP(JNEXT),DRFNDX(I1),DRFNDX(I2),A)
      DO 170 K=1,MEXT
          IF(K.EQ.66 .OR. K.EQ.67 .OR. K.EQ.16)THEN

!             LINEARLY INTERPOLATE CLOUD DENSITIES
              DENP(K,JNEXT)=DDNSTY(K,I1)+A*(DDNSTY(K,I2)-DDNSTY(K,I1))
          ELSE

!             EXPONENTIALLY INTERPOLATE
              CALL DPEXNT(DENP(K,JNEXT),DDNSTY(K,I1),DDNSTY(K,I2),A)
          ENDIF
 170  CONTINUE

      DO 175 KX=1,NMOLX
!        STORE SINGLE PRECISION VARIABLES IN DOUBLE PRECISION.
         DI1X=DBLE(DNSTYX(KX,I1))
         DI2X=DBLE(DNSTYX(KX,I2))
         CALL DPEXNT(DJNXTX, DI1X, DI2X, A)
!        BACK TO SINGLE PRECISION
         DENPX(KX,JNEXT)=REAL(DJNXTX)
 175  CONTINUE

      DO 1 I=1, LAYDIM
         ZM(I)=REAL(Z(I))
         PM(I)=REAL(P(I))
         TM(I)=REAL(T(I))
         RFNDX(I)=REAL(DRFNDX(I))
         DO 1111 J=1, MEXT
            DENSTY(J,I)=REAL(DDNSTY(J,I))
 1111    CONTINUE
!        NO NEED FOR A LOOP CORRESPONDING TO ABOVE FOR CFC'S.
 1    CONTINUE
      DO 11 I=1, LAYDIM+1
         SPZP(I)=REAL(ZP(I))
         SPPP(I)=REAL(PP(I))
         SPTP(I)=REAL(TP(I))
         DO 111 J=1, MEXT
            SPDENP(J,I)=REAL(DENP(J,I))
 111     CONTINUE
!        NO NEED FOR A LOOP CORRESPONDING TO ABOVE FOR CFC'S.
 11   CONTINUE
      RETURN
      END
