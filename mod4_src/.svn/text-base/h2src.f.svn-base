      SUBROUTINE H2SRC(BETAH2,IPH,PARM1,PARM2,                          &
     &  PARM3,PARM4,PSIPO,G,MSOFF,ICH1,KNTRVL,SOLAR_ZENITH)
      INCLUDE 'ERROR.h'

!     THIS ROUTINE CALLS SSGEO FOR A VERTICAL PATH AT H2.

!     DECLARE INPUTS/OUTPUTS
!       BETAH2   EARTH CENTER ANGLE BETWEEN H1 AND H2
!       IPH      PHASE FUNCTION FLAG
!       PARM1    OBSERVER LATITUDE [DEG NORTH OF EQUATOR]
!       PARM2    OBSERVER LONGITUDE [DEG WEST OF GREENWICH]
!       PARM3    SOLAR LATITUDE [DEG NORTH OF EQUATOR]
!       PARM4    SOLAR LONGITUDE [DEG WEST OF GREENWICH]
!       PSIPO    PATH TRUE AZIMUTH ANGLE [DEG EAST OF NORTH]
!       G        HENYEY-GREENSTEIN ASYMMETRY FACTOR (IPH=0)
!       MSOFF    OFFSET FOR MULTIPLE SCATTERING PATH ARRAYS
!       ICH1     HAZE MODEL FLAG
!       KNTRVL   NUMBER OF WEIGHTS IN CORRELATED-K APPROACH
      REAL BETAH2,PARM1,PARM2,PARM3,PARM4,PSIPO,G
      INTEGER IPH,MSOFF,ICH1,KNTRVL
      REAL SOLAR_ZENITH

!     INCLUDE COMMONS
      INCLUDE 'IFIL.h'

!     DECLARE BLOCK DATA ROUTINES EXTERNAL:
      EXTERNAL DEVCBD

!     DECLARE LOCAL VARIABLES
      REAL PRM1SV,PRM2SV,PSISAV
      INTEGER IERROR

!     SAVE SOLAR PATH GEOMETRY VARIABLES
      PRM1SV=PARM1
      PRM2SV=PARM2
      PSISAV=PSIPO

!     WRITE OUT HEADER
      IF(.NOT.LJMASS) WRITE(IPR,'(/2A)')' THE MULTIPLE SCATTERING FLUX',&
     &  ' CALCULATIONS ARE BASED ON A VERTICAL PATH AT H2:'

!     DETERMINE LATITUDE, LONGITUDE AND PATH AZIMUTH AT H2.
      CALL LOCATE(PRM1SV,PRM2SV,PSISAV,BETAH2,PARM1,PARM2)
      CALL PSIECA(PARM1,PARM2,PRM1SV,PRM2SV,PSIPO,BETAH2)
      PSIPO=PSIPO+180.
      IF(PSIPO.GE.360.)PSIPO=PSIPO-360.
      IF(.NOT.LJMASS) WRITE(IPR,'((5X,A,F12.5))')                       &
     &  ' OBSERVER LATITUDE AT H2 [DEG NORTH OF EQUATOR]  ',PARM1,      &
     &  ' OBSERVER LONGITUDE AT H2 [DEG WEST OF GREENWICH]',PARM2,      &
     &  ' OBSERVER PATH AZIMUTH AT H2 [DEG EAST OF NORTH] ',PSIPO

!     CALL SSGEO
      IERROR=0
      CALL SSGEO(IERROR,IPH,PARM1,PARM2,PARM3,PARM4,                    &
     &  PSIPO,G,MSOFF,ICH1,KNTRVL,SOLAR_ZENITH)

!     RETURN SAVED GEOMETRY VARIABLES
      PARM1=PRM1SV
      PARM2=PRM2SV
      PSIPO=PSISAV

!     RETURN TO DRIVER
      RETURN
      END
