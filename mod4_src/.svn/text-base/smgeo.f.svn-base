      SUBROUTINE SMGEO(SPANGL,SPBETA,SPPHI,                             &
     &  DHALFR,DPRNG2,BENDNG,LENN,SMMIN)
      REAL SPANGL,SPPHI,BENDNG,SPBETA,AORIG
      DOUBLE PRECISION DHALFR,DPRNG2,X,Y,BETA,RANGE,H1,H2,              &
     &  DPDEG,PHI,ANGLE,SMMIN,COSPHI,COSANG,DPRE
      INTEGER LENN
      REAL RE,ZMAX
      INTEGER IPATH
      COMMON/PARMTR/RE,ZMAX,IPATH
      COMMON /SMALL4/H1, H2, RANGE

!     AORIG IS THE ORIGINAL INPUT ANGLE IN SINGLE PRECISION
      COMMON /SMALL5/AORIG
      DATA DPDEG/57.2957795131D0/
      DPRE=DBLE(RE)
      X=H2-H1
      Y=(RANGE+X)*(RANGE-X)
      IF(Y.LT.0.)Y=DBLE(0.)
      BETA=2*DPDEG*ASIN(SQRT(Y/((H1+DPRE)*(H2+DPRE)))/2)

!     IF H2 > H1, CALCULATE PHI (>90 DEG) FROM THE COSINE LAW
!     AND ANGLE FROM SUBTRACTION;  IF H1 > H2, CALCULATE ANGLE
!     (>90 DEG) FROM THE COSINE LAW AND PHI FROM SUBTRACTION.
      IF(X.GE.0.)THEN
         COSPHI=-(RANGE**2+X*(H1+H2+2*DPRE))/(2*RANGE*(H2+DPRE))
         PHI=DBLE(180.)
         IF(COSPHI.GT.-1.)PHI=DPDEG*ACOS(COSPHI)
         ANGLE=DBLE(180.)+BETA-PHI
      ELSE
         COSANG=-(RANGE**2-X*(H1+H2+2*DPRE))/(2*RANGE*(H1+DPRE))
         ANGLE=DBLE(180.)
         IF(COSANG.GT.-1.)ANGLE=DPDEG*ACOS(COSANG)
         PHI=DBLE(180.)+BETA-ANGLE
      ENDIF
      BENDNG=0.
      IF(ANGLE.GT.90.D0 .AND. PHI.GT.90.D0 .AND.                        &
     &  (AORIG.GT.90.D0 .OR. AORIG.EQ.0.))THEN
!        THE CONDITION ON AORIG IS TO MAKE SURE THAT PATHS WHOSE
!        ORIGINAL INPUT ZENITH IS 90 DEGREES OR LESS ARE TREATED AS
!        LENN=0 PATHS.  CALCULATED ANGLE WILL LIKELY
!        DIFFER FROM AORIG.
         LENN=1
         IF(H1.LE.H2)THEN
             DHALFR=(H1+DPRE)*COS((DBLE(180.)-ANGLE)/DPDEG)
         ELSE
             DHALFR=(H2+DPRE)*COS((DBLE(180.)-PHI)/DPDEG)
         ENDIF
         DPRNG2=RANGE-2*DHALFR
         IF (DPRNG2 .LE. 1.0D-06) THEN
            DPRNG2=DBLE(0.)
            DHALFR=RANGE/2
         ENDIF
         SMMIN=(DPRE+H1)*SIN((DBLE(180.)-ANGLE)/DPDEG)-DPRE
      ELSE
         LENN=0
         DHALFR=DBLE(0.)
         DPRNG2=RANGE
         SMMIN=MIN(H1,H2)
      ENDIF
      SPANGL=REAL(ANGLE)
      SPBETA=REAL(BETA)
      SPPHI=REAL(PHI)
      RETURN
      END
