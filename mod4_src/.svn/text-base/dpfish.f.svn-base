      SUBROUTINE DPFISH(H,SH,GAMMA)

!     DOUBLE PRECISION VERSION OF THE PREVIOUS ROUTINE FINDSH.  GIVEN AN
!     ALTITUDE H, THIS SUBROUTINE FINDS THE LAYER BOUNDARIES ZM(I1) AND
!     ZM(I2) WHICH CONTAIN H, THEN CALCULATES THE SCALE HEIGHT (SH) AND
!     THE VALUE AT THE GROUND (GAMMA+1) FOR THE INDEX OF REFRACTION.

!     DECLARE ROUTINE ARGUMENTS:
      DOUBLE PRECISION H,SH,GAMMA

!     INCLUDE PARAMETERS:
      INCLUDE 'PARAMS.h'

!     LIST COMMONS:

!     /CNTRL/
!       IKMAX    NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       ML       NUMBER OF ATMOSPHERIC PROFILE LEVELS.
!       MLFLX    NUMBER OF LEVELS FOR WHICH FLUX VALUES ARE WRITTEN.
!       ISSGEO   LINE-OF-SIGHT FLAG (0 = SENSOR PATH, 1 = SOLAR PATHS).
!       IMULT    MULTIPLE SCATTERING FLAG
!                  (0=NONE, 1=AT SENSOR, -1=AT FINAL OR TANGENT POINT).
      INTEGER IKMAX,ML,MLFLX,ISSGEO,IMULT
      COMMON/CNTRL/IKMAX,ML,MLFLX,ISSGEO,IMULT

!     /MODEL/
!       ZM       PROFILE BOUNDARY ALTITUDES [KM].
!       PM       PROFILE BOUNDARY PRESSURES [MBAR].
!       TM       PROFILE BOUNDARY TEMPERATURES [K].
!       RFNDX    PROFILE BOUNDARY REFRACTIVITIES.
!       DENSTY   PROFILE BOUNDARY DENSITIES [UNITS DEPEND ON SPECIES].
!       LRHSET   FLAG, TRUE IF RELATIVE HUMIDITY CANNOT BE SCALED.
      REAL ZM,PM,TM,RFNDX,DENSTY
      LOGICAL LRHSET
      COMMON/MODEL/ZM(LAYDIM),PM(LAYDIM),TM(LAYDIM),                    &
     &  RFNDX(LAYDIM),DENSTY(MEXT,LAYDIM),LRHSET(LAYDIM)

!     LOCAL VARIABLES:
!       I1      INDEX OF LAYER LOWER BOUNDARY.
!       I2      INDEX OF LAYER UPPER BOUNDARY.
      INTEGER I1,I2
      DOUBLE PRECISION Z1,Z2,RFNDX1,RFNDX2

!     LOOP OVER LAYERS
      I1=1
      DO 10 I2=2,ML
          IF(ZM(I2).GE.REAL(H)) GOTO 20
   10 I1=I2
      SH=DBLE(0.)
      GAMMA=DBLE(RFNDX(ML))
      RETURN
   20 CONTINUE
      Z1=DBLE(ZM(I1))
      Z2=DBLE(ZM(I2))
      RFNDX1=DBLE(RFNDX(I1))
      RFNDX2=DBLE(RFNDX(I2))
      CALL DPSCHT(Z1,Z2,RFNDX1,RFNDX2,SH,GAMMA)
      RETURN
      END
