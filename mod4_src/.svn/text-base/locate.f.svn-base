      SUBROUTINE LOCATE(OLAT,OLONG,OPSI,DBETA,RLAT,RLONG)

!     THIS ROUTINE CALCULATES THE LATITUDE AND LONGITUDE AT A POINT "R"
!     ALONG A LINE OF SIGHT FROM AN OBSERVER AT "O"

!     INPUT
!       OLAT    THE LATITUDE AT "O" [DEG NORTH, -90 TO 90]
!       OLONG   THE LONGITUDE AT "O" [DEG WEST, 0 TO 360]
!       OPSI    THE PATH AZIMUTH AT "O" [DEG EAST OF NORTH, 0 TO 360]
!       DBETA   THE EARTH CENTER ANGLE FROM "O" TO "R" [DEG, 0 TO 180]

!     OUTPUT
!       RLAT    THE LATITUDE AT "R" [DEG, -90 TO 90]
!       RLONG   THE LONGITUDE AT "R" [DEG WEST, 0 TO 360]
      REAL OLAT,OLONG,OPSI,DBETA,RLAT,RLONG

!     CAUTION:  OLONG AND OPSI ARE NOT WELL DEFINED AT THE NORTH
!               (OLAT=90) AND SOUTH (OLAT=-90) POLES.  THIS ROUTINE
!               RETURNS THE VALUES OF LATITUDE AND LONGITUDE OBTAINED
!               IN THE LIMIT AS OLAT APPROACHES + OR - 90.

!     DECLARE LOCAL VARIABLES
      REAL DEG,SOLAT,COLAT,SOLONG,COLONG,SDBETA,CDBETA,                 &
     &  SOPSI,COPSI,SRLAT,XP,YP,X,Y
      DATA DEG/57.29578/
      SOLAT=SIN(OLAT/DEG)
      COLAT=COS(OLAT/DEG)
      SOLONG=SIN(OLONG/DEG)
      COLONG=COS(OLONG/DEG)
      SDBETA=SIN(DBETA/DEG)
      CDBETA=COS(DBETA/DEG)
      SOPSI=SIN(OPSI/DEG)
      COPSI=COS(OPSI/DEG)
      SRLAT=SOLAT*CDBETA+COLAT*SDBETA*COPSI
      IF(SRLAT.GE.1.)THEN
          RLAT=90.
      ELSEIF(SRLAT.GT.-1.)THEN
          RLAT=DEG*ASIN(SRLAT)
      ELSE
          RLAT=-90.
      ENDIF
      XP=COLAT*CDBETA-SOLAT*SDBETA*COPSI
      YP=SDBETA*SOPSI
      X=COLONG*XP+SOLONG*YP
      Y=SOLONG*XP-COLONG*YP
      IF(X.EQ.0.)THEN
          RLONG=90.
          IF(Y.LT.0.)RLONG=270.
      ELSE
          RLONG=DEG*ATAN(Y/X)
          IF(X.LT.0)RLONG=RLONG+180.
          IF(RLONG.LT.0.)RLONG=RLONG+360.
      ENDIF
      RETURN
      END
