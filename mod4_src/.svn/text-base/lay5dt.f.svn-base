      SUBROUTINE LAY5DT(V,IKOFF,IPATH,IKMX,TRANSM,P2RAY)

!     THIS ROUTINE DEFINES THE LAYER DEPENDENT 5 CM-1 DATA

!     INPUT ARGUMENTS:
!       IKMX     NUMBER OF PATH SEGMENTS ALONG LINE-OF-SIGHT.
!       P2RAY    SECOND LEGENDRE EXPANSION COEFFICIENT OVER 5 (= 2N+1).
      INTEGER IKOFF,IPATH,IKMX
      REAL V,P2RAY
      LOGICAL TRANSM

!     THE TXNEW AND TXOLD ARRAYS CONTAIN:
!      1  ASYMMETRY PARAMETER WEIGHTED BY SCATTERING DEPTH.
!      2  INCREMENTAL AEROSOL SCATTERING OPTICAL DEPTH.
!      3  TOTAL O2 CONTINUUM TRANSMITTANCE.
!      4  N2 CONTINUUM TRANSMITTANCE.
!      5  TOTAL H2O CONTINUUM TRANSMITTANCE.
!      6  RAYLEIGH MOLECULAR SCATTERED TRANSMITTANCE.
!      7  AEROSOL EXTINCTION.
!      8  TOTAL OZONE CONTINUUM TRANSMITTANCE.
!      9  PRODUCT OF ALL CONTINUUM TRANSMITTANCES EXCEPT O2 AND HNO3.
!     10  AEROSOL ABSORPTION.
!     11  HNO3 TRANSMITTANCE.
!     12  MOLECULAR CONTINUUM OPTICAL DEPTH.
!     13  INCREMENTAL AEROSOL EXTINCTION OPTICAL DEPTH.
!     14  TOTAL (COMBINED SPECIES) INCREMENTAL CONTINUUM OPTICAL DEPTH.
!     15  RAYLEIGH MOLECULAR SCATTERING OPTICAL DEPTH.
!     16  CIRRUS CLOUD TRANSMITTANCE (ICLD=20 ONLY).
!     17  UV/VIS NO2 TRANSMISSION.
!     18  UV/VIS SO2 TRANSMISSION.
!     19  INCREMENTAL WATER DROPLET SCATTERING OPTICAL DEPTH.
!     20  INCREMENTAL ICE PARTICLE SCATTERING OPTICAL DEPTH.
!     21  INCREMENTAL BOUNDARY LAYER AEROSOL SCATTERING OPTICAL DEPTH.
!     22  INCREMENTAL TROPOSPHERIC AEROSOL SCATTERING OPTICAL DEPTH.
!     23  INCREMENTAL STRATOSPHERIC AEROSOL SCATTERING OPTICAL DEPTH.
!     24  INCREMENTAL MESOSPHERE-SPACE AEROSOL SCATTERING OPTICAL DEPTH.
!     25  INCREMENTAL NOVAM AEROSOL SCATTERING OPTICAL DEPTH.
!     26  INCREMENTAL NOVAM AEROSOL ASYMMETRY FACTOR.
!     27  INCREMENTAL STD OR SUB-VIS CIRRUS SCATTERING OPTICAL DEPTH.
!     28  INCREMENTAL CLOUD EXTINCTION OPTICAL DEPTH.
!     29  CLOUD ASYMMETRY PARAMETER WEIGHTED BY SCATTERING DEPTH.
!     30  INCREMENTAL RAIN EXTINCTION OPTICAL DEPTH.
!     31  INCREMENTAL RAIN SCATTERING OPTICAL DEPTH.
!     32  INCREMENTAL RAIN ASYMMETRY FACTOR.
!     33  14 MINUS RAIN, CLOUD AND AEROSOL.

!     FOR LOWTRAN RUNS, TX CONTAINS MOLECULAR LINE CENTER TRANSMITTANCES
!     17=H2O  36=CO2  31=O3   47=N2O  44=CO   46=CH4
!     50=O2   54=NO   56=SO2  55=NO2  52=NH3

!     PARAMETERS:
!       C2       SECOND RADIATION CONSTANT [CM K].
!       T296     HITRAN STANDARD TEMPERATURE [K].
!       TDIF     TEMPERATURE RANGE OF H2O SELF CONTINUUM DATA [K].
!       C2D      C2 OVER TDIF [CM].
!       T296D    T296 OVER TDIF.
      REAL C2,T296,TDIF,C2D,T296D
      PARAMETER(C2=1.438786,T296=296.,TDIF=T296-260.,                   &
     &  C2D=C2/TDIF,T296D=T296/TDIF)
      INCLUDE 'PARAMS.h'

!     COMMONS:
      INCLUDE 'BASE.h'

!     COMMON/RCNSTN/
!       PI       THE CONSTANT PI
!       DEG      NUMBER OF DEGREES IN ONE RADIAN.
!       BIGNUM   MAXIMUM SINGLE PRECISION NUMBER.
!       BIGEXP   MAXIMUM EXPONENTIAL ARGUMENT WITHOUT OVERFLOW.
!       RRIGHT   SMALLEST SINGLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
      REAL PI,DEG,BIGNUM,BIGEXP,RRIGHT
      COMMON/RCNSTN/PI,DEG,BIGNUM,BIGEXP,RRIGHT

!     /CARD1/
      INTEGER MODEL,ITYPE,IEMSCT,M1,M2,M3,IM,NOPRNT
      LOGICAL MODTRN
      COMMON/CARD1/MODEL,ITYPE,IEMSCT,M1,M2,M3,IM,NOPRNT,MODTRN
      INTEGER IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA
      REAL VIS,WSS,WHH,RAINRT
      COMMON/CARD2/IHAZE,ISEASN,IVULCN,ICSTL,ICLD,IVSA,                 &
     &  VIS,WSS,WHH,RAINRT

!     /JM5/
!       IRPT     REPEAT INPUT FLAG (0=NONE, 1=ALL, 3=GEOM, 4=SPEC).
!       IFAC     CURRENT COLUMN SCALING FACTOR INDEX.
!       NFACMN   NUMBER OF COLUMN SCALING FACTOR LESS THAN 1.
!       NFACMX   NUMBER OF COLUMN SCALING FACTOR GREATER THAN 1.
!       FACMC    CURRENT COLUMN SCALING FACTOR.
!       SCALMN   MINIMUM COLUMN SCALING FACTOR.
!       SCALMX   MAXIMUM COLUMN SCALING FACTOR.
      INTEGER IRPT,IFAC,NFACMN,NFACMX
      REAL FACMC
      DOUBLE PRECISION SCALMN,SCALMX
      COMMON/JM5/SCALMN,SCALMX,IRPT,IFAC,NFACMN,NFACMX,FACMC

!     /AER/
!     THERE ARE "MAER=17" COMPONENTS:
!      1     AEROSOL 1 (NOMINALLY, BOUNDARY LAYER AEROSOL).
!      2     AEROSOL 2 (NOMINALLY, TROPOSPHERIC AEROSOL).
!      3     AEROSOL 3 (NOMINALLY, STRATOSPHERIC AEROSOL).
!      4     AEROSOL 4 (NOMINALLY, VOLCANIC AEROSOL).
!      5     CIRRUS CLOUD.
!      6     CLOUD 1 (NOMINALLY, WATER CLOUD).
!      7     CLOUD 2 (NOMINALLY, ICE CLOUD).
!     8-17   NOVAM (NAVY OCEANIC VERTICAL AEROSOL MODEL) AEROSOL LAYERS.

!       NAER     NUMBER OF ACTIVE AEROSOLS.
!       EXTV     SPECTRAL EXTINCTION (NORMALIZED TO 1 AT 550 NM).
!       ABSV     SPECTRAL ABSORPTION (1-ABSV/EXTV=SCATTERING ALBEDO).
!       ASYV     SPECTRAL LEGENDRE MOMENT (DIVIDED BY 2N+1).
      INTEGER NAER
      REAL EXTV,ABSV,ASYV
      COMMON/AER/NAER,EXTV(MAER),ABSV(MAER),ASYV(MXCMU,MAER)
      INTEGER IBND
      REAL A1,B1,C1,QA,CPS
      COMMON/AABBCC/A1(11),B1(11),C1(11),IBND(11),QA(11),CPS(11)

!     /FRQ5/
!       O2C127   O2 1.27 MICRON BAND CONTINUUM ABSORPTION.
!       O2C106   O2 1.06 MICRON BAND CONTINUUM ABSORPTION.
      REAL SIGO20,SIGO2A,SIGO2B,O3T0,O3T1,O3T2,RAYSCT,N2CONT,           &
     &  SH2OT0,SH2OT,FH2O,CHNO3,O2HERZ,O2C127,O2C106,CRSNO2,CRSSO2
      COMMON/FRQ5/SIGO20,SIGO2A,SIGO2B,O3T0,O3T1,O3T2,RAYSCT,N2CONT,    &
     &  SH2OT0,SH2OT,FH2O,CHNO3,O2HERZ,O2C127,O2C106,CRSNO2,CRSSO2

!     /LAY5/
!       TXLEG    SPECTRALLY INTERPOLATED LEGENDRE COEFFICIENTS FOR THE
!                SCATTERING PHASE FUNCTION (OPTICAL DEPTH WEIGHTED SUM).
      REAL TXNEW,TXOLD,TXLEG
      COMMON/LAY5/TXNEW(NLAY5,LAYTHR,3),TXOLD(NLAY5,LAYTHR,3),          &
     &  TXLEG(2:MXCMU,LAYTWO+1:LAYTHR,0:1)
      SAVE/LAY5/

!     /DISRT/
!       DIS      LOGICAL FLAG, TRUE FOR DISORT MULTIPLE SCATTERING.
!       DISAZM   LOGICAL FLAG, TRUE FOR DISORT WITH AZIMUTH DEPENDENCE.
!       DISALB   LOGICAL FLAG, TRUE FOR DISORT SPHERICAL ALBEDO OPTION.
!       LDISCL   LOGICAL FLAG, TRUE FOR ISAACS SCALED TO DISORT.
!       NSTR     NUMBER OF DISCRETE ORDINATE STREAMS.
!       NAZ      NUMBER OF DISORT AZIMUTH COMPONENTS.
!       N2GAUS   ORDER OF DOUBLE-GAUSS QUADRATURES.
      LOGICAL DIS,DISAZM,DISALB,LDISCL
      INTEGER NSTR,NAZ,N2GAUS
      COMMON/DISRT/DIS,DISAZM,DISALB,LDISCL,NSTR,NAZ,N2GAUS

!     LOCAL VARIABLES:
!       IK       PATH SEGMENT INDEX.
!       ISTR     LOOP INDEX FOR MULTIPLE SCATTERING STREAMS.
!       WRAT     TEMPERATURE INTERPOLATION FACTOR.
!       C2DV     C2 TIME SPECTRAL FREQUENCY OVER TDIF.
!       EXPON    EXPONENTIAL USED IN RADIATION FIELD CALCULATION.
!       GLEG     LEGENDRE MOMENT EXPANSION VARIABLE FOR PHASE FUNCTIONS.
      INTEGER IAER,K,IK,ISTR
      REAL ABSSM,SCTSM,ASYMSM,EXTSM,XRAIN,ARAIN,SRAIN,GRAIN,SCTTRM(8),  &
     &  WRAT,C2DV,EXPON,GLEG

!     FUNCTIONS:
      REAL DBLTX

!     RAYLEIGH OPTICAL DEPTH:
      TX(6)=RAYSCT*W(6)

!     CONTRIBUTION OF RAIN TO EXTINCTION, SCATTERING AND ABSORPTION
      IF(W(3).EQ.0.)THEN
          XRAIN=0.
          ARAIN=0.
          SRAIN=0.
          GRAIN=0.
      ELSE
          CALL RAIN(V,XRAIN,ARAIN,SRAIN,GRAIN)
      ENDIF

!     CALCULATE INCREMENTAL OPTICAL DEPTH VARIABLES
      IF(IPATH.EQ.3)THEN

!         LINE-OF-SIGHT PATH WITH IEMSCT=1 OR IEMSCT=2:

!         RAIN:
          TXOLD(30,IKOFF,3)=TXNEW(30,IKOFF,3)
          TXNEW(30,IKOFF,3)=XRAIN
          TXOLD(31,IKOFF,3)=TXNEW(31,IKOFF,3)
          TXNEW(31,IKOFF,3)=SRAIN
          TXOLD(32,IKOFF,3)=TXNEW(32,IKOFF,3)
          TXNEW(32,IKOFF,3)=GRAIN

!         BOUNDARY LAYER AEROSOL (1):
          SCTTRM(1)=W(7)*(EXTV(1)-ABSV(1))
          ASYMSM=SCTTRM(1)*ASYV(1,1)
          SCTSM=SCTTRM(1)
          EXTSM=W(7)*EXTV(1)
          ABSSM=ARAIN+W(7)*ABSV(1)
          TXOLD(21,IKOFF,3)=TXNEW(21,IKOFF,3)
          TXNEW(21,IKOFF,3)=SCTTRM(1)

!         TROPOSPHERIC AEROSOL (2):
          SCTTRM(2)=W(12)*(EXTV(2)-ABSV(2))
          ASYMSM=ASYMSM+SCTTRM(2)*ASYV(1,2)
          SCTSM=SCTSM+SCTTRM(2)
          EXTSM=EXTSM+W(12)*EXTV(2)
          ABSSM=ABSSM+W(12)*ABSV(2)
          TXOLD(22,IKOFF,3)=TXNEW(22,IKOFF,3)
          TXNEW(22,IKOFF,3)=SCTTRM(2)

!         STRATOSPHERIC AEROSOL (3):
          SCTTRM(3)=W(13)*(EXTV(3)-ABSV(3))
          ASYMSM=ASYMSM+SCTTRM(3)*ASYV(1,3)
          SCTSM=SCTSM+SCTTRM(3)
          EXTSM=EXTSM+W(13)*EXTV(3)
          ABSSM=ABSSM+W(13)*ABSV(3)
          TXOLD(23,IKOFF,3)=TXNEW(23,IKOFF,3)
          TXNEW(23,IKOFF,3)=SCTTRM(3)

!         VOLCANIC AEROSOL (4):
          SCTTRM(4)=W(14)*(EXTV(4)-ABSV(4))
          ASYMSM=ASYMSM+SCTTRM(4)*ASYV(1,4)
          SCTSM=SCTSM+SCTTRM(4)
          EXTSM=EXTSM+W(14)*EXTV(4)
          ABSSM=ABSSM+W(14)*ABSV(4)
          TXOLD(24,IKOFF,3)=TXNEW(24,IKOFF,3)
          TXNEW(24,IKOFF,3)=SCTTRM(4)

!         STANDARD OR SUB-VISUAL CIRRUS MODEL (5):
          SCTTRM(5)=W(16)*(EXTV(5)-ABSV(5))
          TXOLD(27,IKOFF,3)=TXNEW(27,IKOFF,3)
          TXNEW(27,IKOFF,3)=SCTTRM(5)
          ABSSM=ABSSM+W(16)*ABSV(5)
          TXOLD(28,IKOFF,3)=TXNEW(28,IKOFF,3)
          TXNEW(28,IKOFF,3)=W(16)*EXTV(5)
          TXOLD(29,IKOFF,3)=TXNEW(29,IKOFF,3)
          TXNEW(29,IKOFF,3)=SCTTRM(5)*ASYV(1,5)

!         WATER DROPLET CLOUD (6):
          SCTTRM(6)=W(66)*(EXTV(6)-ABSV(6))
          TXOLD(19,IKOFF,3)=TXNEW(19,IKOFF,3)
          TXNEW(19,IKOFF,3)=SCTTRM(6)
          ABSSM=ABSSM+W(66)*ABSV(6)
          TXNEW(28,IKOFF,3)=TXNEW(28,IKOFF,3)+W(66)*EXTV(6)
          TXNEW(29,IKOFF,3)=TXNEW(29,IKOFF,3)+SCTTRM(6)*ASYV(1,6)

!         ICE PARTICLE CLOUD (7):
          SCTTRM(7)=W(67)*(EXTV(7)-ABSV(7))
          TXOLD(20,IKOFF,3)=TXNEW(20,IKOFF,3)
          TXNEW(20,IKOFF,3)=SCTTRM(7)
          ABSSM=ABSSM+W(67)*ABSV(7)
          TXNEW(28,IKOFF,3)=TXNEW(28,IKOFF,3)+W(67)*EXTV(7)
          TXNEW(29,IKOFF,3)=TXNEW(29,IKOFF,3)+SCTTRM(7)*ASYV(1,7)

!         PHASE FUNCTION LEGENDRE MOMENTS (SCATTERING DEPTH WEIGHTED):
          IF(IKOFF.GT.LAYTWO)THEN
              GLEG=SRAIN*GRAIN
              DO ISTR=2,NSTR
                  TXLEG(ISTR,IKOFF,0)=TXLEG(ISTR,IKOFF,1)
                  GLEG=GLEG*GRAIN
                  TXLEG(ISTR,IKOFF,1)=GLEG+SCTTRM(1)*ASYV(ISTR,1)       &
     &              +SCTTRM(2)*ASYV(ISTR,2)+SCTTRM(3)*ASYV(ISTR,3)      &
     &              +SCTTRM(4)*ASYV(ISTR,4)+SCTTRM(5)*ASYV(ISTR,5)      &
     &              +SCTTRM(6)*ASYV(ISTR,6)+SCTTRM(7)*ASYV(ISTR,7)
              ENDDO

!             ADD RAYLEIGH SCATTERING CONTRIBUTION TO THE SECOND MOMENT:
              TXLEG(2,IKOFF,1)=TXLEG(2,IKOFF,1)+TX(6)*P2RAY
          ENDIF

!         NOVAM AEROSOL (8-17):
          IF(NAER.GT.MAER-10)THEN
              TXOLD(25,IKOFF,3)=TXNEW(25,IKOFF,3)
              TXNEW(25,IKOFF,3)=0.
              TXOLD(26,IKOFF,3)=TXNEW(26,IKOFF,3)
              TXNEW(26,IKOFF,3)=0.
              DO 10 IAER=8,NAER
                  SCTTRM(8)=W(60+IAER)*(EXTV(IAER)-ABSV(IAER))
                  TXNEW(25,IKOFF,3)=TXNEW(25,IKOFF,3)+SCTTRM(8)
                  TXNEW(26,IKOFF,3)                                     &
     &              =TXNEW(26,IKOFF,3)+SCTTRM(8)*ASYV(1,IAER)
                  EXTSM=EXTSM+W(60+IAER)*EXTV(IAER)
                  ABSSM=ABSSM+W(60+IAER)*ABSV(IAER)
                  IF(IKOFF.GT.LAYTWO)THEN
                      DO ISTR=2,NSTR
                          TXLEG(ISTR,IKOFF,1)=TXLEG(ISTR,IKOFF,1)       &
     &                      +SCTTRM(8)*ASYV(ISTR,NAER)
                      ENDDO
                  ENDIF
   10         CONTINUE
              SCTSM=SCTSM+TXNEW(25,IKOFF,3)
              ASYMSM=ASYMSM+TXNEW(26,IKOFF,3)
              IF(TXNEW(25,IKOFF,3).GT.0.)                               &
     &          TXNEW(26,IKOFF,3)=TXNEW(26,IKOFF,3)/TXNEW(25,IKOFF,3)
          ENDIF

!         COMBINED EXTINCTION AND SCATTERING TERMS:
          TXOLD( 1,IKOFF,3)=TXNEW( 1,IKOFF,3)
          TXOLD( 2,IKOFF,3)=TXNEW( 2,IKOFF,3)
          TXOLD(13,IKOFF,3)=TXNEW(13,IKOFF,3)
          TXNEW( 1,IKOFF,3)=ASYMSM
          TXNEW( 2,IKOFF,3)=SCTSM
          TXNEW(13,IKOFF,3)=EXTSM
          EXTSM=EXTSM+XRAIN+TXNEW(28,IKOFF,3)
      ELSE

!         ADD AEROSOL EXTINCTION AND ABSORPTION TO RAIN CONTRIBUTION
          EXTSM=XRAIN+W( 7)*EXTV( 1)+W(12)*EXTV( 2)+W(13)*EXTV( 3)      &
     &               +W(14)*EXTV( 4)+W(16)*EXTV( 5)+W(66)*EXTV( 6)      &
     &               +W(67)*EXTV( 7)+W(68)*EXTV( 8)+W(69)*EXTV( 9)      &
     &               +W(70)*EXTV(10)+W(71)*EXTV(11)+W(72)*EXTV(12)      &
     &               +W(73)*EXTV(13)+W(74)*EXTV(14)+W(75)*EXTV(15)      &
     &               +W(76)*EXTV(16)+W(77)*EXTV(17)
          ABSSM=ARAIN+W( 7)*ABSV( 1)+W(12)*ABSV( 2)+W(13)*ABSV( 3)      &
     &               +W(14)*ABSV( 4)+W(16)*ABSV( 5)+W(66)*ABSV( 6)      &
     &               +W(67)*ABSV( 7)+W(68)*ABSV( 8)+W(69)*ABSV( 9)      &
     &               +W(70)*ABSV(10)+W(71)*ABSV(11)+W(72)*ABSV(12)      &
     &               +W(73)*ABSV(13)+W(74)*ABSV(14)+W(75)*ABSV(15)      &
     &               +W(76)*ABSV(16)+W(77)*ABSV(17)
      ENDIF

!     DETERMINE OPTICAL DEPTHS
!     TX(4)   N2 CONTINUUM
!     TX(5)   SUM OF H2O CONTINUUM CONTRIBUTIONS
!     TX(8)   VISIBLE AND ULTRAVIOLET O3 (= 0 IF ABBUV > 0)
!     TX(11)  HNO3 ABSORPTION (LOWTRAN CALCULATIONS ONLY)
!     TX(16)  CIRRUS CLOUDS
!     TX(9)   SUM OF CONTINUUM CONTRIBUTIONS EXCLUDING O2 AND HNO3
!     TX(3)   O2 CONTINUUM CONTRIBUTIONS
      TX(4)=FACMC*N2CONT*W(4)
      IF(W(5).GT.0. .AND. SH2OT0.GT.0.)THEN

!         TEMPERATURE-DEPENDENT H2O CONTINUUM:
          IF(.NOT.TRANSM)THEN
              WRAT=W(9)/W(5)
              TX(5)=W(10)*FH2O+W(5)*SH2OT0*SH2OT**WRAT
              IF(V.LT.2100.)THEN

!                 RADIATION FIELD (SPECTRAL FREQUENCY ALREADY INCLUDED):
                  EXPON=EXP(C2D*V/(WRAT-T296D))
                  TX(5)=TX(5)*(1-EXPON)/(1+EXPON)
              ENDIF
          ELSEIF(V.GE.2100.)THEN

!             TRANSMITTANCE MODE REQUIRES A LAYER LOOP:
              TX(5)=0.
              DO 20 IK=1,IKMX
                  WRAT=WPATH(IK,9)/WPATH(IK,5)
                  TX(5)=TX(5)+WPATH(IK,5)*SH2OT0*SH2OT**WRAT            &
     &              +WPATH(IK,10)*FH2O
   20         CONTINUE
          ELSE

!             TRANSMITTANCE MODE REQUIRES A LAYER LOOP:
              C2DV=C2D*V
              TX(5)=0.
              DO 30 IK=1,IKMX
                  WRAT=WPATH(IK,9)/WPATH(IK,5)

!                 RADIATION FIELD (SPECTRAL FREQUENCY ALREADY INCLUDED):
                  EXPON=EXP(C2DV/(WRAT-T296D))
                  TX(5)=TX(5)+(WPATH(IK,5)*SH2OT0*SH2OT**WRAT           &
     &              +WPATH(IK,10)*FH2O)*(1-EXPON)/(1+EXPON)
   30         CONTINUE
          ENDIF
          TX(5)=FACMC*TX(5)
      ELSE
          TX(5)=0.
      ENDIF
      TX(8)=FACMC*O3T0*(.269*W(8)+O3T1*W(59)+O3T2*W(60))
      TX(11)=FACMC*CHNO3*W(11)
      IF(ICLD.EQ.20)THEN
          TX(16)=2*W(16)
      ELSE
          TX(16)=0.
      ENDIF
      TX(3)=FACMC*(W(58)*O2HERZ+W(82)*O2C127+W(83)*O2C106               &
     &  +SIGO20*(W(63)+SIGO2A*(W(1)-220.*W(63))+SIGO2B*W(2)))
      TX(64)=FACMC*CRSNO2*W(64)
      TX(65)=FACMC*CRSSO2*W(65)
      TX(9)=TX(4)+TX(5)+TX(6)+TX(8)+EXTSM+TX(16)

!     STORE CUMULATIVE AEROSOL PARAMETERS FOR DIFFERENT VERTICAL REGIONS
      TX(10)=ABSSM
      TX(7)=EXTSM

!     STORE OPTICAL THICKNESS PARAMETERS
      TXOLD(12,IKOFF,IPATH)=TXNEW(12,IKOFF,IPATH)
      TXNEW(12,IKOFF,IPATH)=TX(4)+TX(5)+TX(8)+TX(3)
      TXOLD(14,IKOFF,IPATH)=TXNEW(14,IKOFF,IPATH)
      TXNEW(14,IKOFF,IPATH)=TX(9)+TX(3)+TX(64)+TX(65)
      TXOLD(33,IKOFF,IPATH)=TXNEW(33,IKOFF,IPATH)
      TXNEW(33,IKOFF,IPATH)=TX(4)+TX(5)+TX(6)+TX(8)+TX(3)+TX(64)+TX(65)
      TXOLD(15,IKOFF,IPATH)=TXNEW(15,IKOFF,IPATH)
      TXNEW(15,IKOFF,IPATH)=TX(6)
      DO 40 K=3,11
          TXOLD(K,IKOFF,IPATH)=TXNEW(K,IKOFF,IPATH)
          IF(TX(K).LE.BIGEXP)THEN
              TXNEW(K,IKOFF,IPATH)=EXP(-TX(K))
          ELSE
              TXNEW(K,IKOFF,IPATH)=1./BIGNUM
          ENDIF
   40 CONTINUE
      TXOLD(16,IKOFF,IPATH)=TXNEW(16,IKOFF,IPATH)
      IF(TX(16).LE.BIGEXP)THEN
         TXNEW(16,IKOFF,IPATH)=EXP(-TX(16))
      ELSE
         TXNEW(16,IKOFF,IPATH)=1./BIGNUM
      ENDIF
      TXOLD(17,IKOFF,IPATH)=TXNEW(17,IKOFF,IPATH)
      IF(TX(64).LE.BIGEXP)THEN
         TXNEW(17,IKOFF,IPATH)=EXP(-TX(64))
      ELSE
         TXNEW(17,IKOFF,IPATH)=1./BIGNUM
      ENDIF

      TXOLD(18,IKOFF,IPATH)=TXNEW(18,IKOFF,IPATH)
      IF(TX(65).LE.BIGEXP)THEN
         TXNEW(18,IKOFF,IPATH)=EXP(-TX(65))
      ELSE
         TXNEW(18,IKOFF,IPATH)=1./BIGNUM
      ENDIF

      IF(MODTRN)RETURN

!     LOWTRAN7 DOUBLE EXPONENTIAL MOLECULAR TRANSMITTANCES
      DO 50 K=1,11
          IF(CPS(K).GT.-20.)THEN
              TX(KPOINT(K))=DBLTX(FACMC*W(IBND(K)),CPS(K),QA(K))
          ELSE
              TX(KPOINT(K))=1.
          ENDIF
   50 CONTINUE
      RETURN
      END
