      FUNCTION PSI(PSIO,DELO,BETA,IARB,IARBO)

!     FUNCTION PSI RETURNS THE VALUE OF SOLAR AZIMUTH RELATIVE TO
!     THE LINE OF SIGHT, AT THE CURRENT SCATTERING LOCATION

!     COMMON/RCNSTN/
!       PI       THE CONSTANT PI
!       DEG      NUMBER OF DEGREES IN ONE RADIAN.
!       BIGNUM   MAXIMUM SINGLE PRECISION NUMBER.
!       BIGEXP   MAXIMUM EXPONENTIAL ARGUMENT WITHOUT OVERFLOW.
!       RRIGHT   SMALLEST SINGLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
      REAL PI,DEG,BIGNUM,BIGEXP,RRIGHT
      COMMON/RCNSTN/PI,DEG,BIGNUM,BIGEXP,RRIGHT
      DATA  EPSILN/1.0E-5/
      PSI=PSIO
      DELOR=DELO/DEG
      BETAR=BETA/DEG
      IF(IARBO.EQ.0) GO TO 5
!     SPECIAL CASES WHEN PSIO IS ARBITRARY
      IARB=IARBO
      PSI = 0.
      IF(IARBO.EQ.1.OR.IARBO.EQ.3) RETURN
      IF(BETA.LE.EPSILN) RETURN
!     PSI=180.0 (MOVED OUT FROM UNDER THE SUN)
      IARB=0
      PSI=180.0
      RETURN
5     CONTINUE
!     GENERAL CASE
      PSIOR=PSIO/DEG
      IARB=0
      ANUMER=SIN(DELOR)*SIN(PSIOR)
      DENOM=COS(BETAR)*SIN(DELOR)*COS(PSIOR)-SIN(BETAR)*COS(DELOR)
!     SPECIAL CASES
!     NUMERATOR GOES TO ZERO IN THE FOLLOWING 3 CASES
! 1)  DELO=0.0
      IF(DELO.GT.EPSILN) GO TO 20
      IF(BETA.GT.EPSILN) GO TO 10
      IARB=2
      RETURN
10    PSI=180.0
      RETURN
! 2)  PSIO=0.0
20    IF(ABS(PSIO).GT.EPSILN) GO TO 40
      IF(ABS(BETA-DELO).GE.EPSILN) GO TO 30
!     SCATTERING POINT IS DIRECTLY UNDER THE SUN
      IARB=2
      RETURN
30    IF(BETA.LT.DELO) PSI=0.0
      IF(BETA.GT.DELO) PSI=180.0
      RETURN
! 3)  PSIO=180.0
40    IF(ABS(PSIO).LT.(180.0-EPSILN)) GO TO 60
      PSI=180.0
      RETURN
60    CONTINUE
!     DENOMINATOR CAN GO TO ZERO FOR THE FOLLOWING 2 CASES
! 1)  BETA=DELO AND PSIO=0.0
!     THIS CASE WAS HANDLED EARLIER
! 2)  GENERAL CASE
      IF(ABS(DENOM).GT.EPSILN) GO TO 80
      IF(PSIO.LT.0.0) PSI=-90.0
      IF(PSIO.GT.0.0) PSI=90.0
      RETURN
80    CONTINUE
      PSI=DEG*ATAN(ANUMER/DENOM)
!     NOTE ATAN RETURNS ARGUMENTS BETWEEN -90 AND 90, PSI
!     AND PSIO SHOULD BE OF THE SAME SIGN.
      IF(PSIO.GT.0.0.AND.PSI.LT.0.0) PSI=PSI+180.
      IF(PSIO.LT.0.0.AND.PSI.GT.0.0) PSI=PSI-180.
      RETURN
      END
