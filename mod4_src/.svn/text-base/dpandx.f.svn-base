      DOUBLE PRECISION FUNCTION DPANDX(ECD,HEIGHT,SCALHT,GAMMA)

!     DPANDX COMPUTES THE PRODUCT OF THE EARTH CENTER DISTANCE AND
!     THE INDEX OF REFRACTION.

!     ARGUMENTS:
!       ECD      EARTH CENTER DISTANCE [KM].
!       HEIGHT   HEIGHT ABOVE THE SPHERICAL EARTH [KM].
!       SCALHT   SCALE HEIGHT [KM].
!       GAMMA    REFRACTIVITY, EQUAL TO INDEX OF REFRACTION MINUS ONE.
      DOUBLE PRECISION ECD,HEIGHT,SCALHT,GAMMA

!     COMMONS:
!     COMMON/RCNSTN/
!       PI       THE CONSTANT PI
!       DEG      NUMBER OF DEGREES IN ONE RADIAN.
!       BIGNUM   MAXIMUM SINGLE PRECISION NUMBER.
!       BIGEXP   MAXIMUM EXPONENTIAL ARGUMENT WITHOUT OVERFLOW.
!       RRIGHT   SMALLEST SINGLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
      REAL PI,DEG,BIGNUM,BIGEXP,RRIGHT
      COMMON/RCNSTN/PI,DEG,BIGNUM,BIGEXP,RRIGHT

!     COMMON/GRAUND/
!       GNDALT   GROUND ALTITUDE [KM].
      REAL GNDALT
      COMMON/GRAUND/GNDALT

!     LOCAL VARIABLES:
!       ARG      ARGUMENT OF THE EXPONENTIAL.
      DOUBLE PRECISION ARG
      IF(SCALHT.EQ.0.)THEN
          DPANDX=ECD+GAMMA*ECD
          RETURN
      ENDIF
      IF(HEIGHT.LT.GNDALT)THEN
          ARG=DBLE(GNDALT)/SCALHT
      ELSE
          ARG=HEIGHT/SCALHT
      ENDIF
      IF(ARG.GT.BIGEXP)THEN
          DPANDX=ECD
      ELSE
          DPANDX=ECD+GAMMA*ECD*EXP(-ARG)
      ENDIF
      RETURN
      END
