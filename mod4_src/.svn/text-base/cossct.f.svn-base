      REAL FUNCTION COSSCT(ANGLST,THTST,PSIST,IARB)

!     COSSCT RETURNS THE COSINE OF THE SCATTERING ANGLE.

!     INPUT ARGUMENTS:
!       ANGLST  SOLAR ZENITH ANGLE [DEG].
!       THTST   LINE-OF-SIGHT ZENITH ANGLE [DEG].
!       PSIST   RELATIVE AZIMUTH ANGLE [DEG].
!       IARG    VERTICAL/NADIR LINE-OF-SIGHT FLAG
!               (=1 FOR VERTICAL/NADIR, =0 OTHERWISE).
      REAL ANGLST,THTST,PSIST
      INTEGER IARB

!     /RCNSTN/
!       PI       THE CONSTANT PI
!       DEG      NUMBER OF DEGREES IN ONE RADIAN.
!       BIGNUM   MAXIMUM SINGLE PRECISION NUMBER.
!       BIGEXP   MAXIMUM EXPONENTIAL ARGUMENT WITHOUT OVERFLOW.
!       RRIGHT   SMALLEST SINGLE PRECISION REAL ADDED TO 1 EXCEEDS 1.
      REAL PI,DEG,BIGNUM,BIGEXP,RRIGHT
      COMMON/RCNSTN/PI,DEG,BIGNUM,BIGEXP,RRIGHT

!     LOCAL VARIABLES:
!       SUNZEN  SOLAR ZENITH ANGLE [RAD].
!       PTHZEN  LINE-OF-SIGHT ZENITH ANGLE [RAD].
      REAL SUNZEN,PTHZEN

!     CONVERT FROM DEGREES TO RADIANS:
      SUNZEN=ANGLST/DEG
      PTHZEN=THTST/DEG
      COSSCT=COS(SUNZEN)*COS(PTHZEN)

!     FOR VERTICAL/NADIR LINE-OF-SIGHT (IARB=1), SIN(PTHZEN)=0.
      IF(IARB.EQ.0)COSSCT=COSSCT+SIN(SUNZEN)*SIN(PTHZEN)*COS(PSIST/DEG)

!     PROTECT AGAINST ROUNDOFF:
      IF(COSSCT.GT. 1.)THEN
          COSSCT= 1.
      ELSEIF(COSSCT.LT.-1.)THEN
          COSSCT=-1.
      ENDIF
      RETURN
      END
