      SUBROUTINE M3DIN(IKMX)

!     DEFINES SPECTRALLY INDEPENDENT PATH SEGMENT DATA FOR MOD3D.

!     ARGUMENTS:
!       IKMX     NUMBER OF LINE-OF-SIGHT PATH SEGMENTS.
      INTEGER IKMX

!     PARAMETERS:
      INCLUDE 'PARAMS.h'

!     COMMONS:
      INCLUDE 'BASE.h'

!     COMMON/CONSTN/
!       PZERO    STANDARD PRESSURE [MB].
!       TZERO    STANDARD TEMPERATURE [K].
!       AVOGAD   AVOGADRO CONSTANT [MOLECULES/MOLE].
!       STDVOL   STANDARD VOLUME OF IDEAL GAS [CM3 ATM / MOLE].
!       AIRMWT   EFFECTIVE WEIGHT OF AIR [GM/MOLE].
!       AMWT     MOLECULAR WEIGHTS [GM/MOLE].
!                  1  H2O   2  CO2   3  O3    4  N2O   5  CO    6  CH4
!                  7  O2    8  NO    9  SO2   10 NO2   11 NH3   12 HNO3
      REAL PZERO,TZERO,AVOGAD,STDVOL,AIRMWT,AMWT
      COMMON/CONSTN/PZERO,TZERO,AVOGAD,STDVOL,AIRMWT,AMWT(12)

!     /M3DDEN/
!       PRES     LAYER PRESSURE [ATM].
!       AER550   AEROSOL EXTINCTION OPTICAL DEPTH AT 550 NM.
!       DENH2O   LAYER H2O DENSITY [GM/M3].
!       DENCO2   LAYER CO2 DENSITY [GM/M3].
!       DENO3    LAYER O3 DENSITY [GM/M3].
      REAL PRES,AER550,DENH2O,DENCO2,DENO3
      COMMON/M3DDEN/PRES(LAYTWO),AER550(LAYTWO),                        &
     &  DENH2O(LAYTWO),DENCO2(LAYTWO),DENO3(LAYTWO)
      SAVE /M3DDEN/,/CONSTN/
      EXTERNAL ATMCON

!     LOCAL VARIABLES:
!       IK       PATH SEGMENT INDEX.
!       CONVRT   ATM-CM TO [(KM K MOLE) / (ATM M3)] CONVERSION FACTOR.
!       CONLAY   COLUMN DENSITY [ATM-CM] TO DENSITY [MOLE/M3] CONVERSION
      INTEGER IK
      REAL CONVRT,CONLAY

!     CONVERSION FACTOR FROM ATM-CM TO [KM MOLE / M3]
!       CONVRT = (1.E6 CM3/M3) (1.E-5 KM/CM) (MOLE / 22413.83 CM3 ATM )
      CONVRT=10/STDVOL

!     LOOP OVER LAYERS:
      DO 10 IK=1,IKMX
          PRES(IK)=WPATH(IK,6)*(TBBY(IK)/TZERO)/DRNG(IK)
          AER550(IK)=WPATH(IK, 7)+WPATH(IK,12)+WPATH(IK,13)+WPATH(IK,14)&
     &              +WPATH(IK,68)+WPATH(IK,69)+WPATH(IK,70)+WPATH(IK,71)&
     &              +WPATH(IK,72)+WPATH(IK,73)+WPATH(IK,74)+WPATH(IK,75)&
     &              +WPATH(IK,76)+WPATH(IK,77)

!         COLUMN DENSITY [ATM-CM] TO DENSITY [MOLE/M3] CONVERSION
!           CONLAY = CONVRT / DIST(KM)
          CONLAY=CONVRT/DRNG(IK)
          DENH2O(IK)=WPATH(IK,17)*CONLAY*AMWT(1)
          DENCO2(IK)=WPATH(IK,36)*CONLAY*AMWT(2)
          DENO3(IK)=WPATH(IK,31)*CONLAY*AMWT(3)
   10 CONTINUE
      RETURN
      END
